; "V98ADVPL.ASM" 1989/3/21 R.H  PC-9801UV11 make by R.H 1989
;       modify 1989/12/08 R.H
;       modify 1990/10/27 R.H
;       modify 1992/04/09 R.H 486/33
;
; public subroutine.

INCLUDE	TSTSET.INC

IN_GDC	MACRO	AA,NUM
;same;	IN	AL,NUM
	PUSH	DX
	MOV	DX,NUM			;
	CALL	IN_7220_GDC_PORT	; (far) in  port 7220 GDC.
	POP	DX
	ENDM

OUTGDC	MACRO	NUM,AA
;same;	OUT	NUM,AL
	PUSH	DX
	MOV	DX,NUM			;
	CALL	OUT_7220_GDC_PORT	; (far) out port 7220 GDC.
	POP	DX
	ENDM


DIV8N	MACRO
	DIV	DIV8
	ENDM

DIV100N	MACRO
	LOCAL	L1
	DIV	DIV100
	ENDM

DIV100N2	MACRO
	LOCAL	L1
	DIV	DIV100
	CMP	DX,50
	JL	L1
	INC	AX
L1:
	ENDM

DIV6000N	MACRO
	LOCAL	L1
	DIV	DIV6000
	CMP	DX,30
	JL	L1
	INC	AX
L1:
	ENDM


CODE01	SEGMENT	PUBLIC	'CODE01'

	ASSUME	CS:CODE01,DS:DATA

PUBLIC	V98SETCLR		; color table set.

EXTRN	IN_7220_GDC_PORT:NEAR	; in  port 7220 GDC.
EXTRN	OUT_7220_GDC_PORT:NEAR	; out port 7220 GDC.


; color set.
; -i- CLRCDE : color code.
;     CLRVEW : color.
;     CLRCNS : contrast.
;     CLRBRI : brightness.

V98SETCLR	PROC	NEAR
;*DBG*;	PRV	"CLRCDE=",CLRCDE
;*DBG*;	PRV	"CLRVEW=",CLRVEW
;*DBG*;	PRV	"CLRCNS=",CLRCNS
;*DBG*;	PRV	"CLRBRI=",CLRBRI

	CALL	CHG2BRG			; chenge BRG.

	CMP	SW_ANALOG,0		; 0=normal analog
	JE	C1SETCLR_ANALOG		;
	CMP	SW_ANALOG,1		; 1=386LSX(15color)
	JE	C1SETCLR_386LSX		;
	CMP	SW_ANALOG,2		; 2=full DIGITAL
	JE	C1SETCLR_FULL_DIGITAL	;
	CMP	SW_ANALOG,3		; 3=B/W.
	JE	C1SETCLR_BW		;

C1SETCLR_ANALOG:
	CALL	C1CLRSET		; color register set.
	JMP	C1SETCLR_EXIT		;

C1SETCLR_386LSX:
	CALL	C1CLRSET_386LSX		; color register set for PC-386LSX
	JMP	C1SETCLR_EXIT		;

C1SETCLR_FULL_DIGITAL:
	CALL	C1CLRSET_FULL_DIGITAL	; color register set for full DIGITAL.
	JMP	C1SETCLR_EXIT		;

C1SETCLR_BW:				;
	CALL	C1CLRSET_BW		; color register set for B/W.
	JMP	C1SETCLR_EXIT		;

C1SETCLR_EXIT:
	RET
V98SETCLR	ENDP


;	chenge BRG.
;	-i- CLRCDE : color code.
;	    CLRVEW : color.
;	    CLRCNS : contrast.
;	    CLRBRI : brightness.
;	-O- CRNUM  : color code.
;	    CBLUE  : blue  value (0-15)
;	    CRED   : red   value (0-15)
;	    CGREEN : green value (0-15)

CHG2BRG	PROC	NEAR
	MOV	AX,UPCOLOR
	MOV	UPPER,AX

	MOV	AX,100
	SUB	AX,CLRCNS	; contrast.
	MUL	UPCOLOR
	DIV100N2
	MOV	UNDER,AX

	MOV	AX,CLRVEW	; color.
	CMP	AX,60
	JL	CHG2BRG_C1
	CMP	AX,120
	JL	CHG2BRG_C2
	CMP	AX,180
	JL	CHG2BRG_C3
	CMP	AX,240
	JL	CHG2BRG_C4
	CMP	AX,300
	JL	CHG2BRG_C5
	JMP	CHG2BRG_C6
CHG2BRG_C1:
	MOV	CX,CLRVEW	; color.
	JMP	CHG2BRG_5
CHG2BRG_C2:
	MOV	CX,120
	SUB	CX,CLRVEW	; color.
	JMP	CHG2BRG_5
CHG2BRG_C3:
	MOV	CX,CLRVEW	; color.
	SUB	CX,120
	JMP	CHG2BRG_5
CHG2BRG_C4:
	MOV	CX,240
	SUB	CX,CLRVEW	; color.
	JMP	CHG2BRG_5
CHG2BRG_C5:
	MOV	CX,CLRVEW	; color.
	SUB	CX,240
	JMP	CHG2BRG_5
CHG2BRG_C6:
	MOV	CX,360
	SUB	CX,CLRVEW	; color.
CHG2BRG_5:
	MOV	AX,CLRCNS	; contrast.
	MUL	CX
	MUL	UPCOLOR
	DIV6000N
	ADD	AX,UNDER
	MOV	SLIDE,AX

	MOV	AX,UPPER
	MUL	CLRBRI		; brightness.
	DIV100N
	MOV	UPPER,AX

	MOV	AX,UNDER
	MUL	CLRBRI		; brightness.
	DIV100N
	MOV	UNDER,AX

	MOV	AX,SLIDE
	MUL	CLRBRI		; brightness.
	DIV100N
	MOV	SLIDE,AX

	MOV	BX,UPPER
	MOV	CX,UNDER
	MOV	DX,SLIDE

	MOV	AX,CLRVEW	; color.
	CMP	AX,60
	JL	CHG2BRG_C1S
	CMP	AX,120
	JL	CHG2BRG_C2S
	CMP	AX,180
	JL	CHG2BRG_C3S
	CMP	AX,240
	JL	CHG2BRG_C4S
	CMP	AX,300
	JL	CHG2BRG_C5S
	JMP	CHG2BRG_C6S
CHG2BRG_C1S:
	MOV	CRED,BX
	MOV	CBLUE,CX
	MOV	CGREEN,DX
	JMP	CHG2BRG_SET
CHG2BRG_C2S:
	MOV	CGREEN,BX
	MOV	CBLUE,CX
	MOV	CRED,DX
	JMP	CHG2BRG_SET
CHG2BRG_C3S:
	MOV	CGREEN,BX
	MOV	CRED,CX
	MOV	CBLUE,DX
	JMP	CHG2BRG_SET
CHG2BRG_C4S:
	MOV	CBLUE,BX
	MOV	CRED,CX
	MOV	CGREEN,DX
	JMP	CHG2BRG_SET
CHG2BRG_C5S:
	MOV	CBLUE,BX
	MOV	CGREEN,CX
	MOV	CRED,DX
	JMP	CHG2BRG_SET
CHG2BRG_C6S:
	MOV	CRED,BX
	MOV	CGREEN,CX
	MOV	CBLUE,DX
	JMP	CHG2BRG_SET
CHG2BRG_SET:

	MOV	AX,CLRCDE		;
	MOV	CRNUM,AX		; CRNUM = CLRCDE

	MOV	DX,0
	MOV	AX,CBLUE	; blue  value.
	DIV8N
	MOV	CBLUE,AX	; blue  value.

	MOV	DX,0
	MOV	AX,CRED		; red   value (0-15)
	DIV8N
	MOV	CRED,AX		; red   value (0-15)

	MOV	DX,0
	MOV	AX,CGREEN	; green value (0-15)
	DIV8N
	MOV	CGREEN,AX	; green value (0-15)

;*;	PRV	"CBLUE =",CBLUE
;*;	PRV	"CRED  =",CRED
;*;	PRV	"CGREEN=",CGREEN
;*;	PR	"("
;*;	MOV	DX,CRNUM	; color code.
;*;	CALL	DECSTR
;*;	CALL	PRINT
;*;	PR	","
;*;	MOV	DX,CBLUE	; blue  value (0-15)
;*;	CALL	DECSTR
;*;	CALL	PRINT
;*;	PR	","
;*;	MOV	DX,CRED		; red   value (0-15)
;*;	CALL	DECSTR
;*;	CALL	PRINT
;*;	PR	","
;*;	MOV	DX,CGREEN	; green value (0-15)
;*;	CALL	DECSTR
;*;	CALL	PRINT
;*;	PR	")"
;*;	LF

CHG2BRG_EXIT:
	RET
CHG2BRG	ENDP


; color register set.
; -i- CRNUM  : color code.
;     CBLUE  : blue  value (0-15)
;     CRED   : red   value (0-15)
;     CGREEN : green value (0-15)

C1CLRSET	PROC	NEAR
;*DBG*;	PR	"NO,B,R,G="
;*DBG*;	MOV	DX,CRNUM	; color code.
;*DBG*;	CALL	DECSTR
;*DBG*;	CALL	PRINT
;*DBG*;	PR	","
;*DBG*;	MOV	DX,CBLUE	; blue  value (0-15)
;*DBG*;	CALL	DECSTR
;*DBG*;	CALL	PRINT
;*DBG*;	PR	","
;*DBG*;	MOV	DX,CRED		; red   value (0-15)
;*DBG*;	CALL	DECSTR
;*DBG*;	CALL	PRINT
;*DBG*;	PR	","
;*DBG*;	MOV	DX,CGREEN	; green value (0-15)
;*DBG*;	CALL	DECSTR
;*DBG*;	CALL	PRINT
;*DBG*;	LF
;*DBG*;
	MOV	AX,CRNUM	;
	OUTGDC	0A8H,AL		; AD REG

	MOV	AX,CGREEN	;
	CMP	AX,16		;
	JB	C1CLRSET1		;
	MOV	AX,15		;
C1CLRSET1:
	OUTGDC	0AAH,AL		; pallet CGREEN

	MOV	AX,CRED		;
	CMP	AX,16		;
	JB	C1CLRSET2		;
	MOV	AX,15		;
C1CLRSET2:
	OUTGDC	0ACH,AL		; pallet CRED

	MOV	AX,CBLUE	;
	CMP	AX,16		;
	JB	C1CLRSET3		;
	MOV	AX,15		;
C1CLRSET3:
	OUTGDC	0AEH,AL		; pallet CBLUE

	RET
C1CLRSET	ENDP




; color register set for PC-386LSX
; -i- CRNUM  : color code.
;     CBLUE  : blue  value (0-15)
;     CRED   : red   value (0-15)
;     CGREEN : green value (0-15)

C1CLRSET_386LSX	PROC	NEAR
	MOV	AX,CRNUM	;
	OUTGDC	0A8H,AL		; AD REG

	MOV	AX,CGREEN	;
	CMP	AX,8		;
	JB	C1CLRSET51	;
	CMP	AX,12		;
	JB	C1CLRSET510	;
	MOV	AX,15		;
	JMP	C1CLRSET511	;
C1CLRSET51:			;
	MOV	AX,0		;
	JMP	C1CLRSET511	;
C1CLRSET510:			;
	MOV	AX,9		;
C1CLRSET511:			;
	OUTGDC	0AAH,AL		; pallet CGREEN

	MOV	AX,CRED		;
	CMP	AX,8		;
	JB	C1CLRSET52	;
	CMP	AX,11		;
	JB	C1CLRSET520	;
	MOV	AX,15		;
	JMP	C1CLRSET521	;
C1CLRSET52:			;
	MOV	AX,0		;
	JMP	C1CLRSET521	;
C1CLRSET520:			;
	MOV	AX,0		;
C1CLRSET521:			;
	OUTGDC	0ACH,AL		; pallet CRED

	MOV	AX,CBLUE	;
	CMP	AX,8		;
	JB	C1CLRSET53	;
	CMP	AX,12		;
	JB	C1CLRSET530	;
	MOV	AX,15		;
	JMP	C1CLRSET531	;
C1CLRSET53:			;
	MOV	AX,0		;
	JMP	C1CLRSET531	;
C1CLRSET530:			;
	MOV	AX,0		;
C1CLRSET531:			;
	OUTGDC	0AEH,AL		; pallet CBLUE

	RET
C1CLRSET_386LSX	ENDP


; color register set for full DIGITAL.
; -i- CRNUM  : color code.
;     CBLUE  : blue  value (0-15)
;     CRED   : red   value (0-15)
;     CGREEN : green value (0-15)

C1CLRSET_FULL_DIGITAL	PROC	NEAR
	MOV	AX,CRNUM	;
	OUTGDC	0A8H,AL		; AD REG

	CMP	CRNUM,8		;
	JNE	C1CLRSET6_GRAY	;

	MOV	AL,9		;
	OUTGDC	0AEH,AL		; pallet CBLUE
	OUTGDC	0ACH,AL		; pallet CRED
	OUTGDC	0AAH,AL		; pallet CGREEN
	RET
C1CLRSET6_GRAY:

	MOV	CBLUE,0		;
	MOV	CRED,0		;
	MOV	CGREEN,0	;

	MOV	DX,15		; on color.
	TEST	CRNUM,1000B	;
	JNZ	C1CLRSET60	;
	MOV	DX,15		; harf color.
C1CLRSET60:


	TEST	CRNUM,0100B	; color code.
	JZ	C1CLRSET61	;
	MOV	CGREEN,DX	;
C1CLRSET61:
	MOV	AX,CGREEN	;
	OUTGDC	0AAH,AL		; pallet CGREEN


	TEST	CRNUM,0010B	; color code.
	JZ	C1CLRSET62	;
	MOV	CRED,DX		;
C1CLRSET62:
	MOV	AX,CRED		;
	OUTGDC	0ACH,AL		; pallet CRED


	TEST	CRNUM,0001B	; color code.
	JZ	C1CLRSET63	;
	MOV	CBLUE,DX	;
C1CLRSET63:
	MOV	AX,CBLUE	;
	OUTGDC	0AEH,AL		; pallet CBLUE

	RET
C1CLRSET_FULL_DIGITAL	ENDP


; color register set for B/W.
; -i- CLRCDE : color code.
;     CLRVEW : color.
;     CLRCNS : contrast.
;     CLRBRI : brightness.

C1CLRSET_BW	PROC	NEAR
	MOV	AX,CLRCDE	;
	OUTGDC	0A8H,AL		; AD REG

	MOV	AX,CLRBRI	; brightness.
	SUB	AX,11		;
	JGE	C1CLRSET_BW01	;
	MOV	AX,0		;
C1CLRSET_BW01:

	MOV	BX,13		; 0-100 -> 0-15
	MOV	DX,0		;
	DIV	BX		; / 12
	MOV	CX,AX		;

	CMP	CX,7		;
	JLE	C1CLRSET_BW1	;
	MOV	CX,7		;
C1CLRSET_BW1:

	CMP	CLRCNS,5		; contrast.
	JG	C1CLRSET_BW11		;		white.
	CMP	CLRBRI,97		; brightness.
	JL	C1CLRSET_BW11		;
	MOV	CX,7			;
	JMP	C1CLRSET_BW5		;
C1CLRSET_BW11:

	CMP	WORD PTR CLRVEW,50	;
	JL	C1CLRSET_BW12		; blue.
	CMP	WORD PTR CLRVEW,90	;		yellow.
	JG	C1CLRSET_BW12		;
	CMP	CLRBRI,80		; brightness.
	JL	C1CLRSET_BW12		;
	MOV	CX,6			;
	JMP	C1CLRSET_BW5		;
C1CLRSET_BW12:


	CMP	CLRCNS,40		; contrast.
	JL	C1CLRSET_BW_GRAY		;

	CMP	WORD PTR CLRVEW,180	;
	JL	C1CLRSET_BW2		; blue.
	CMP	WORD PTR CLRVEW,300	;
	JG	C1CLRSET_BW2		;
	CMP	CX,0			;
	JE	C1CLRSET_BW13		;
	SHR	CX,1			; /2
	SHR	CX,1			; /2
	ADD	CX,1			;
C1CLRSET_BW13:				;
	JMP	C1CLRSET_BW5		;
C1CLRSET_BW2:				;

	CMP	CX,0			;
	JE	C1CLRSET_BW44		;
	SHR	CX,1			; /2
;*;	ADD	CX,1			;
C1CLRSET_BW44:				;

	JMP	C1CLRSET_BW5		;

C1CLRSET_BW_GRAY:				;
	CMP	CX,0			;
	JE	C1CLRSET_BW5		;
	DEC	CX			;
	CMP	CX,1			;
	JGE	C1CLRSET_BW5		;
	MOV	CX,1			;

C1CLRSET_BW5:				;

	MOV	AL,0		;
	TEST	CX,0100B	; color code.
	JZ	C1CLRSET_BW61	;
	MOV	AL,15		;
C1CLRSET_BW61:			;
	OUTGDC	0AAH,AL		; pallet CGREEN

	MOV	AL,0		;
	TEST	CX,0010B	; color code.
	JZ	C1CLRSET_BW62	;
	MOV	AL,15		;
C1CLRSET_BW62:			;
	OUTGDC	0ACH,AL		; pallet CRED

	MOV	AL,0		;
	TEST	CX,0001B	; color code.
	JZ	C1CLRSET_BW63	;
	MOV	AL,15		;
C1CLRSET_BW63:			;
	OUTGDC	0AEH,AL		; pallet CBLUE

	RET
C1CLRSET_BW	ENDP


CODE01	ENDS




DATA	SEGMENT	PUBLIC	'DATA'

PUBLIC	SW_ANALOG	; analog switch. 0=normal analog,1=386LSX(15color)
			; 2=full DIGITAL,3=B/W

EXTRN	CLRCDE:WORD	; color code.
EXTRN	CLRVEW:WORD	; color view.
EXTRN	CLRCNS:WORD	; contrast.
EXTRN	CLRBRI:WORD	; brightness.

EXTRN	CRNUM:WORD	; color code.       
EXTRN	CBLUE:WORD	; blue  value (0-15)
EXTRN	CRED:WORD	; red   value (0-15)
EXTRN	CGREEN:WORD	; green value (0-15)

SW_ANALOG	DW	-1	; analog switch.
				;
UPPER		DW	0	;
UNDER		DW	0	;
SLIDE		DW	0	;

DIV8		DW	8	;
DIV100		DW	100	;
DIV6000		DW	6000	;
UPCOLOR		DW	07FH	;
;;UPCOLOR		DW	0FH	;

DATA	ENDS

	END
;
;	end of "V98ADVPL.ASM"
;
