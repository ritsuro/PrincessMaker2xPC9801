;"EVNT051.TXT"
;
;51 亡霊騎士出現
;【 概　要 】墓守のアルバイト中，騎士の亡霊が出現．戦いとなる．
;【 いつ？ 】墓守のアルバイト実行中．
;【発生条件】娘の戦闘技術が70以上のとき，墓守のバイトをする．
;			亡霊騎士と戦って勝てば，二度と発生しないが，
;			負ければ何度でも発生する．
;【 効　果 】墓守のバイトの最初に，墓守バランが騎士の幽霊の話しをする．
;			バイト終了時に，亡霊騎士が現れ，娘に戦いを挑む．
;
;	E_ETC[17]=1		「亡霊騎士」に勝つ
;

.SUCCESS_DAYS 		;成功日数
.WORK_DAYS	 		;仕事日数
.GUARA_ALL	 		;バイト料合計
.GUARA_ONE	 		;バイト料

;"JOB010.TXT"から引き継ぐ

SUCCESS_DAYS= 	C1 	;成功日数
WORK_DAYS	= 	C2 	;仕事日数
GUARA_ALL	= 	C3 	;バイト料合計
GUARA_ONE	= 	C4 	;バイト料

.FLAG_VICTORY		;勝利フラグ

.FLAG_BATLE = 0		;0=戦闘中,1=娘勝ち,2=敵勝ち,3=娘逃げる,4=敵逃げる
.G_DAMEG
.T_DAMEG
.REC_TBL[50]
.TOOL_BUKI

.ATTACK_VAL			;攻撃バリュー
.DEFENS_VAL			;防御バリュー
.BATLE_NOW			;1=命中,2=失敗

.MY_KAISHIN		;会心率(%)
.FLAG_KAISHIN=0	;会心ﾌﾗｸﾞ
.FLAG_TSUKON=0	;痛恨ﾌﾗｸﾞ
.BATL_COUNT	=0
.FLG_HABAM	=0

.R_HP		=0		;ＨＰ
.R_MP		=0		;ＭＰ

.R_FACE			=0	;フェイス
.R_MONSTOR		=0	;モンスター
.R_NAME			=0	;名前
.R_SENTOH		=0	;戦闘技術
.R_KOUGEKI		=0	;攻撃力
.R_BOUGYO		=0	;防御力
.R_SOUKOU		=0	;装甲
.R_SKNAME		=0	;装甲名
.R_MAHOGIJUTSU	=0	;魔法技術
.R_MARYOKU		=0	;魔力
.R_KOHMARYOKU	=0	;抗魔力
.R_SENI			=0	;戦意
.R_DASSYUTU		=0	;脱出率(%)
.R_TSUKON		=0	;痛恨率(%)
.R_MAX_HP		=0	;最大ＨＰ
.R_MAX_MP		=0	;最大ＭＰ
.R_GOLD			=0	;所持金

.R_SE_ATTACK	=0	;剣の当たる(1=皮,2=鉄,3=体毛,4=固い体)
.GIRL_SE_ATTACK	=0	;娘に当たる(1=皮,2=鉄,3=体毛)

.MNSTR_X	=	20
.MNSTR_Y	=	20

RUN_MONTH[51]++		;「亡霊騎士出現」実行回数

STRSCLEAR

TXLC(1,19,290,0)	;text window locate.
TXLC(2,17,148,1)	;text window locate.
TXLC(3,31,270,2)	;text window locate.
WWIVENT(5,0,6,40)	;ivent window locate.

STARTINIT			;start init.

EVNT051

JOB010				;墓守・後処理

PTBK(0)
LOAD("RUNNING")


;***************************************
;
;		墓守
;
;***************************************

*JOB010

RUN_ARBEIT[0]++					;「娘を働かせる」実行回数
IF ( SUCCESS_DAYS>0 )			; 成功日数
	RUN_ARBEIT[11]++			;「墓守」実行回数

PLAY(12)	;P12 ｱﾙﾊﾞｲﾄ

TXOPEN(2)
TXFACE("F069")

IF ( SUCCESS_DAYS = WORK_DAYS )		;バイト終了時（パーフェクト）
	P1X100(GUARA_ALL,50)
	GUARA_ALL += AX
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	@C
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊TXPOINT(SUCCESS_DAYS)＊＊TXGOLD(GUARA_ALL)＊」
	GOLDADD(GUARA_ALL)				; バイト料合計

IF ( SUCCESS_DAYS < WORK_DAYS   SUCCESS_DAYS > 0 );（失敗があった場合）
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	@C
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊TXPOINT(SUCCESS_DAYS)＊＊TXGOLD(GUARA_ALL)＊＊＊＊＊＊＊＊＊＊＊＊」
	GOLDADD(GUARA_ALL)				; バイト料合計

IF ( SUCCESS_DAYS = 0 )				;バイト終了時（全滅）
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
WWPROF(3)					;ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・所持金
@P
TXCLOSE(2)
RET

;************************************
;	亡霊騎士出現
;************************************

*EVNT051
;;WWANIME(8,0)		;restore vram ARA002 -> bank#6 move.

MUSIC(2)	;音楽ﾌｪｰﾄﾞｱｳﾄ

BLACK_DOWN

NIGHT_DATA			;亡霊騎士のデータ
CHECK_GIRL

PLAY(29)	;P29 緊迫

TXOPEN(2)
TXFACE("F177")		;亡霊騎士・普通	;（＝グラフィックは骸骨と兼用）
EFC(0)				;Ｆ・Ｉ（フェードイン）
「＊＊＊＊＊＊＊＊＊＊＊」
@P
TXOPEN(3)
TXGIRL(11)			;娘・不安
「TXA(NAME_FIRST)＊＊＊TXA(NAME_FIRST)＊TXA(NAME_FAMILY)＊＊＊＊＊＊」
@P
@2
@C
「＊＊＊＊＊＊＊＊＊＊＊＊TXA(NAME_FIRST)＊TXA(NAME_FAMILY)＊＊＊＊＊＊＊＊＊＊」
@P
@3
@C
;娘・不安
「＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
EFC(1)					;F･O
TXCLOSE(3)
TXCLOSE(2)
;;WWANIME(9,0)		: anime window close.(to bank#6)

MONSTOR_OPEN

BATLE_MODE			;戦闘モード

MONSTOR_CLOSE

RET

;**************
;	frame.
;**************

*BLACK_DOWN				;
EFC(1)					;F･O
GRPCLS					;graphics clear screen.
PALET_SETONLY(2,0)		;2=PM2_W.ALG	ＲＰＧ・西
RET

*MONSTOR_OPEN			;モンスターグラフィック表示
PTBK(4)					;パーツバンク設定
PTMD(0)					;ＰＵＴモード設定
SI=R_MONSTOR PTLD		;パーツロード
PTMF(MNSTR_X,MNSTR_Y,0)	;ﾏｽｸﾊﾟﾀｰﾝ座標ｵﾌｾｯﾄPUT
EFC(0)					; Ｆ・Ｉ（フェードイン）
RET

*MONSTOR_CLOSE
EFC(1)					;F･O
GRPCLS					;graphics clear screen.
WWFRAME(1)				;
CLENDER(1)				;
WWPROF(1)				;
WWGIRL(3)				;girl quick put.
PALET_SET(0)			;0=PM2_1.ALG	通常
RET

;**************
;	graph.
;**************

*GRAPSHOW
;GR_OPEN			;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN
GR_F(1,2)			;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN

SI = R_NAME
IGRP_X = 1
IGRP_Y = 248
GR_F(3)
C1 = R_SENTOH + R_KOUGEKI + R_BOUGYO			;戦士評価は《戦闘技術＋攻撃力＋防御力》
C2 = R_MAHOGIJUTSU + R_MARYOKU + R_KOHMARYOKU	;魔法評価は《魔法技術＋魔力＋抗魔力》
IGRP_TBL1[0]="ＨＰ"
IGRP_TBL1[1]="ＭＰ"
IGRP_TBL1[2]="戦士評価"
IGRP_TBL1[3]="魔法評価"
IGRP_TBL1[4]="戦意"
IGRP_TBL3[0]=R_HP			;ＨＰ
IGRP_TBL3[1]=R_MP			;ＭＰ
IGRP_TBL3[2]=C1				;戦闘評価
IGRP_TBL3[3]=C2				;魔法評価
IGRP_TBL3[4]=R_SENI			;戦意
IGRP_CNT=5
IGRP_X=1
IGRP_Y=286
GRPOPEN(1,2)		;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ

SI = NAME_FIRST
IGRP_X = 55
IGRP_Y = 248
GR_F(3)

IGRP_TBL1[0]="ＨＰ"
IGRP_TBL1[1]="ＭＰ"
IGRP_TBL1[2]="戦士評価"
IGRP_TBL1[3]="魔法評価"
IGRP_TBL1[4]="戦意"
IGRP_TBL3[0]=W_HP			;ＨＰ
IGRP_TBL3[1]=W_MP			;ＭＰ
IGRP_TBL3[2]=H_SENSHI		;戦士評価
IGRP_TBL3[3]=H_MAHO			;魔法評価
IGRP_TBL3[4]=W_SENI			;戦意
IGRP_CNT=5
IGRP_X=55
IGRP_Y=286
GRPOPEN(1,3)		;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
RET

*DISP_HP
GRPUPD(2,0,R_HP)
GRPUPD(2,1,R_MP)
GRPUPD(2,4,R_SENI)
GRPUPD(3,0,W_HP)
GRPUPD(3,1,W_MP)
GRPUPD(3,4,W_SENI)
RET

*RDM
;RDM(start,end)
;-o- AX : randmize
C1 = AX
C2 = BX - AX + 1
;*;RANDAM(C2)
IRAND999X(C2)		;乱数９９９範囲指定
AX = IRND + C1 - 1
RET


;**************
;	戦闘モード
;**************

*BATLE_MODE			;戦闘モード

PLAY(17)			;P17 戦闘

SLCPOS(2)
R_HP = R_MAX_HP		;最大ＨＰ
R_MP = R_MAX_MP		;最大ＭＰ
FLAG_VICTORY=0		;勝利フラグ
FLAG_BATLE = 0		;0=戦闘中
MY_KAISHIN = 3		;会心率(%)
IF ( E_KAISIN ! 0 )	;会心の一撃発生倍率２倍
	MY_KAISHIN = 6	;会心率(%)
BATL_COUNT = 0

GRAPSHOW

TXLC(7,26,284,0)	;text window locate.
TXOPEN(7)
戦闘開始
@P

TXF(11)			; 戦闘時ﾃｷｽﾄﾓｰﾄﾞ
BTL_LOOP
TXF(10)			; ﾉｰﾏﾙ時ﾃｷｽﾄﾓｰﾄﾞ

GR_CLOSE			;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ CLOSE

MUSIC(2)			;音楽ﾌｪｰﾄﾞｱｳﾄ

IF ( FLAG_BATLE=1 )		;1=娘勝ち
	TXA(NAME_FIRST)はTXA(R_NAME)を倒した．
	@P
	TXCLOSE(7)
	TXOPEN(2)
	TXFACE("F177")		;亡霊騎士・普通	;（＝グラフィックは骸骨と兼用）
	「＊＊＊＊＊＊TXA(NAME_FIRST)＊TXA(NAME_FAMILY)＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(2)
	TXOPEN(1)
	亡霊騎士は夜の闇に消えていった・・・
	@P
	TXCLOSE(1)
	E_ETC[17]=1			;「亡霊騎士」に勝つ
	FLAG_VICTORY=1		;勝利フラグ
	GIRL_VICTORY_CHK	;勝利チェック
	RET

IF ( FLAG_BATLE=2 )		;2=敵勝ち
	TXA(NAME_FIRST)は負けた．
	@P
	TXCLOSE(7)
	TXOPEN(2)
	TXFACE("F177")		;亡霊騎士・普通	;（＝グラフィックは骸骨と兼用）
	「＊＊＊＊＊＊＊＊＊＊TXA(NAME_FIRST)＊TXA(NAME_FAMILY)＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXOPEN(3)
	TXGIRL(6)			;娘・大怪我
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(2)
	TXCLOSE(3)
	TXOPEN(1)
	亡霊騎士は夜の闇に消えていった・・・
	@P
	TXCLOSE(1)
	RET

IF ( FLAG_BATLE=3 )		;3=娘逃げる
	なんとか逃げきったようだ．
	@P
	TXCLOSE(7)
	TXOPEN(3)
	TXGIRL(11)	;娘・不安
	「＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(3)
	RET
IF ( FLAG_BATLE=4 )		;4=敵逃げる
	;;TXA(R_NAME)は逃げた．
	TXA(R_NAME)は去っていった・・・
	@P
	TXCLOSE(7)
	RET
RET


;**************
;	勝利チェック
;**************

*GIRL_VICTORY_CHK	;勝利チェック
IF ( R_GOLD > 0 )
	TXOPEN(1)
	金TXGOLD(R_GOLD)を手に入れた．
	GOLDADD(R_GOLD)				;娘の所持金
	;;WWPROF(3)				;ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・所持金
	@P
	TXCLOSE(1)
RET

;**************
;	戦闘モード
;**************

*BTL_LOOP

ATTAKE_GIRL
DISP_HP
IF ( FLAG_BATLE ! 0 ) RET

@P
@C

ATTAKE_MONSTOR
DISP_HP
IF ( FLAG_BATLE ! 0 ) RET

BATL_COUNT++
IF ( BATL_COUNT>=30 )
	FLAG_BATLE=3		;3=娘逃げる
	;;IF ( W_HP >= R_HP )	FLAG_BATLE=4		;4=敵逃げる
	;;IF ( W_HP <  R_HP )	FLAG_BATLE=3		;3=娘逃げる
	RET

GOTO BTL_LOOP

*ATTAKE_GIRL
SLCTX("攻撃する,魔法を使う,道具を使う")
@C
IF ( ISLCT=1   W_MP < 10 )
	ＭＰが足りません．
	GOTO ATTAKE_GIRL
IF ( ISLCT=0 )
	GIRL_ATTAKE
	TEKI_KAISHIN_DMG
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(V_KOUGEKI,R_BOUGYO)
		TEKI_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		TEKI_DAMEG_NONE
	RET
IF ( ISLCT=1 )
	IF ( E_MAX_MP=0 )		;ＭＰが減らない
		W_MP -= 10
	GIRL_MAGIC
	TEKI_KAISHIN_DMG
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(V_MARYOKU,R_KOHMARYOKU)
		TEKI_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		TEKI_DAMEG_NONE
	RET
IF ( ISLCT=2 )
	USE_TOOL			;道具
	IF ( TOOL_BUKI=0 )	GOTO ATTAKE_GIRL	;0=NONE
RET

*ATTAKE_MONSTOR
;魔力を持っている敵は，《現在のＭＰ：現在のＨＰ》の比率で魔法
C1 = R_HP + R_MP		;
P100PAR(R_MP,C1)		;百分率を求める
C2 = AX					;parcent.
RANDAM(100)				;
IF ( C2 < IRND )( R_MP < 10 )
	TEKI_ATTAKE
	GIRL_TSUKON_DMG
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(R_KOUGEKI,V_BOUGYO)
		GIRL_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		GIRL_DAMEG_NONE
	RET
IF ( C2 >= IRND )
	R_MP -= 10
	TEKI_MAGIC
	GIRL_TSUKON_DMG
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(R_MARYOKU,V_KOHMARYOKU)
		GIRL_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		GIRL_DAMEG_NONE
	RET
RET

*GIRL_ATTAKE
TXA(NAME_FIRST)の攻撃！@@
PPP1
CALC_ATTACK(V_SENTOH)	ATTACK_VAL=AX
CALC_DEFENS(R_SENTOH)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*GIRL_MAGIC
TXA(NAME_FIRST)は呪文を唱えた．
SE_MAGIC
魔法攻撃をしかけた！！
PPP1
CALC_ATTACK(V_MAHOGIJUTSU)	ATTACK_VAL=AX
CALC_DEFENS(R_MAHOGIJUTSU)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*TEKI_ATTAKE
TXA(R_NAME)の攻撃！@@
PPP1
CALC_ATTACK(R_SENTOH)	ATTACK_VAL=AX
CALC_DEFENS(V_SENTOH)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*TEKI_MAGIC
TXA(R_NAME)は呪文を唱えた！
SE_MAGIC
魔法攻撃を受けた！！
PPP1
CALC_ATTACK(R_MAHOGIJUTSU)	ATTACK_VAL=AX
CALC_DEFENS(V_MAHOGIJUTSU)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*TEKI_KAISHIN_DMG
FLAG_KAISHIN=0			;会心ﾌﾗｸﾞ
RANDAM(100)
IF ( MY_KAISHIN >=IRND )	;会心率(%)
	FLAG_KAISHIN=1
RET

*TEKI_DAMEG
T_DAMEG = AX
IF ( FLAG_KAISHIN!0 )
	T_DAMEG = T_DAMEG * 3
	会心の一撃！
	PPP1
IF ( FLG_HABAM=0 )
	IF ( FLAG_KAISHIN=0 )	SE_TK_DAMEG
	IF ( FLAG_KAISHIN!0 )	SE_KAISHIN
	命中！TXA(R_NAME)にTXPOINT(T_DAMEG)のダメージを与えた！
IF ( FLG_HABAM!0 )
	IF ( R_SOUKOU > 0 )		;装甲
		SE_TK_DEFENS
		命中！しかし，TXA(R_NAME)のTXA(R_SKNAME)に阻まれた！
	IF ( R_SOUKOU = 0 )		;装甲
		SE_TK_DEFENS
		TXA(R_NAME)は攻撃を受けとめた！
	TXA(R_NAME)はTXPOINT(T_DAMEG)のダメージ．
PPP1
TEKI_DAMEG_CHK
RET

*TEKI_DAMEG_NONE
TXA(R_NAME)は攻撃を回避した！
PPP1
RET

*TEKI_DAMEG_CHK
R_HP -= T_DAMEG
IF ( R_HP <= 0 )
	R_HP = 0
	FLAG_BATLE=1	;1=娘勝ち
	RET
R_SENI -= T_DAMEG	;戦意
IF ( R_SENI <= 0 )
	R_SENI = 0
	RANDAM(30)
	IF ( IRND<=10 )
		FLAG_BATLE=4	;4=敵逃げる
RET

*GIRL_TSUKON_DMG
FLAG_TSUKON=0			;痛恨ﾌﾗｸﾞ
RANDAM(100)
IF ( R_TSUKON >=IRND )	;痛恨率(%)
	FLAG_TSUKON=1
RET

*GIRL_DAMEG
G_DAMEG = AX
IF ( FLAG_TSUKON!0 )		;痛恨ﾌﾗｸﾞ
	G_DAMEG = G_DAMEG * 3
	痛恨の一撃！
	PPP1
IF ( FLG_HABAM=0 )
	IF ( FLAG_TSUKON=0 )	SE_GR_DAMEG
	IF ( FLAG_TSUKON!0 )	SE_TSUKON
	命中！TXA(NAME_FIRST)はTXPOINT(G_DAMEG)のダメージを受けた！
IF ( FLG_HABAM!0 )
	IF ( EQUIP_ICON ! 0 )
		SE_GR_DEFENS
		命中！しかし，TXA(NAME_FIRST)のTXA(EQUIP_NAME)に阻まれた！
	IF ( EQUIP_ICON = 0 )
		SE_GR_DEFENS
		TXA(NAME_FIRST)は攻撃を受けとめた！
	TXA(NAME_FIRST)はTXPOINT(G_DAMEG)のダメージ．
PPP1
GIRL_DAMEG_CHK
RET

*GIRL_DAMEG_NONE
TXA(NAME_FIRST)は攻撃をかわした。
PPP1
RET

*GIRL_DAMEG_CHK
W_HP -= G_DAMEG		; ＨＰ
IF ( W_HP <= 0 )
	W_HP = 0
	FLAG_BATLE=2	;2=敵勝ち
	RET
W_SENI -= G_DAMEG	; 戦意
IF ( W_SENI <= 0 )
	W_SENI = 0
	RANDAM(30)
	IF ( IRND<=10 )
		TXA(NAME_FIRST)は一目散に逃げ出した．
		RANDAM(100)
		IF ( R_DASSYUTU>=IRND )		;脱出に成功した
			FLAG_BATLE=3	;3=娘逃げる
			RET
		しかし，敵に回り込まれてしまった．
		逃げられない！
RET

*USE_TOOL			; 道具
SLCT_TOOLS
IF (TOOL_BUKI=0) RET	;0=NONE
IF (TOOL_BUKI=1)		;1=快癒丸
	TXA(NAME_FIRST)は快癒丸を一粒飲んだ．
	IRAND2(20,60)		;20〜60まで
	C2 = AX
	C1 = W_HP_MAX - W_HP
	IF ( C1 < C2 ) C2 = C1
	W_HP = W_HP + C2
	ＨＰがTXPOINT(C2)回復した．
	PPP1
	RET
IF (TOOL_BUKI=2)		;2=焙碌
	TXA(NAME_FIRST)は手投げ焙碌を投げつけた．
	SE_HOUROKU
	ドッカーン！！
	RANDAM(80)			;1〜80のダメージ
	R_HP -= IRND
	IF ( R_HP<0 ) R_HP = 0
	TXA(R_NAME)にTXPOINT(IRND)のダメージを与えた！
	PPP1
	TEKI_DAMEG_CHK
	RET

*SLCT_TOOLS			; -O- TOOL_BUKI:0=NONE,1=快癒丸,2=焙碌
ITEM_SRCH(1,16)		;快癒丸
C2 = DX				;item record.
C1 = 0				;
IF ( AX = 1 )		;1=search
	ITEMID_TBL1[C1]=ITEM_NAME[C2]	;所有アイテム名前
	ITEMID_TBL2[C1]=ITEM_ICON[C2]	;ＩＣＯＮ
	ITEMID_TBL3[C1]=ITEM_GOLD[C2]	;金
	ITEMID_TBL4[C1]=ITEM_HELP[C2]	;所有アイテム説明
	ITEMID_TBL5[C1]=0
	ITEMID_TBL6[C1]=""
	ITEMID_TBL7[C1]=0
	REC_TBL[C1]=C2
	C1++
ITEM_SRCH(1,17)		;投げ焙碌（ﾎｳﾛｸ）
C2 = DX				;item record.
IF ( AX = 1 )		;1=search
	ITEMID_TBL1[C1]=ITEM_NAME[C2]	;所有アイテム名前
	ITEMID_TBL2[C1]=ITEM_ICON[C2]	;ＩＣＯＮ
	ITEMID_TBL3[C1]=ITEM_GOLD[C2]	;金
	ITEMID_TBL4[C1]=ITEM_HELP[C2]	;所有アイテム説明
	ITEMID_TBL5[C1]=0
	ITEMID_TBL6[C1]=""
	ITEMID_TBL7[C1]=0
	REC_TBL[C1]=C2
	C1++
ITEMID_CNT = C1			;アイテムＩＤ数
IF ( ITEMID_CNT=0 )
	TOOL_BUKI = 0
	戦闘に使用できる道具がありません
	PPP2
	RET
ITEMSLCT(1)				;マルチアイテム選択
IF ( ISITM = -1 )
	TOOL_BUKI = 0
	RET
IF ( ITEMID_TBL2[ISITM]=16 ) ;快癒丸
	TOOL_BUKI=1	;快癒丸
IF ( ITEMID_TBL2[ISITM]=17 ) ;投げ焙碌（ﾎｳﾛｸ）
	TOOL_BUKI=2	;焙碌
ITEM_KILL(REC_TBL[ISITM])	; アイテム削除
RET

*CALC_ATTACK	;-I-戦闘技術 -O- AX : 攻撃バリュー
;AX=AX+-30%randam
C1 = AX
IRNDPAR(C1,30)
RET

*CALC_DEFENS	;-I-戦闘技術 -O- AX : 攻撃バリュー
;AX=AX+-30%randam
C1 = AX
IRNDPAR(C1,30)
RET

*CALC_DAMEG		;-I-攻撃力,防御側の防御力 -O- AX : ダメージ
;AX = AX+-20%randam - BX
FLG_HABAM=0
C1 = AX
C2 = BX
IRNDPAR(C1,20)
AX = AX - C2
IF ( AX < 0 )
	FLG_HABAM=1
	AX = 1
RET

*PPP1
TIMER1(10)
WAIT1
RET

*PPP2
@P
RET

*SE_TK_DEFENS
IF(R_SE_ATTACK=1)MUSIC(6,7)	;1=皮
IF(R_SE_ATTACK=2)MUSIC(6,8)	;2=鉄
IF(R_SE_ATTACK=3)MUSIC(6,9)	;3=体毛
IF(R_SE_ATTACK=4)MUSIC(6,10);4=固い体
IF(R_SE_ATTACK=5)MUSIC(6,15);5=武神
RET

*SE_GR_DEFENS
IF(GIRL_SE_ATTACK=0)MUSIC(6,9)	;3=体毛
IF(GIRL_SE_ATTACK=1)MUSIC(6,7)	;1=皮
IF(GIRL_SE_ATTACK=2)MUSIC(6,8)	;2=鉄
IF(GIRL_SE_ATTACK=3)MUSIC(6,9)	;3=体毛
RET

*SE_MAGIC
MUSIC(6,4)
RET

*SE_TK_DAMEG
MUSIC(6,11)
RET

*SE_GR_DAMEG
MUSIC(6,12)
RET

*SE_KAISHIN
MUSIC(6,13)
RET

*SE_TSUKON
MUSIC(6,14)
RET

*SE_HOUROKU
MUSIC(6,16)
RET

*CHECK_GIRL
					;娘に当たる(1=皮,2=鉄,3=体毛)
IF ( EQUIP_ICON = 11 )	GIRL_SE_ATTACK=1		;11=皮の鎧
IF ( EQUIP_ICON = 12 )	GIRL_SE_ATTACK=2		;12=鎖帷子
IF ( EQUIP_ICON = 13 )	GIRL_SE_ATTACK=2		;13=鉄の鎧
IF ( EQUIP_ICON = 14 )	GIRL_SE_ATTACK=2		;14=ミスリルの鎧
IF ( EQUIP_ICON = 15 )	GIRL_SE_ATTACK=3		;15=シルクの鎧
RET

;**************
;	start init.
;**************

*STARTINIT
W_HP_MAX = B_TAIRYOKU - S_BYOUKI		; 最大ＨＰ 体力−病気指数
IF ( W_HP_MAX < 0 ) W_HP_MAX = 0
W_MP_MAX = B_CHINOH						; 最大ＭＰ 知能
W_SENI = H_SENSHI						; 戦士評価 -> 戦意
IF ( H_MAHO > H_SENSHI ) W_SENI= H_MAHO	; 魔法評価 -> 戦意
W_HP = W_HP_MAX
W_MP = W_MP_MAX
RET

;**************
;	亡霊騎士のデータ
;**************

*NIGHT_DATA
R_FACE			=	"F177"		;"骸骨"と同じ
R_MONSTOR		=	"M017"
R_NAME			=	"亡霊騎士"
R_SENTOH		=	80
R_KOUGEKI		=	75
R_BOUGYO		=	50
R_SOUKOU		=	0
R_SKNAME		=	""
R_MAHOGIJUTSU	=	85
R_MARYOKU		=	0
R_KOHMARYOKU	=	75
R_SENI			=	150
R_DASSYUTU		=	0
R_TSUKON		=	10
R_MAX_HP		=	250
R_MAX_MP		=	0
R_GOLD			=	2539
RET

;
;	end of "EVNT051.TXT"
;
