; "JOB010.TXT"
;
;
;
;	イベント	51 亡霊騎士出現		-> LOAD("EVNT051")
;	【発生条件】娘の戦闘技術が70以上のとき，墓守のバイトをする．
;				亡霊騎士と戦って勝てば，二度と発生しないが，
;				負ければ何度でも発生する．
;	E_ETC[17]	;1=「亡霊騎士」に勝つ
;

.S_STARTDAY		; スケジュール開始日
.S_ENDDAY		; スケジュール終了日

.GG_MAX
.GG_STR[6]
.GG_VAL[6]
.GG_GOLD_HDL

.DAY_COUNTER	=0	; 日付カウンタ
.NOW_COUNT		=0	; 実行回数

.FLAG_UP		=0	; 待遇アップフラグ
.GUARA_BASE		=0	; 基本給
.GUARA_ONE		=0	; バイト料
.GUARA_ALL		=0	; バイト料合計
.WORK_DAYS		=0	; 仕事日数
.SUCCESS_DAYS	=0	; 成功日数
.SUCCESS_LINE	=0	; 成功基準値
.SUCCESS_LINE2	=0	; 成功基準値
.SUCCESS_FLAG	=0	; 成功フラグ

.FLAG_ALLOPEN=0

.A_TEXT_HDL=6		;アニメ中テキストハンドル

.SLCANM
.TIMELOP
.TIMELOPMAX
.TIMEWAIT1
.GIRX
.GIRY
.ALOCX[12]
.ALOCY[12]
.ALOCC[12]
.ALOCF[12]
.ALOCCNT[12]
.AFWORK=0
.AFWORKCNT=0
.AFMISS=0
.AFMISSCNT=0
.TITLES="墓守"

.FLAG_EVE051=0		;イベント５１発生中

.FLAG_FIRE_ANM=0
.FIRE_COUNT1=0
.FIRE_COUNT2=0
.FLAG_BOUREIKISI=0

ANIME_INIT

S_STARTDAY	= SCH_STARTDAY[SCH_NOW]		; スケジュール開始日
S_ENDDAY	= SCH_ENDDAY[SCH_NOW]		; スケジュール終了日

SCH_NOW++		; スケジュール実行中項目

R_CEMETERY		; 10=墓守

LOAD("RUNNING")


;***************************************
;
;		墓守
;
;***************************************

*R_CEMETERY		; 10=墓守

TXOPEN(1)
TXA(NAME_FIRST)は今日から，
墓守のアルバイトです．
@P
TXCLOSE(1)

TXOPEN(2)
TXFACE("F069")

GUARA_BASE	=8						; 基本給
GUARA_ALL	=0						; バイト料合計
WORK_DAYS	=0						; 仕事日数
SUCCESS_DAYS=0						; 成功日数

CK_GUARA(RUN_ARBEIT[11],GUARA_BASE)	; バイト料
FLAG_UP		= AX					; 待遇アップ
GUARA_ONE	= DX					; バイト料

墓守バラン

IF ( S_HIKOUKA >= 60 )
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(2)	
	TXOPEN(1)
	TXA(NAME_FIRST)はアルバイトを断られてしまった・・・
	@P
	TXCLOSE(1)

	DAY_COUNTER	= S_ENDDAY - S_STARTDAY 	; 日付カウンタ
		TIMER1(10)
		WAIT1
		CLENDER(3)					; カレンダー・更新処理
		CLENDER(2)					; カレンダー・再表示
	LOOP DAY_COUNTER
	RET

RUN_ARBEIT[27]++	;「墓守」訪問回数

IF ( GRA_ARBEIT[11] = FLAG_UP )		; 待遇

	IF ( RUN_ARBEIT[27] = 1 )		;１回目
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	IF ( RUN_ARBEIT[27] > 1 )		;２回目以降
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」

IF ( GRA_ARBEIT[11] ! FLAG_UP )		; 待遇

	GRA_ARBEIT[11] = FLAG_UP		; 待遇

	IF ( FLAG_UP = 1 )				; 待遇アップ１回目
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊TXGOLD(GUARA_ONE)＊」

	IF ( FLAG_UP ! 1 )				; 待遇アップ２回目以降
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊TXGOLD(GUARA_ONE)＊」

@P
TXCLOSE(2)

IF ( V_SENTOH >= 70 )		;戦闘技術が70以上のとき
	IF ( E_ETC[17] = 0 )	;1=「亡霊騎士」に勝つ
		FLAG_EVE051=1		;イベント５１発生中
		EVENT051_START		;亡霊騎士出現／墓守バイトの最初

ALL_OPEN
DAY_COUNTER	= S_ENDDAY - S_STARTDAY + 1	; 日付カウンタ

	TXF(11)

	IF ( VAL_WEEK!0 )					; Sunday

		RANDAM(2) AX--
		T_KOHMARYOKU += AX				; 抗魔力

		B_KANJUSEI++					; 感受性
		B_IROKE--						; 色気
		IF ( B_IROKE < 0 ) B_IROKE=0	; 色気
		B_STRESS += 5					; ストレス

		CLENDER(13)	;訓練技能値計算

		WORK_DAYS++						; 仕事日数
		TXCLR TXA(TITLES)・TXPOINT(WORK_DAYS)日目

		IRAND999X(B_STRESS)				; ストレス
		SUCCESS_LINE= B_SHINKOH - IRND	; 信仰
		IRAND999X(B_STRESS)				; ストレス
		SUCCESS_LINE2= B_TAIRYOKU - IRND	; 体力

		CHK_RENAI
		CHK_HIKOUKA

		IF ( SUCCESS_LINE >= 10  SUCCESS_LINE2 >= 10 )	; 信仰１０ 体力１０ 成功
			今日の墓場は平穏無事だった・・・
			SUCCESS_DAYS++				; 成功日数
			GUARA_ALL += GUARA_ONE		; バイト料合計
			GXXDGOLD					;
			SUCCESS_FLAG = 1			; 成功フラグ
			ANIME_WIDW					;

		IF ( SUCCESS_LINE < 10 )( SUCCESS_LINE2 < 10 )	; 信仰１０ 体力１０ 失敗
			IF ( S_HIKOUKA < 10 )
				今日は墓場で騒ぎがあった．
			IF ( S_HIKOUKA >= 10 )
				TXA(NAME_FIRST)は仕事をサボっている・・・
			SUCCESS_FLAG = 0			; 成功フラグ
			ANIME_WIDW					;

		GXXD(0,V_KOHMARYOKU)		; 抗魔力
		GXXD(1,B_KANJUSEI)			; 感受性
		GXXD(2,B_IROKE)				; 色気
		GXXD(3,B_STRESS)			; ストレス

	IF ( VAL_WEEK=0 )				; Sunday
		@C
		今日は日曜日です．
		ANMTSUNDAY

	TXF(10)

	IF ( DAY_COUNTER ! 1 )
		BORTH_CHECK

	IF ( DAY_COUNTER = 1 )
		IF ( VAL_WEEK=0 )( FLAG_EVE051=0 )		; Sunday | イベント５１発生無し
			BORTH_CHECK

	IF ( DAY_COUNTER ! 1 )
		CLENDER(3)					; カレンダー・更新処理
		CLENDER(2)					; カレンダー・再表示
LOOP DAY_COUNTER
IF ( VAL_WEEK!0 )					; Sunday
	IF ( FLAG_EVE051 ! 0 )			;イベント５１発生中
		ANMT_MOOOON
		EVENT051_GO					;亡霊騎士出現／墓守バイトの終了時
ALL_CLOSE

RUN_ARBEIT[0]++						;「娘を働かせる」実行回数
IF ( SUCCESS_DAYS>0 )				; 成功日数
	RUN_ARBEIT[11]++				;「墓守」実行回数

TXOPEN(2)
TXFACE("F069")

IF ( SUCCESS_DAYS = WORK_DAYS )		; バイト終了時（パーフェクト）
	P1X100(GUARA_ALL,50)
	GUARA_ALL += AX
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	@C
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊TXPOINT(SUCCESS_DAYS)＊＊TXGOLD(GUARA_ALL)＊」
	GOLDADD(GUARA_ALL)				; バイト料合計

IF ( SUCCESS_DAYS < WORK_DAYS   SUCCESS_DAYS > 0 );（失敗があった場合）
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	@C
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊TXPOINT(SUCCESS_DAYS)＊＊TXGOLD(GUARA_ALL)＊＊＊＊＊＊＊＊＊＊＊＊」
	GOLDADD(GUARA_ALL)				; バイト料合計

IF ( SUCCESS_DAYS = 0 )				; バイト終了時（全滅）
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
WWPROF(3)					; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・所持金
@P
TXCLOSE(2)

RET

*ALL_OPEN
ANIME_OPEN
STRSCLEAR
GG_STR[0]="抗魔力"
GG_VAL[0]=V_KOHMARYOKU
GG_STR[1]="感受性"
GG_VAL[1]=B_KANJUSEI
GG_STR[2]="色気"
GG_VAL[2]=B_IROKE
GG_STR[3]="ストレス"
GG_VAL[3]=B_STRESS
GG_MAX=4
GXXSTART
FLAG_ALLOPEN=1
RET

*ALL_CLOSE
IF ( FLAG_ALLOPEN=1 )
	FLAG_ALLOPEN=0
	GR_CLOSE						; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ CLOSE
	ANIME_CLOSE
RET

; ******************
;	lib.
; ******************

*GXXSTART
GR_F(1,16)			; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN  #16	13,000byte
C3 = 0
C2 = 0
IGRP_X=4
IGRP_Y=340
IGRP_CNT=0
C1 = GG_MAX
	IGRP_TBL1[IGRP_CNT]=GG_STR[C2]
	IGRP_TBL3[IGRP_CNT]=GG_VAL[C2]
	IGRP_CNT++ C2++
	IF ( IGRP_CNT = 2 )
		C3++ GRPOPEN(1,C3)		; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
		IGRP_CNT = 0
		IGRP_X += 24
LOOP C1
IF ( IGRP_CNT ! 0 )
	C3++ GRPOPEN(1,C3)		; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
IGRP_TBL1[0]="もうけ"
IGRP_TBL3[0]=0
IGRP_CNT=1
IGRP_X=4
IGRP_Y=140
C3++ GOLDOPEN(1,C3)			; ﾊﾟﾗﾒｰﾀ 所持金ｳｲﾝﾄﾞｳ
GG_GOLD_HDL=C3
RET

*GXXD
C3 = AX
C4 = BX
MOD( C3, 2 )	C5 = AX		; 余り
C6 = C3 / 2 + 1				; handle.
GRPUPD(C6,C5,C4)			;
RET

*GXXDGOLD
GOLDUPD(GG_GOLD_HDL,0,GUARA_ALL)
RET

; ******************
;	Txxx
; ******************

*ANIME_INIT
TIMEWAIT1=8		;8;6
TIMELOPMAX=5	;5;12

;仕事中１
RANDAM(16)			;160(20) - 280(35)
ALOCX[0]	= AX + 19
ALOCY[0]	=113-79
ALOCC[0]	=0
ALOCCNT[0]	=2
.FILM0[2]
;仕事中２
ALOCX[1]	=104/8
ALOCY[1]	=113-79
ALOCC[1]	=0
ALOCCNT[1]	=15
.FILM1[15]=1,2,3,4,5,6,7,8,9,10,9 ,1,2,3,4

;仕事ミス・病気１
ALOCX[2]	= ALOCX[0]
ALOCY[2]	=113-79
ALOCC[2]	=0
ALOCCNT[2]	=2
.FILM2[2]=17,18
;仕事ミス・病気２
ALOCX[3]	=104/8
ALOCY[3]	=113-79
ALOCC[3]	=0
ALOCCNT[3]	=4
.FILM3[4]=11,11,12,12

;サボリ
ALOCX[4]	=0/8
ALOCY[4]	=113-79
ALOCC[4]	=0
ALOCCNT[4]	=2
.FILM4[2]=19,20

;お化け
ALOCX[5]	=248/8
ALOCY[5]	=0				;<=====
ALOCC[5]	=0
ALOCCNT[5]	=16
.FILM5[16]=27,28,29,30,31, 32,33,34,35,36, 37,38,39,40,41, 42

;ローソク
ALOCX[6]	=72/8
ALOCY[6]	=127-79
ALOCC[6]	=0
ALOCCNT[6]	=2
.FILM6[2]=49,50

;ヒトダマ
RANDAM(18)			;168(21) - 304(38)
ALOCX[7]	= AX + 20
ALOCY[7]	=0				;<=====
ALOCC[7]	=0
ALOCCNT[7]	=3
.FILM7[3]

;墓守(仕事１)
ALOCX[8]	=104/8
ALOCY[8]	=113-79
ALOCC[8]	=0
ALOCCNT[8]	=13
.FILM8[13]=21,21,22,22, 21,21,21,21,21, 21,21, 22,22

;墓守(仕事２)
ALOCX[9]	=8/8
ALOCY[9]	=113-79
ALOCC[9]	=0
ALOCCNT[9]	=12
.FILM9[12]=23,24,25,26,  23,24,25,26,  23,24,  23,24

;亡霊騎士
ALOCX[10]	=184/8
ALOCY[10]	=79-79
ALOCC[10]	=0
ALOCCNT[10]	=15
.FILM10[15]=51,51, 52,52, 53,53, 54,54, 55,55, 55,55,55,55,55

RANDAM(2)
IF ( IRND=1 )
	WORKS1_LEFT
IF ( IRND=2 )
	WORKS1_RIGHT

RANDAM(2)
IF ( IRND=1 )
	HITODAMA_LEFT
IF ( IRND=2 )
	HITODAMA_RIGHT
RET


*WORKS1_RIGHT
FILM0[0]=13
FILM0[1]=14
ALOCF[0] = 0
RET

*WORKS1_LEFT
FILM0[0]=15
FILM0[1]=16
ALOCF[0] = 1
RET

*HITODAMA_RIGHT
FILM7[0]=46
FILM7[1]=47
FILM7[2]=48
ALOCF[7] = 0
RET

*HITODAMA_LEFT
FILM7[0]=43
FILM7[1]=44
FILM7[2]=45
ALOCF[7] = 1
RET

*ANIME_OPEN
WWANIME(1,0)			; anime window open.
WWANIME(3,0,"J011B")	; pictuer load.
WWANIME(6,0,"J011A")	; anime pattern add load.
WWANIME(4,0)			; put BG.
WWANIME(5,0)			; pictuer copy vvram -> CRT.
TXOPEN(A_TEXT_HDL)
RET

*ANIME_CLOSE
WWANIME(2,0)			; anime window close.
TXCLOSE(A_TEXT_HDL)
RET

*ANIME_WIDW
IF ( AFMISSCNT = 0 )
	RANDAM(2)
	AFMISS = IRND - 1
	IF ( SLCANM = 0 )( SLCANM = 1 )
		AFMISS = AFWORK
	IF ( AFMISS=0 )	AFMISSCNT = 3
	IF ( AFMISS=1 )	AFMISSCNT = 1
IF ( AFWORKCNT = 0 )
	RANDAM(2)
	AFWORK = IRND - 1
	AFWORKCNT = 2
AFWORKCNT--
IF ( SUCCESS_FLAG ! 0 )			; 成功フラグ
	AFMISSCNT = 0
	IF ( AFWORK=0 ) ANMT001(0)	; 0=仕事中１,1=仕事中２
	IF ( AFWORK=1 ) ANMT001(1)	; 0=仕事中１,1=仕事中２
	GOTO ANIME_WIDW_SHOW
IF ( S_HIKOUKA >= 10 )		; 失敗時（非行化指数１０以上）
	ANMT001(4)				; 4=サボリ
	GOTO ANIME_WIDW_SHOW
AFMISSCNT--
IF ( AFMISS=0 ) ANMT001(2)	; 2=仕事ミス１,3=仕事ミス２
IF ( AFMISS=1 ) ANMT001(3)	; 2=仕事ミス１,3=仕事ミス２
*ANIME_WIDW_SHOW
RET


*ANMT001
; AX :	0=仕事中１,1=仕事中２,2=仕事ミス１,3=仕事ミス２,4=サボリ
SLCANM = AX

FIRE_COUNT1++

IF ( FIRE_COUNT1 =3 )
	FLAG_FIRE_ANM =1

TIMELOP=TIMELOPMAX
	WWANIME(4,0)		; put BG.
	TIMER1(TIMEWAIT1)

	ANIM_NUM(6)	APUT(ALOCX[6],ALOCY[6],DX)		;ローソク
	ALOCC[6]++

	IF ( FLAG_FIRE_ANM ! 0 )
		ANIM_NUM(7)	APUT(ALOCX[7],ALOCY[7],DX)		;ヒトダマ
		ALOCC[7]++

	IF ( SLCANM = 0 )( SLCANM = 2 )( SLCANM = 4 )
		ANIM_NUM(8)	APUT(ALOCX[8],ALOCY[8],DX)	ALOCC[8]++
	IF ( SLCANM = 1 )( SLCANM = 3 )
		ANIM_NUM(9)	APUT(ALOCX[9],ALOCY[9],DX)	ALOCC[9]++

	ANIM_NUM(SLCANM)	APUT(ALOCX[SLCANM],ALOCY[SLCANM],DX)
	ALOCC[SLCANM]++

	IF ( SLCANM = 2 )
		ANIM_NUM(5)	APUT(ALOCX[5],ALOCY[5],DX)		;オバケ
		ALOCC[5]++

	IF ( SLCANM = 0 )				;0=仕事中１
		IF ( ALOCF[0] = 0 )
			IF ( ALOCX[0]>=35 )		;280/8
				WORKS1_LEFT
			IF ( ALOCX[0]<35 )		;280/8
				ALOCX[0]++
				ALOCX[2]++
		IF ( ALOCF[0] = 1 )
			IF ( ALOCX[0]<=20 )		;160/8
				WORKS1_RIGHT
			IF ( ALOCX[0]>20 )		;160/8
				ALOCX[0]--
				ALOCX[2]--

	FIRE_ANM

	WAIT1
	WWANIME(5,0)		; pictuer copy vvram -> CRT.
LOOP TIMELOP
RET

*FIRE_ANM
	IF ( FLAG_FIRE_ANM ! 0 )
		IF ( ALOCF[7] = 0 )
			IF ( ALOCX[7]>=38 )		;304/8
				HITODAMA_LEFT
			IF ( ALOCX[7]<38 )		;304/8
				ALOCX[7]++
		IF ( ALOCF[7] = 1 )
			IF ( ALOCX[7]<=21 )		;168/8
				HITODAMA_RIGHT
				FIRE_COUNT2++
				IF ( FIRE_COUNT2 = 2 ) FLAG_FIRE_ANM = 0
			IF ( ALOCX[7]>21 )		;168/8
				ALOCX[7]--
RET

*ANMT_MOOOON
; AX :	0=仕事中１,1=仕事中２,2=仕事ミス１,3=仕事ミス２,4=サボリ
SLCANM = AX
TIMELOP=40+5

	WWANIME(4,0)		; put BG.

	TIMER1(TIMEWAIT1)

	ANIM_NUM(6)	APUT(ALOCX[6],ALOCY[6],DX)		;ローソク
	ALOCC[6]++
	ANIM_NUM(8)	APUT(ALOCX[8],ALOCY[8],DX)
	ALOCC[8]++
	IF ( FLAG_FIRE_ANM ! 0 )
		ANIM_NUM(7)	APUT(ALOCX[7],ALOCY[7],DX)		;ヒトダマ
		ALOCC[7]++

	IF ( FLAG_BOUREIKISI = 0 )
		ANIM_NUM(0)	APUT(ALOCX[0],ALOCY[0],DX)
		ALOCC[0]++

	IF ( FLAG_BOUREIKISI ! 0 )
		ANIM_NUM(10)	APUT(ALOCX[10],ALOCY[10],DX)		;亡霊騎士
		ALOCC[10]++
		ANIM_NUM(0)		APUT(ALOCX[0],ALOCY[0],DX)

	IF ( ALOCF[0]=0  ALOCX[0]=19 FLAG_BOUREIKISI=0 )			;152/8
		FLAG_BOUREIKISI = 1
		TIMELOP=16
	IF ( ALOCF[0]=1  ALOCX[0]=27 FLAG_BOUREIKISI=0 )			;216/8
		FLAG_BOUREIKISI = 1
		TIMELOP=16

	IF ( FLAG_BOUREIKISI = 0 )
		IF ( ALOCF[0] = 0 )
			IF ( ALOCX[0]>=35 )		;280/8
				WORKS1_LEFT
			IF ( ALOCX[0]<35 )		;280/8
				ALOCX[0]++
				ALOCX[2]++
		IF ( ALOCF[0] = 1 )
			IF ( ALOCX[0]<=20 )		;160/8
				WORKS1_RIGHT
			IF ( ALOCX[0]>20 )		;160/8
				ALOCX[0]--
				ALOCX[2]--

	IF ( FLAG_FIRE_ANM ! 0 )
		IF ( ALOCF[7] = 0 )
			IF ( ALOCX[7]>=38 )		;304/8
				HITODAMA_LEFT
			IF ( ALOCX[7]<38 )		;304/8
				ALOCX[7]++
		IF ( ALOCF[7] = 1 )
			IF ( ALOCX[7]<=21 )		;168/8
				HITODAMA_RIGHT
				FIRE_COUNT2++
				IF ( FIRE_COUNT2 = 2 ) FLAG_FIRE_ANM = 0
			IF ( ALOCX[7]>21 )		;168/8
				ALOCX[7]--
	WAIT1
	WWANIME(5,0)		; pictuer copy vvram -> CRT.
LOOP TIMELOP
RET

*ANMTSUNDAY
TIMELOP=TIMELOPMAX
	WWANIME(4,0)		; put BG.
	TIMER1(TIMEWAIT1)
	IF ( FLAG_FIRE_ANM ! 0 )
		ANIM_NUM(7)	APUT(ALOCX[7],ALOCY[7],DX)		;ヒトダマ
		ALOCC[7]++
		FIRE_ANM
	WAIT1
	WWANIME(5,0)		; pictuer copy vvram -> CRT.
LOOP TIMELOP
RET

*ANIM_NUM
;ANIM_NUM(filmnumber)
;-O- DX pattern number.
IF ( ALOCC[AX] >= ALOCCNT[AX] )
	ALOCC[AX] = 0
BX = ALOCC[AX]
IF (AX=0) DX=FILM0[BX]
IF (AX=1) DX=FILM1[BX]
IF (AX=2) DX=FILM2[BX]
IF (AX=3) DX=FILM3[BX]
IF (AX=4) DX=FILM4[BX]
IF (AX=5) DX=FILM5[BX]
IF (AX=6) DX=FILM6[BX]
IF (AX=7) DX=FILM7[BX]
IF (AX=8) DX=FILM8[BX]
IF (AX=9) DX=FILM9[BX]
IF (AX=10) DX=FILM10[BX]
DX--	; DX pattern number.
RET

*APUT
; anime pattern put vvram.
; APUT(x,y,pattern)
GIRX=AX
GIRY=BX
SI = CX * 2 + 1
DX = BX
CX = AX
AX = 7
BX = 0
;	WWANIME(7,0,ALOCX[0],ALOCY[0],ALOCC[0])
WWANIME	; anime pattern put vvram.
RET


;************************************
;	誕生日チェック
;************************************

*BORTH_CHECK

IF ( P_BORTHMONTH = VAL_MONTH  P_BORTHDAY = VAL_DAY )			; 娘の誕生日
	IF ( P_BORTHMONTH[1] = VAL_MONTH  P_BORTHDAY[1] = VAL_DAY )	; 父の誕生日
		E_ETC[14]=P_NENREI[1]			;「父の誕生日」をチェックした年齢
		IF ( URA_FATHER >=60 )			;父との関係値が６０以上
			ALL_CLOSE
			EVE4243						;娘の誕生日と父の誕生日の合体
			IF ( DAY_COUNTER ! 1 )
				ALL_OPEN
			RET

IF ( P_BORTHMONTH = VAL_MONTH )			; 誕生月
	IF ( P_BORTHDAY = VAL_DAY )			; 誕生日
		ALL_CLOSE
		EVENT042						; 42 娘の誕生日
		IF ( DAY_COUNTER ! 1 )
			ALL_OPEN
		RET

IF ( P_BORTHMONTH[1] = VAL_MONTH )		; 父の誕生月
	IF ( P_BORTHDAY[1] = VAL_DAY )		; 父の誕生日
		E_ETC[14]=P_NENREI[1]			;「父の誕生日」をチェックした年齢
		IF ( URA_FATHER >=60 )			;父との関係値が６０以上
			ALL_CLOSE
			EVENT043					; 43 父の誕生日
			IF ( DAY_COUNTER ! 1 )
				ALL_OPEN
			RET
RET

;************************************
;	51 亡霊騎士出現／墓守バイトの終了時
;************************************

*EVENT051_GO
;＊バイトのアニメ舞台に亡霊騎士が現れる
@C
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
TXOPEN(3)
TXGIRL(15)			;娘・驚き
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
;;TXCLOSE(3)
;;GR_CLOSE						; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ CLOSE
;;TXCLOSE(A_TEXT_HDL)
;"EVNT051.TXT"へ引き継ぐ

C1 = SUCCESS_DAYS		; 成功日数
C2 = WORK_DAYS			; 仕事日数
C3 = GUARA_ALL			; バイト料合計
C4 = GUARA_ONE			; バイト料

LOAD("EVNT051")			; 51 亡霊騎士出現

;************************************
;	51 亡霊騎士出現／墓守バイトの最初
;************************************

*EVENT051_START

SHORTWAIT

TXOPEN(2)
TXFACE("F069")		;墓守バラン・普通
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
TXOPEN(3)
TXGIRL(11)			;娘・不安
「＊＊＊＊＊＊」
@P
@2
@C
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
@3
@C
TXGIRL(16)			;娘・ガ〜ン
「＊＊＊＊＊」
@P
@2
@C
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
@3
@C
TXGIRL(11)			;娘・不安
「＊＊＊＊＊＊＊＊＊＊＊＊」
@P
TXCLOSE(2)
TXCLOSE(3)
RET

*SHORTWAIT
TIMER1(20)WAIT1
RET

*CHK_RENAI
IF ( FLAG_RENAI!0 )		;状態・恋愛
	RANDAM(100)			;２０％の確率
	IF ( IRND<=20 )
		SUCCESS_LINE=0
RET

*CHK_HIKOUKA
IF ( S_HIKOUKA >= 10 S_HIKOUKA < 30 )	;状態・やや反抗的
	RANDAM(100)							;４０％の確率
	IF ( IRND<=40 )
		SUCCESS_LINE=0
	RET

IF ( S_HIKOUKA >= 30 )			;状態・反抗的
	RANDAM(100)					;８０％の確率
	IF ( IRND<=80 )
		SUCCESS_LINE=0
	RET
RET
;
;	end of "JOB010.TXT"
;
