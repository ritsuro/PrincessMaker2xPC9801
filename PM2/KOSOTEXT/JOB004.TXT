; "JOB004.TXT"

.S_STARTDAY		; スケジュール開始日
.S_ENDDAY		; スケジュール終了日

.GG_MAX
.GG_STR[6]
.GG_VAL[6]
.GG_GOLD_HDL

.DAY_COUNTER	=0	; 日付カウンタ
.NOW_COUNT		=0	; 実行回数

.FLAG_UP		=0	; 待遇アップフラグ
.GUARA_BASE		=0	; 基本給
.GUARA_ONE		=0	; バイト料
.GUARA_ALL		=0	; バイト料合計
.WORK_DAYS		=0	; 仕事日数
.SUCCESS_DAYS	=0	; 成功日数
.SUCCESS_LINE	=0	; 成功基準値
.SUCCESS_FLAG	=0	; 成功フラグ

.FLAG_ALLOPEN=0

.A_TEXT_HDL=6		;アニメ中テキストハンドル

.SLCANM
.TIMELOP
.TIMELOPMAX
.TIMEWAIT1
.GIRX
.GIRY
.ALOCX[10]
.ALOCY[10]
.ALOCC[10]
.ALOCF[10]
.ALOCCNT[10]
.AFWORK=0
.AFWORKCNT=0
.AFMISS=0
.AFMISSCNT=0
.TITLES="教会"

ANIME_INIT

S_STARTDAY	= SCH_STARTDAY[SCH_NOW]		; スケジュール開始日
S_ENDDAY	= SCH_ENDDAY[SCH_NOW]		; スケジュール終了日

SCH_NOW++		; スケジュール実行中項目

R_CHURCH		; 4=教会

LOAD("RUNNING")

;***************************************
;
;		教会
;
;***************************************

*R_CHURCH		; 4=教会

TXOPEN(1)
TXA(NAME_FIRST)は今日から，
街の教会で奉仕活動です．
@P
TXCLOSE(1)

TXOPEN(2)
TXFACE("F053")

GUARA_BASE	=1						; 基本給
GUARA_ALL	=0						; バイト料合計
WORK_DAYS	=0						; 仕事日数
SUCCESS_DAYS=0						; 成功日数

CK_GUARA(RUN_ARBEIT[5],GUARA_BASE)	; バイト料
FLAG_UP		= AX					; 待遇アップ
GUARA_ONE	= DX					; バイト料

シスター　リイ

RUN_ARBEIT[21]++	;「教会」訪問回数

IF ( GRA_ARBEIT[5] = FLAG_UP )		;「教会」待遇

	IF ( RUN_ARBEIT[21]=1 )		;１回目
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」

	IF ( RUN_ARBEIT[21]>1 )		;２回目以降
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」

IF ( GRA_ARBEIT[5] ! FLAG_UP )		;「教会」待遇

	GRA_ARBEIT[5] = FLAG_UP			;「教会」待遇

	IF ( FLAG_UP = 1 )				; 待遇アップ１回目
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
		@P
		@C
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊TXGOLD(GUARA_ONE)＊＊＊＊＊＊＊＊＊＊」

	IF ( FLAG_UP ! 1 )				; 待遇アップ２回目以降
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊TXGOLD(GUARA_ONE)＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
TXCLOSE(2)

ALL_OPEN
DAY_COUNTER	= S_ENDDAY - S_STARTDAY + 1	; 日付カウンタ

	TXF(11)

	IF ( VAL_WEEK!0 )					; Sunday

		B_SHINKOH += 2					; 信仰
		B_MORALS++						; モラル
		B_INGYO -= 2					; 因業
		IF ( B_INGYO < 0 ) B_INGYO = 0	; 因業
		B_STRESS++						; ストレス

		WORK_DAYS++						; 仕事日数
		TXCLR TXA(TITLES)・TXPOINT(WORK_DAYS)日目

		IRAND999X(B_STRESS)				; ストレス
		SUCCESS_LINE= B_TAIRYOKU - IRND	; 体力

		CHK_RENAI
		CHK_HIKOUKA

		IF ( SUCCESS_LINE >= 10 )		; 体力１０ 成功
			今日は１日無事，奉仕を終えた．
			SUCCESS_DAYS++				; 成功日数
			GUARA_ALL += GUARA_ONE		; バイト料合計
			GXXDGOLD
			SUCCESS_FLAG = 1			; 成功フラグ
			ANIME_WIDW					;

		IF ( SUCCESS_LINE < 10 )			; 体力１０ 失敗
			IF ( S_HIKOUKA < 10 )
				今日はなにか失敗をしたようだ・・・
			IF ( S_HIKOUKA >= 10 )
				TXA(NAME_FIRST)は仕事をサボっている・・・
			SUCCESS_FLAG = 0			; 成功フラグ
			ANIME_WIDW					;

		GXXD(0,B_SHINKOH)			; 信仰
		GXXD(1,B_MORALS)			; モラル
		GXXD(2,B_INGYO)				; 因業
		GXXD(3,B_STRESS)			; ストレス

	IF ( VAL_WEEK=0 )				; Sunday
		@C
		今日は日曜日です．
		ANMTSUNDAY

	TXF(10)

	BORTH_CHECK

	IF ( DAY_COUNTER ! 1 )
		CLENDER(3)					; カレンダー・更新処理
		CLENDER(2)					; カレンダー・再表示
LOOP DAY_COUNTER
ALL_CLOSE

RUN_ARBEIT[0]++					;「娘を働かせる」実行回数
IF ( SUCCESS_DAYS>0 )			; 成功日数
	RUN_ARBEIT[5]++				;「教会」実行回数

TXOPEN(2)
TXFACE("F053")

IF ( SUCCESS_DAYS = WORK_DAYS )		; バイト終了時（パーフェクト）
	P1X100(GUARA_ALL,50)
	GUARA_ALL += AX
	「＊＊＊＊＊＊TXPOINT(SUCCESS_DAYS)＊＊＊＊＊TXGOLD(GUARA_ALL)＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	GOLDADD(GUARA_ALL)			; バイト料合計

IF ( SUCCESS_DAYS < WORK_DAYS   SUCCESS_DAYS > 0 );（失敗があった場合）
	「＊＊＊＊＊＊TXPOINT(SUCCESS_DAYS)＊＊＊＊＊TXGOLD(GUARA_ALL)＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	GOLDADD(GUARA_ALL)			; バイト料合計

IF ( SUCCESS_DAYS = 0 )			; バイト終了時（全滅）
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
WWPROF(3)					; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・所持金
@P
TXCLOSE(2)

RET

*ALL_OPEN
ANIME_OPEN
STRSCLEAR
GG_STR[0]="信仰"
GG_VAL[0]=B_SHINKOH
GG_STR[1]="モラル"
GG_VAL[1]=B_MORALS
GG_STR[2]="因業"
GG_VAL[2]=B_INGYO	
GG_STR[3]="ストレス"
GG_VAL[3]=B_STRESS
GG_MAX=4
GXXSTART
FLAG_ALLOPEN=1
RET

*ALL_CLOSE
IF ( FLAG_ALLOPEN=1 )
	FLAG_ALLOPEN=0
	GR_CLOSE						; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ CLOSE
	ANIME_CLOSE
RET

; ******************
;	lib.
; ******************

*GXXSTART
GR_F(1,16)			; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN  #16	13,000byte
C3 = 0
C2 = 0
IGRP_X=4
IGRP_Y=340
IGRP_CNT=0
C1 = GG_MAX
	IGRP_TBL1[IGRP_CNT]=GG_STR[C2]
	IGRP_TBL3[IGRP_CNT]=GG_VAL[C2]
	IGRP_CNT++ C2++
	IF ( IGRP_CNT = 2 )
		C3++ GRPOPEN(1,C3)		; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
		IGRP_CNT = 0
		IGRP_X += 24
LOOP C1
IF ( IGRP_CNT ! 0 )
	C3++ GRPOPEN(1,C3)		; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
IGRP_TBL1[0]="もうけ"
IGRP_TBL3[0]=0
IGRP_CNT=1
IGRP_X=4
IGRP_Y=140
C3++ GOLDOPEN(1,C3)			; ﾊﾟﾗﾒｰﾀ 所持金ｳｲﾝﾄﾞｳ
GG_GOLD_HDL=C3
RET

*GXXD
C3 = AX
C4 = BX
MOD( C3, 2 )	C5 = AX		; 余り
C6 = C3 / 2 + 1				; handle.
GRPUPD(C6,C5,C4)			;
RET

*GXXDGOLD
GOLDUPD(GG_GOLD_HDL,0,GUARA_ALL)
RET

; ******************
;	Txxx
; ******************

*ANIME_INIT
TIMEWAIT1=8		;8;6
TIMELOPMAX=5	;5;12

;仕事中１
RANDAM(35)			;8(1) - 280(35)
ALOCX[0]	= IRND
ALOCY[0]	=113-79
ALOCC[0]	=0
ALOCCNT[0]	=2
.FILM0[2]=1,2
;仕事中２
RANDAM(35)			;8(1) - 280(35)
ALOCX[1]	= IRND
ALOCY[1]	=113-79
ALOCC[1]	=0
ALOCCNT[1]	=2
.FILM1[2]=7,8
;仕事ミス・病気１
ALOCX[2]	=ALOCX[0]
ALOCY[2]	=113-79
ALOCC[2]	=0
ALOCCNT[2]	=2
.FILM2[2]=5,6
;仕事ミス・病気２
ALOCX[3]	=ALOCX[1]
ALOCY[3]	=113-79
ALOCC[3]	=0
ALOCCNT[3]	=10
.FILM3[10]
;サボリ
ALOCX[4]	=8/8
ALOCY[4]	=113-79
ALOCC[4]	=0
ALOCCNT[4]	=2
.FILM4[2]=25,26

;シスター
ALOCX[5]	=240/8
ALOCY[5]	=113-79
ALOCC[5]	=0
ALOCCNT[5]	=12
.FILM5[12]=27,28,27,28, 29,28,27,28, 30,31,32,33

;机（左）
ALOCX[6]	=200/8
ALOCY[6]	=113-79
ALOCC[6]	=0
ALOCCNT[6]	=1
.FILM6[1]=34

;机（右）
ALOCX[7]	=280/8
ALOCY[7]	=113-79
ALOCC[7]	=0
ALOCCNT[7]	=1
.FILM7[1]=35

;机（中）
ALOCX[8]	=240/8
ALOCY[8]	=113-79
ALOCC[8]	=0
ALOCCNT[8]	=1
.FILM8[1]=36

RANDAM(2)
IF ( IRND=1 )
	WORKS1_LEFT
IF ( IRND=2 )
	WORKS1_RIGHT

RANDAM(2)
IF ( IRND=1 )
	WORKS2_LEFT
IF ( IRND=2 )
	WORKS2_RIGHT

RET

*WORKS1_RIGHT
FILM0[0]=1
FILM0[1]=2
ALOCF[0] = 0
RET

*WORKS1_LEFT
FILM0[0]=3
FILM0[1]=4
ALOCF[0] = 1
RET

*WORKS2_RIGHT
FILM1[0]=9
FILM1[1]=10
ALOCF[1] = 0
FILM3[0]=18
FILM3[1]=19
FILM3[2]=20
FILM3[3]=21
FILM3[4]=22
FILM3[5]=23
FILM3[6]=24
FILM3[7]=23
FILM3[8]=24
FILM3[9]=23
ALOCF[3] = 0
RET

*WORKS2_LEFT
FILM1[0]=7
FILM1[1]=8
ALOCF[1] = 1
FILM3[0]=11
FILM3[1]=12
FILM3[2]=13
FILM3[3]=14
FILM3[4]=15
FILM3[5]=16
FILM3[6]=17
FILM3[7]=16
FILM3[8]=17
FILM3[9]=16
ALOCF[3] = 1
RET

*ANIME_OPEN
WWANIME(1,0)			; anime window open.
WWANIME(3,0,"T003B")	; pictuer load.
WWANIME(6,0,"J006A")	; anime pattern add load.
WWANIME(4,0)			; put BG.
WWANIME(5,0)			; pictuer copy vvram -> CRT.
TXOPEN(A_TEXT_HDL)
RET

*ANIME_CLOSE
WWANIME(2,0)		: anime window close.
TXCLOSE(A_TEXT_HDL)
RET

*ANIME_WIDW
IF ( AFMISSCNT = 0 )
	RANDAM(2)
	AFMISS = IRND - 1
	ALOCC[2] = 0
	ALOCC[3] = 0
	IF ( SLCANM = 0 )( SLCANM = 1 )
		AFMISS = AFWORK
	IF ( AFMISS=0 )	AFMISSCNT = 1
	IF ( AFMISS=1 )	AFMISSCNT = 2
IF ( AFWORKCNT = 0 )
	RANDAM(2)
	AFWORK = IRND - 1
	RANDAM(2)
	AFWORKCNT = IRND+1
AFWORKCNT--
IF ( SUCCESS_FLAG ! 0 )			; 成功フラグ
	AFMISSCNT = 0
	IF ( AFWORK=0 ) ANMT001(0)	; 0=仕事中１,1=仕事中２
	IF ( AFWORK=1 ) ANMT001(1)	; 0=仕事中１,1=仕事中２
	GOTO ANIME_WIDW_SHOW
IF ( S_HIKOUKA >= 10 )		; 失敗時（非行化指数１０以上）
	ANMT001(4)				; 4=サボリ
	GOTO ANIME_WIDW_SHOW
AFMISSCNT--
IF ( AFMISS=0 ) ANMT001(2)	; 2=仕事ミス１,3=仕事ミス２
IF ( AFMISS=1 ) ANMT001(3)	; 2=仕事ミス１,3=仕事ミス２
*ANIME_WIDW_SHOW
RET


*ANMT001
; AX :	0=仕事中１,1=仕事中２,2=仕事ミス１,3=仕事ミス２,4=サボリ
SLCANM = AX

TIMELOP=TIMELOPMAX
	WWANIME(4,0)		; put BG.
	TIMER1(TIMEWAIT1)

	ANIM_NUM(5)	APUT(ALOCX[5],ALOCY[5],DX)
	ANIM_NUM(6)	APUT(ALOCX[6],ALOCY[6],DX)
	ANIM_NUM(7)	APUT(ALOCX[7],ALOCY[7],DX)
	ANIM_NUM(8)	APUT(ALOCX[8],ALOCY[8],DX)
	ALOCC[5]++

	ANIM_NUM(SLCANM)	APUT(ALOCX[SLCANM],ALOCY[SLCANM],DX)
	ALOCC[SLCANM]++

	IF ( SLCANM = 0 )				;0=仕事中１
		IF ( ALOCF[0] = 0 )
			IF ( ALOCX[0]>=35 )		;280/8
				WORKS1_LEFT
			IF ( ALOCX[0]<35 )		;280/8
				ALOCX[0]++
				ALOCX[2]++
		IF ( ALOCF[0] = 1 )
			IF ( ALOCX[0]<=1 )		;8/8
				WORKS1_RIGHT
			IF ( ALOCX[0]>1 )		;8/8
				ALOCX[0]--
				ALOCX[2]--

	IF ( SLCANM = 1 )				;1=仕事中２
		IF ( ALOCF[1] = 0 )
			IF ( ALOCX[1]>=35 )		;280/8
				WORKS2_LEFT
			IF ( ALOCX[1]<35 )		;280/8
				ALOCX[1]++
				ALOCX[3]++
		IF ( ALOCF[1] = 1 )
			IF ( ALOCX[1]<=1 )		;8/8
				WORKS2_RIGHT
			IF ( ALOCX[1]>1 )		;8/8
				ALOCX[1]--
				ALOCX[3]--

	WAIT1
	WWANIME(5,0)		; pictuer copy vvram -> CRT.
LOOP TIMELOP
RET

*ANMTSUNDAY
TIMELOP=TIMELOPMAX
	WWANIME(4,0)		; put BG.
	TIMER1(TIMEWAIT1)
	ANIM_NUM(6)	APUT(ALOCX[6],ALOCY[6],DX)
	ANIM_NUM(7)	APUT(ALOCX[7],ALOCY[7],DX)
	ANIM_NUM(8)	APUT(ALOCX[8],ALOCY[8],DX)
	WAIT1
	WWANIME(5,0)		; pictuer copy vvram -> CRT.
LOOP TIMELOP
RET

*ANIM_NUM
;ANIM_NUM(filmnumber)
;-O- DX pattern number.
IF ( ALOCC[AX] >= ALOCCNT[AX] )
	ALOCC[AX] = 0
BX = ALOCC[AX]
IF (AX=0) DX=FILM0[BX]
IF (AX=1) DX=FILM1[BX]
IF (AX=2) DX=FILM2[BX]
IF (AX=3) DX=FILM3[BX]
IF (AX=4) DX=FILM4[BX]
IF (AX=5) DX=FILM5[BX]
IF (AX=6) DX=FILM6[BX]
IF (AX=7) DX=FILM7[BX]
IF (AX=8) DX=FILM8[BX]
DX--	; DX pattern number.
RET

*APUT
; anime pattern put vvram.
; APUT(x,y,pattern)
GIRX=AX
GIRY=BX
SI = CX * 2 + 1
DX = BX
CX = AX
AX = 7
BX = 0
;	WWANIME(7,0,ALOCX[0],ALOCY[0],ALOCC[0])
WWANIME	; anime pattern put vvram.
RET


;************************************
;	誕生日チェック
;************************************

*BORTH_CHECK

IF ( P_BORTHMONTH = VAL_MONTH  P_BORTHDAY = VAL_DAY )			; 娘の誕生日
	IF ( P_BORTHMONTH[1] = VAL_MONTH  P_BORTHDAY[1] = VAL_DAY )	; 父の誕生日
		E_ETC[14]=P_NENREI[1]			;「父の誕生日」をチェックした年齢
		IF ( URA_FATHER >=60 )			;父との関係値が６０以上
			ALL_CLOSE
			EVE4243						;娘の誕生日と父の誕生日の合体
			IF ( DAY_COUNTER ! 1 )
				ALL_OPEN
			RET

IF ( P_BORTHMONTH = VAL_MONTH )			; 誕生月
	IF ( P_BORTHDAY = VAL_DAY )			; 誕生日
		ALL_CLOSE
		EVENT042						; 42 娘の誕生日
		IF ( DAY_COUNTER ! 1 )
			ALL_OPEN
		RET

IF ( P_BORTHMONTH[1] = VAL_MONTH )		; 父の誕生月
	IF ( P_BORTHDAY[1] = VAL_DAY )		; 父の誕生日
		E_ETC[14]=P_NENREI[1]			;「父の誕生日」をチェックした年齢
		IF ( URA_FATHER >=60 )			;父との関係値が６０以上
			ALL_CLOSE
			EVENT043					; 43 父の誕生日
			IF ( DAY_COUNTER ! 1 )
				ALL_OPEN
			RET
RET

*CHK_RENAI
IF ( FLAG_RENAI!0 )		;状態・恋愛
	RANDAM(100)			;２０％の確率
	IF ( IRND<=20 )
		SUCCESS_LINE=0
RET

*CHK_HIKOUKA
IF ( S_HIKOUKA >= 10 S_HIKOUKA < 30 )	;状態・やや反抗的
	RANDAM(100)							;４０％の確率
	IF ( IRND<=40 )
		SUCCESS_LINE=0
	RET

IF ( S_HIKOUKA >= 30 )			;状態・反抗的
	RANDAM(100)					;８０％の確率
	IF ( IRND<=80 )
		SUCCESS_LINE=0
	RET
RET
;
;	end of "JOB004.TXT"
;
