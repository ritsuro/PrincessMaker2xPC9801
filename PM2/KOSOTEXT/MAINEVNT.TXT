;"MAINEVNT.TXT"
;
;	C1 : スケジュール項目
;	C2 : スケジュール内容
;
;	イベント(EVENT)
;
;	1 執事のあいさつ
;	2 執事の忠告		"MAINLOOP.TXT"
;	3 猛暑
;	4 厳寒
;	5 占い師の訪問
;	6 訪問販売
;	7 魔族の訪問
;	8 道化師の訪問
;	9 家出
;	10 魔法使いの訪問
;	11 幼精霊の訪問
;	12 老人のお礼
;	13 プロポーズ
;	14 妾のお手当
;	15 技能アップの祝福
;	16 極楽鳥の卵を食べる
;	17 極楽鳥の誕生
;	18 ムダ使い			"SCNKYUSO.TXT","SCNVACAN.TXT" -> KAIMONO.TXT"
;	19 ライバルを意識する		"MAINLOOP.TXT"
;	20 ドラゴンのプロポーズ
;	21 妾 - 26 ライバル登場 -> "RUNNING.TXT"	＜＜＜未処理＞＞＞
;
;	27 none.
;
;	28 精霊に出会う - 35 精霊猫に会う  -> 「武者修行実行中」
;
;	36 マーメイドに会う -> "SCNVACAN.TXT"
;
;	37 ビーナスの祝福	"EVNT042.TXT"
;
;	38 none.
;
;	39 絵を描く			"TRG008.TXT" -> "EVNT039.TXT"
;
;	40 ただ食い			"KAIMONO.TXT"
;
;	41 恩賜の剣売却事件	"KAIMONO.TXT" -> "EVNT041"
;
;	42 娘の誕生日  43 父の誕生日
;				"EVNT042.TXT"
;				"EVNT042S.TXT"
;				"EVNT043S.TXT"
;				"EVNT044.TXT"
;
;	44 青年武官と面会	"EVNT044"		＜＜＜未処理＞＞＞お城とリンク
;
;	45 逮捕
;	46 追い剥ぎの発覚
;
;	51 亡霊騎士出現		"JOB010.TXT" -> "EVNT051"
;
;

.S_SECTION				; スケジュール項目
.S_WORKS				; スケジュール内容
S_SECTION	=	C1		; スケジュール項目
S_WORKS		=	C2		; スケジュール内容

.NOW_MONTH_ATR		;どんな月？

CLENDER(11)			;カレンダー・今月の計算
NOW_MONTH_ATR=DX	;どんな月？
					;DX : 0=通常,1=一番最初の日,2=運命の日
					;     3=一番最後の月,4=１月前,5=２月前
					;     6=３月前

RUN_MONTH[0]++		;「月１イベント」実行回数

STRSCLEAR

TXF(10)				; ﾉｰﾏﾙ時ﾃｷｽﾄﾓｰﾄﾞ

TXLC(1,4,290,0)		;text window locate.
TXLC(2,1,260,2)		;text window locate.
TXLC(3,31,270,2)	;text window locate.
TXLC(5,5,260,0)		;text window locate.
TXLC(9,48,260,2)	;text window locate.
WWIVENT(5,0,6,40)	;ivent window locate.
CLENDER(6,0,0)		;カレンダー・セットアップ

IF ( GIRL_DAMEG!0 )
	PLAY(8)		;P8 病気
IF ( GIRL_DAMEG=0 )
	IF ( TM_SEASON =1 ) PLAY(4)		;P4 ﾒｲﾝ春
	IF ( TM_SEASON =2 ) PLAY(5)		;P5 ﾒｲﾝ夏
	IF ( TM_SEASON =3 ) PLAY(6)		;P6 ﾒｲﾝ秋
	IF ( TM_SEASON =4 ) PLAY(7)		;P7 ﾒｲﾝ冬

IF ( RUN_MONTH[0]=1 )			;「月１イベント」実行回数
	GRPCLS						;
	FFDISK(4)					;ﾌﾛｯﾋﾟｰﾃﾞｨｽｸを使用するか
	IF ( AX=0 )
		IF ( P_NENREI < 15 )
			FFDISK(5,3)			; チェンジディスク指定
		IF ( P_NENREI >=15 )
			FFDISK(5,12)		; チェンジディスク指定
	MAIN_FRAME					;
	EVENT001					;執事のあいさつ
	LOAD("MAINBORN")			;

;** clear event.

IF ( VAL_MONTH = 2 )	E_COOL = 0	;ｲﾍﾞﾝﾄ｢厳寒」
IF ( VAL_MONTH = 8 )	E_HOT = 0	;ｲﾍﾞﾝﾄ｢猛暑」

EVENT009			;家出

EVENT016			;極楽鳥の卵を食べる
EVENT017			;極楽鳥の誕生

IF ( NOW_MONTH_ATR=2 )( NOW_MONTH_ATR=3 )	; 2=運命の日,3=一番最後の月
	GOTO NEXT_LOAD

EVENT045			;逮捕

EVENT011			;幼精霊の訪問

;;;EVENT014			;妾のお手当		"MAINBORN.TXT"へ移動

EVENT052			;表彰

EVENT003			;猛暑
EVENT004			;厳寒
EVENT006			;訪問販売
EVENT007			;魔族の訪問
EVENT008			;道化師の訪問
EVENT010			;魔法使いの訪問

EVENT015			;技能アップの祝福
EVENT020			;ドラゴンのプロポーズ
EVENT046			;追い剥ぎの発覚

EVENT005			;占い師の訪問
EVENT013			;プロポーズ

*NEXT_LOAD
LOAD("MAINBORN")

;************************************
;
;	「一ヶ月の最初」に発生するイベント
;
;************************************

;1 執事のあいさつ

*EVENT001
TXOPEN(2)
TXFACE("F039")	;執事・普通
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
@C
「TXA(NAME_FIRST)＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
TXCLOSE(2)
RET


;3 猛暑
;【 概　要 】異常に暑い夏になる．
;【 いつ？ 】７月と８月の最初．
;【発生条件】《１／４》の確率でランダムに発生．
;【 効　果 】夏服を持っていなければ，体力が《１／３》低下．
;				夏服を持っている場合，夏服に着替えれば（YES,NO）
;				ストレスが５低下．

*EVENT003
IF ( E_HOT = 0 )	;ｲﾍﾞﾝﾄ｢猛暑」
	IF ( VAL_MONTH = 6 )( VAL_MONTH = 7 )
		RANDAM(4)
		IF ( IRND = 4 ) LOAD("EVNT003")
RET

;4 厳寒
;【 概　要 】異常寒波が襲来する．
;【 いつ？ 】１２月と２月の最初．
;【発生条件】《１／４》の確率でランダムに発生．
;【 効　果 】冬服を持っていなければ，体力が《１／３》低下．
;　　　　　　冬服を持っている場合，冬服に着替えれば（YES,NO）ストレスが10低下

*EVENT004

IF ( E_COOL = 0 )						;ｲﾍﾞﾝﾄ｢厳寒」
	IF ( VAL_MONTH = 11 )( VAL_MONTH = 1 )
		RANDAM(4)
		IF ( IRND = 4 ) LOAD("EVNT004")
RET

;5 占い師の訪問
;【 概　要 】占い師が訪れて，娘の将来を占ってくれる．
;【 いつ？ 】一ヶ月の最初．
;【発生条件】《１／１２》の確率でランダムに発生．
;【 効　果 】占い師が現れて，見料《年齢×3》Gで，娘の将来を占うかどうか
;			聞いてくる　　　　　　（YES,NO）．
;			占ってもらう場合は，その時点での娘のパラメータで，
;			エンディングの基準に照らして，将来の職業名を教えてくれる．

*EVENT005

RANDAM(12)
IF ( IRND = 1 ) LOAD("EVNT005")
RET

;6 訪問販売
;【 概　要 】旅の行商人が訪れてきて，珍しいアイテム，服，武具を打ってくれる．
;【 いつ？ 】一ヶ月の最初．
;【発生条件】所持金が初めて，1000Ｇ，2000Ｇ，5000Ｇを越えたとき．
;【 効　果 】行商人が持っている品物は，精霊の指輪1000G，ユニコーンの角笛1000G
;　　　　　　悪魔のペンダント1200G，ビーナスの首飾り3000G，傾国のドレス4000G，
;　　　　　　見えないドレス5000G．

*EVENT006

GOLDCMP(1000)
IF ( RUN_MONTH[6]=0  AX=3 )		;「訪問販売」実行回数  私の所持金
	LOAD("EVNT006")
GOLDCMP(2000)
IF ( RUN_MONTH[6]=1  AX=3 )		;「訪問販売」実行回数  私の所持金
	LOAD("EVNT006")
GOLDCMP(5000)
IF ( RUN_MONTH[6]=2  AX=3 )		;「訪問販売」実行回数  私の所持金
	LOAD("EVNT006")
RET

;7 魔族の訪問
;【 概　要 】悪魔が現れて，モラルと引き替えに金をくれる．
;【 いつ？ 】一ヶ月の最初．
;【発生条件】基本能力中でモラルが最も高いとき，《１／８》の確率で発生．
;【 効　果 】魔物の申し出を受けると（YES,NO），モラル値の10倍の金がもらえる．
;				そのかわり，モラル値が０になる。断ると，何も起こらない．

*EVENT007
IF ( B_MORALS < B_TAIRYOKU)	GOTO EVENT007N2	;体力
IF ( B_MORALS < B_KINRYOKU)	GOTO EVENT007N2	;筋力
IF ( B_MORALS < B_CHINOH)	GOTO EVENT007N2	;知能
IF ( B_MORALS < B_KIHIN	)	GOTO EVENT007N2	;気品
IF ( B_MORALS < B_IROKE	)	GOTO EVENT007N2	;色気
IF ( B_MORALS < B_SHINKOH)	GOTO EVENT007N2	;信仰
IF ( B_MORALS < B_INGYO	)	GOTO EVENT007N2	;因業
IF ( B_MORALS < B_KANJUSEI)	GOTO EVENT007N2	;感受性
;*;IF ( B_MORALS < B_STRESS)	GOTO EVENT007N2	;ストレス
IF ( B_MORALS = 0 )			GOTO EVENT007N2
RANDAM(8)
IF ( IRND = 8 ) LOAD("EVNT007")
*EVENT007N2
RET

;8 道化師の訪問
;【 概　要 】お城の道化師が訪ねてきて，ヒントをくれる．
;【 いつ？ 】一ヶ月の最初．
;【発生条件】メインコマンド「お城に行く」で，道化師と通算７回面会すると発生．
;				以後は７回面会するごとに発生し，合計３回までおこる．
;【 効　果 】１回目は，１月にお城にいる若者に会うことを勧める．
;				２回目は，アイテム「王家の竪琴」をくれる．
;				３回目は，あいさつするだけ．

*EVENT008
IF ( RUN_OSHIRO[9]>=7  RUN_MONTH[8]=0 )  ;「面会・道化師」「道化師の訪問」
	LOAD("EVNT008")
IF ( RUN_OSHIRO[9]>=14  RUN_MONTH[8]=1 ) ;「面会・道化師」「道化師の訪問」
	LOAD("EVNT008")
IF ( RUN_OSHIRO[9]>=21 RUN_MONTH[8]=2 )  ;「面会・道化師」「道化師の訪問」
	LOAD("EVNT008")
RET


;9 家出
;【 概　要 】娘が家出して，一ヶ月間何も出来なくなる．
;【 いつ？ 】一ヶ月の最初．
;【発生条件】基本能力中で感受性が最も高く，かつモラルが１０未満のとき，
;				《１／４》の確率で発生．
;   　　　　　　非行化時，《１／２０》の確率で発生．
;【 効　果 】発生した月は，メインコマンドのうち「能力を見る」「持物を見る」
;				「システム」「終了」しか選べない．
;				一ヶ月がむなしく経過し，各評価値が１０％低下する．

*EVENT009
;     DX : 0=通常,1=一番最初の日,2=運命の日
;          3=一番最後の月,4=１月前,5=２月前
;	        6=３月前


IF ( NOW_MONTH_ATR=1 )( NOW_MONTH_ATR=2 ) RET
IF ( NOW_MONTH_ATR=3 )( NOW_MONTH_ATR=4 )( NOW_MONTH_ATR=5 ) RET
IF ( S_BYOUKI>=90 )	RET	; 病気指数(%)

IF ( S_HIKOUKA >= 60 )	; 非行化指数(%)
	RANDAM(20)
	IF ( IRND = 4 )
		E_RWAY = 1					;ｲﾍﾞﾝﾄ｢家出」
		LOAD("MAINBORN")			;

IF ( B_KANJUSEI < B_TAIRYOKU )	RET	;体力
IF ( B_KANJUSEI < B_KINRYOKU )	RET	;筋力
IF ( B_KANJUSEI < B_CHINOH )	RET	;知能
IF ( B_KANJUSEI < B_KIHIN )		RET	;気品
IF ( B_KANJUSEI < B_IROKE )		RET	;色気
IF ( B_KANJUSEI < B_MORALS )	RET	;モラル
IF ( B_KANJUSEI < B_SHINKOH )	RET	;信仰
IF ( B_KANJUSEI < B_INGYO )		RET	;因業
IF ( B_KANJUSEI = 0 )			RET	;感受性
RANDAM(4)
IF ( IRND = 4 )
	E_RWAY = 1					;ｲﾍﾞﾝﾄ｢家出」
	LOAD("MAINBORN")			;
RET


;10 魔法使いの訪問
;【 概　要 】偉大な魔法使いが訪問してきて，魔法技術，魔力，抗魔力の
;				どれかひとつを上げてくれます．
;【 いつ？ 】一ヶ月の最初．
;【発生条件】前の月の３旬すべてを「魔法」の修行に使うと発生．
;【 効　果 】魔法技術，魔力，抗魔力のどれか（ランダムに決定）が，２０％上昇．

*EVENT010
C1 = 0
C2 = SCH_COUNT		;スケジュール数
	IF ( SCHED_SECTION[C1] ! 0 )	;0=娘を鍛える スケジュール項目
		GOTO EVENT010_EXIT
	IF ( SCHED_WORKS[C1] ! 6 )		;6=魔法 スケジュール内容
		GOTO EVENT010_EXIT
	C1++
LOOP C2

IF ( RUN_MONTH[10]<2 )			;「魔法使いの訪問」実行回数
	LOAD("EVNT010")
*EVENT010_EXIT
RET


;11 幼精霊の訪問
;【 概　要 】泉の精霊に返した「妖精の指輪（実は妖精の卵）」から孵化した
;				子供の妖精が訪れてきて，お礼をしてくれます．
;【 いつ？ 】一ヶ月の最初．
;【発生条件】１年前にイベント「精霊に出会う」で，アイテム「精霊の指輪」を
;				泉の精霊に返してやっていること．
;【 効　果 】所持金が２０００増える．感受性が１００上昇．

*EVENT011
IF ( E_ETC[1] ! 0 )			;「幼精霊の訪問」発生条件
	C1 = VAL_YEAR * 12		;年
	C1 += VAL_MONTH			;月
	C2 = E_ETC[82] * 12		;幼精霊卵返却年
	C2 += E_ETC[83]			;幼精霊卵返却月
	C3 = C1 - C2			;何ヶ月目か
	IF ( C3>=12 )			;
		LOAD("EVNT011")			;「幼精霊の訪問」発生条件
RET


;12 老人のお礼
;【 概　要 】イベント「年寄りに親切」の老人から礼金が届く．
;【 いつ？ 】一ヶ月の最初．
;【発生条件】イベント「年寄りに親切」で老人に金を恵む．
;【 効　果 】所持金が２０００Ｇ増える．
;＊＊＊＊＊＊このイベントは廃止します＊＊＊＊＊＊


;13 プロポーズ
;【 概　要 】男性から正式に求婚される．
;【 いつ？ 】一ヶ月の最初．
;【発生条件】15歳以上で，色気と気品がともに２００以上のとき，
;				《１／６》の確率で発生．
;【 効　果 】求婚者からのプレゼントで，所持金が
;				《500〜1000の乱数，下２桁切捨て》増える．
;				求婚に応じても断ってもお金はもらえるが，応じると
;				「父との関係」が２０マイナス．断ると１０プラスとなる．

*EVENT013
IF ( E_PROPOSAL ! 0 )			;ｲﾍﾞﾝﾄ｢プロポーズ｣毎月発生
	IF ( VAL_MONTH=6 )( VAL_MONTH=12 )	;月
		LOAD("EVNT013")

RANDAM(6)
IF ( IRND=1 )
	IF ( P_NENREI >= 15 )			;年齢
		IF ( B_IROKE >= 200 )		;色気
			IF ( B_KIHIN >= 200 )	;気品
				LOAD("EVNT013")
RET

;14 妾のお手当
;
;	"MAINBORN.TXT"へ移動

;*EVENT014
;IF ( E_ETC[2] ! 0 )		;「妾」状態
;	LOAD("EVNT014")
;RET


;15 技能アップの祝福
;【 概　要 】技能パラメータ（戦士評価，魔法評価，社交評価，家事評価）が，
;				一定値を越えると，祝福してもらえる．
;				戦士評価はヴァルキュリア，魔法評価はフェイ，社交評価はパイモン，
;				家事評価はドモヴォイがそれぞれ祝福してくれる．
;【 いつ？ 】一ヶ月の最初．
;【発生条件】各評価がそれぞれ，１００，２００，３００，４００を越える．
;【 効　果 】お祝いの言葉や励ましをうけ，技能が上がったり，
;				アイテムがもらえたりする．その内容は以下の通り．
;	　　　　戦士　　　　　 魔法　　　　　 社交　　 　　　家事
;	 100　戦闘技術+3 　　魔法技術+3 　　礼儀作法+3 　　料　理　+3
;	 200　攻撃力　+4 　　魔　力　+4 　　芸　術　+4 　　掃除洗濯+4
;	 300　防御力　+5 　　抗魔力　+5 　　人　脈　+5 　　気だて　+5
;	 400　ｳﾞｧﾙｷｭﾘｱの剣 　無敵の指輪　　 誘惑の香水　　 至福のパン粉

*EVENT015

;E_ETC[4] 技能アップの祝福・戦士評価・回数
;E_ETC[5] 技能アップの祝福・魔法評価・回数
;E_ETC[6] 技能アップの祝福・社交評価・回数
;E_ETC[7] 技能アップの祝福・家事評価・回数

IF ( E_ETC[4]=3  H_SENSHI>=400 )	LOAD("EVNT015")	;戦士評価
IF ( E_ETC[4]=2  H_SENSHI>=300 )	LOAD("EVNT015")	;戦士評価
IF ( E_ETC[4]=1  H_SENSHI>=200 )	LOAD("EVNT015")	;戦士評価
IF ( E_ETC[4]=0  H_SENSHI>=100 )	LOAD("EVNT015")	;戦士評価

IF ( E_ETC[5]=3  H_MAHO>=400 )		LOAD("EVNT015")	;魔法評価
IF ( E_ETC[5]=2  H_MAHO>=300 )		LOAD("EVNT015")	;魔法評価
IF ( E_ETC[5]=1  H_MAHO>=200 ) 		LOAD("EVNT015")	;魔法評価
IF ( E_ETC[5]=0  H_MAHO>=100 )		LOAD("EVNT015")	;魔法評価

IF ( E_ETC[6]=3  H_SYAKOH>=400 )	LOAD("EVNT015")	;社交評価
IF ( E_ETC[6]=2  H_SYAKOH>=300 )	LOAD("EVNT015")	;社交評価
IF ( E_ETC[6]=1  H_SYAKOH>=200 )	LOAD("EVNT015")	;社交評価
IF ( E_ETC[6]=0  H_SYAKOH>=100 )	LOAD("EVNT015")	;社交評価

IF ( E_ETC[7]=3  H_KAJI>=400 )		LOAD("EVNT015")	;家事評価
IF ( E_ETC[7]=2  H_KAJI>=300 )		LOAD("EVNT015")	;家事評価
IF ( E_ETC[7]=1  H_KAJI>=200 )		LOAD("EVNT015")	;家事評価
IF ( E_ETC[7]=0  H_KAJI>=100 )		LOAD("EVNT015")	;家事評価

RET


;16 極楽鳥の卵を食べる
;【 概　要 】「お料理コンクール」の優勝賞品「極楽鳥の卵」の使い方を決めます．
;【 いつ？ 】「極楽鳥の卵」入手直後の一ヶ月の最初
;【発生条件】「極楽鳥の卵」入手直後である
;【 効　果 】執事が「極楽鳥の卵」を焼いて食べますか？と聞いてくるので，「食べる」
;				と答えると，体力が５０上昇．「極楽鳥」の卵は失われる．
;				「食べない」場合は何も起こらず，翌月の一ヶ月の始めに
;				イベント「極楽鳥の誕生」が発生．

*EVENT016
IF ( E_EAT_EGG ! 0 )		;ｲﾍﾞﾝﾄ｢極楽鳥の卵を食べる｣発生条件
	LOAD("EVNT016")
RET


;17 極楽鳥の誕生
;【 概　要 】極楽鳥が卵からかえって，お礼をしてくれる．
;【 いつ？ 】イベント「極楽鳥の卵を食べる」が発生した翌月の一ヶ月の最初
;【発生条件】イベント「極楽鳥の卵を食べる」で，卵を食べないこと．
;【 効　果 】卵からかえった極楽鳥の雛がお礼を言って，故郷に飛び去って行く．
;				娘の感受性が１００上昇する．

*EVENT017
IF ( E_ETC[8] ! 0 )				;「極楽鳥の誕生」発生条件
	E_ETC[8]=0					;「極楽鳥の誕生」発生条件
	ITEM_SRCH(1,34)				;ICON search. 34=極楽鳥の卵
	IF ( AX=1 )					;0=none,1=search.
		LOAD("EVNT017")
RET


;18 ムダ使い
;【 概　要 】娘が勝手に買物をしてしまう．
;【 いつ？ 】休息の１旬終了時．
;【発生条件】非行化指数が１以上で，かつ，所持金が仕立屋，雑貨屋，料理屋のいずれか
;				で買物出来る額あるときに，《非行化指数％》の確率で発生．
;【 効　果 】仕立屋，雑貨屋，料理屋のなかから，ランダムにどれか一つ買物をする．
;				手に入れた品物は，通常の買物同様の効果がある．
;				持物がいっぱいの場合は，料理屋を選ぶ．
;
;	"SCNKYUSO.TXT"へ移動
;	"SCNVACAN.TXT"へ移動


;19 ライバルを意識する
;【 概　要 】娘がライバルを意識して，収穫祭イベントに参加したがる．
;【 いつ？ 】１０月の始め
;【発生条件】１４歳以降，ライバルが存在する場合に，ライバルの参加イベントに参加さ
;				せない予定を組む．
;【 効　果 】もし，１０月の予定にライバルの参加するイベントを組み込まないと，娘が
;				予定の変更を要求する．
;				聞き入れる場合は，自動的にそのイベント参加の予定が組まれる．
;				また，父との関係値が５増える．
;				聞き入れなければ，娘はがっかりして，ストレスが１０増え，父との関係値
;				が１０下がる．
;
;	"MAINLOOP.TXT"へ移動


;20 ドラゴンのプロポーズ
;【 概　要 】娘に負けた若いドラゴンがプロポーズする
;【 いつ？ 】一ヶ月の最初
;【発生条件】イベント「生意気なドラゴンに会う」で若いドラゴンと戦って勝つ．
;			イベント「老ドラゴンに会う」が２回発生している．
;			娘が１６歳以上．
;【 効　果 】家に派手な身なりの若者が訪ねてきて，娘と結婚したいという．若者には老
;			人が付き添っている．二人の正体は武者修行のイベント「生意気なドラゴン
;			に会う」で娘に倒された若いドラゴンと，イベント「老ドラゴンに会う」の
;			老ドラゴンの変身で，老ドラゴンは媒酌人を買って出たもの．
;			プロポーズを受けると，引出物として10000Ｇがもらえる．モラルが３０下
;			がる．父との関係が２０下がる．
;			エンディングに影響．
;			断れば気品が５上がる．父との関係が１０上がる．

*EVENT020
IF ( P_NENREI >= 16 )					;年齢
	IF ( E_ETC[10] ! 0 )				;若いドラゴンと戦って勝つ．
		IF ( E_ETC[80] >= 2 )			;15歳以上で「老ドラゴンに会う」回数
			IF ( RUN_MONTH[20]=0 )		;「ドラゴンのプロポーズ」実行回数
				LOAD("EVNT020")			;ドラゴンのプロポーズ
RET

;45 逮捕
;【 概　要 】非行化指数が１００になると，娘が何か問題を起こして，逮捕される．
;【 いつ？ 】一ヶ月の最初．
;【発生条件】非行化指数が一ヶ月の最初の時点で１００．
;【 効　果 】各評価値が《１／１０》に．因業値とストレスが０に．

*EVENT045
IF ( S_HIKOUKA >= 100 )			;非行化指数(%)
	LOAD("EVNT045")				;45 逮捕
RET


;46 追い剥ぎの発覚
;【 概　要 】武者修行時に追い剥ぎをしていたことが，世間にバレる．
;【 いつ？ 】一ヶ月の最初．
;【発生条件】追い剥ぎ回数が１０回になる．以後，１０回ごとに発生する．
;【 効　果 】気品が０に．人脈が初期値と同じに．各評価値が《１／２》に．

*EVENT046
C1 = E_ETC[15] - E_ETC[16]		;追い剥ぎ回数 - 前科取り調べ時の追い剥ぎ回数
IF ( C1 >= 10 )
	LOAD("EVNT046")
RET

;52 表彰
;【 概　要 】イベント「お尋ね者に遭遇」で，お尋ね者に勝つと，翌月表彰される．
;【 いつ？ 】お尋ね者に勝った直後の一ヶ月の最初．
;【発生条件】イベント「お尋ね者に遭遇」で，お尋ね者と戦って勝つ．
;【 効　果 】お尋ね者に懸けられた賞金をもらえる．また，戦士評価が３０上がる．

*EVENT052
IF ( E_ETC[19]=1 )( E_ETC[20]=1 )( E_ETC[21]=1 )	;お尋ね者(1=逮捕,2=賞金)
	LOAD("EVNT052")
RET

;**************
;	main frame
;**************

*MAIN_FRAME
CLENDER(6,0,0)		; カレンダー・セットアップ
WWIVENT(5,0,6,40)	; ivent window locate.
PALET_SET(0)		;
WWFRAME(1)			;
CLENDER(1)			;
WWPROF(1)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・表示
WWGIRL(3)			; girl quick put.
RET


;
;	end of "MAINEVNT.TXT"
;
