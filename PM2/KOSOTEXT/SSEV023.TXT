;"SSEV023.TXT"
;
;
;	道場破りの挑戦
;
;	-I- C1 : mode 1=EVNT023.TXT		33333=なんでも屋	;機能
;

.RUN_EV023_MODE
RUN_EV023_MODE = C1

STRSCLEAR

.CMN_FUNC				;機能
.CMN_SHIAI				;第何試合(1-n)
.CMN_LINK				;組み合せ番号(1-n)
.CMN_VIC_FLG			;勝敗 1=娘勝ち,2=敵勝ち

.TIMELOP
.TIMELOPMAX
.TIMEWAIT1
.SLCANM=0
.SLCATK=0
.ALCX[15]
.ALCY[15]
.ALCC[15]
.ALCF[15]
.ALCCNT[15]
.APUTNUM

.BLCX[20]
.BLCY[20]
.BLCC[20]
.BLCF[20]
.BLCCNT[20]

.CHAR_SEQ_TBL[20]

.BANK_BG_CHAR=6			;#6 text buffer.後半

.OFST_GIRL=1			;1
.OFST_FITER=27			;1+13*2
.OFST_SLCATK=7

.GIRL_XX = 0
.GIRL_YY = 0
.GIRL_SHOT = 0

.FITER_XX= 0
.FITER_YY= 0
.FITER_SHOT= 0

.BOUGYO_TK = 0
.FITER_WALK_WAIT= 0

.WAIT_MAGIC = 0
.COUNT_ATTACK=0

.LENG_ATTACK_GR=5			;通常攻撃
.LENG_MAGIC_GR=1			;魔法攻撃
.LENG_ATTACK_TK=4			;敵・通常攻撃
.LENG_MAGIC_TK=-4			;敵・魔法攻撃
							;"SSBTnn.TXT"の処理によっては変更
.XX1.YY1
.FLAG_BATLE_EXIT=0			;戦闘の終了フラグ

.BATLE_TK_SLCT				;1=通常攻撃,2=魔法攻撃
.BATLE_GR_SLCT				;1=通常攻撃,2=魔法攻撃

.A_TEXT_HDL=6				;アニメ中テキストハンドル

.MEN_YY_L=0
.MEN_YY_R=0

.FLAG_CHARNGE=0				;1=挑戦を受ける,2=挑戦を断る

;**************
;	戦闘パラメータ
;**************

.FLAG_VICTORY	;勝利フラグ
.FLAG_KAIWA_OK	;会話条件成立
.FLAG_BATLE = 0	;0=戦闘中,1=娘勝ち,2=敵勝ち,3=娘逃げる,4=敵逃げる,5=引き分け
.G_DAMEG
.T_DAMEG
.REC_TBL[50]
.TOOL_BUKI

.ATTACK_VAL		;攻撃バリュー
.DEFENS_VAL		;防御バリュー
.BATLE_NOW		;1=命中,2=失敗

.MY_KAISHIN		;会心率(%)

.R_HP		=0	;ＨＰ
.R_MP		=0	;ＭＰ

.MNSTR_X
.MNSTR_Y

.R_MS_NUMBER	=0	;モンスター番号
.R_FACE			=0	;フェイス

.R_NAME			=0	;名前
.R_SENTOH		=0	;戦闘技術
.R_KOUGEKI		=0	;攻撃力
.R_BOUGYO		=0	;防御力
.R_SOUKOU		=0	;装甲
.R_SKNAME		=0	;装甲名
.R_MAHOGIJUTSU	=0	;魔法技術
.R_MARYOKU		=0	;魔力
.R_KOHMARYOKU	=0	;抗魔力
.R_SENI			=0	;戦意
.R_DASSYUTU		=0	;脱出率(%)
.R_TSUKON		=0	;痛恨率(%)
.R_MAX_HP		=0	;最大ＨＰ
.R_MAX_MP		=0	;最大ＭＰ

.R_SENSHI		=0	;戦士評価
.R_MAHO			=0	;魔法評価

.R_GOLD			=0	;懸賞金
.R_SE_ATTACK	=0	;剣の当たる(1=皮,2=鉄,3=体毛,4=固い体)
.GIRL_SE_ATTACK	=0	;娘に当たる(1=皮,2=鉄,3=体毛)

;−戦闘相手用（６種類）−
.MONSTR[6]
.M_HELLO	=0	;対戦時の口上
.M_VICTRY	=1	;勝利の台詞
.M_DEFEAT	=2	;敗戦の台詞
.M_HELP		=3	;降参の台詞
.M_AWARD	=4	;優勝の台詞
.M_STEWARD	=5	;執事の評価

.STR_WORK="12345678123456781234567812345678.."

.FLAG_KAISHIN	;会心ﾌﾗｸﾞ
.FLAG_TSUKON	;痛恨ﾌﾗｸﾞ

.BATL_COUNT	=0

.FLG_HABAM	=0

TXLC(1,28,284,0)	;text window locate.
TXLC(2,17,148,1)	;text window locate.
TXLC(3,31,270,2)	;text window locate.
TXLC(7,26,293,0)	;text window locate.

CHECK_GIRL_DATA				;娘データ
CHECK_BATLER				;対戦相手データ

R_HP = R_MAX_HP				;最大ＨＰ
R_MP = R_MAX_MP				;最大ＭＰ
R_SENSHI = R_SENTOH + R_KOUGEKI + R_BOUGYO			;戦士評価は《戦闘技術＋攻撃力＋防御力》
R_MAHO   = R_MAHOGIJUTSU + R_MARYOKU + R_KOHMARYOKU	;魔法評価は《魔法技術＋魔力＋抗魔力》

E_ETC[100]=0				;通常攻撃時ﾀﾞﾒｰｼﾞ合計
E_ETC[101]=0				;魔法攻撃時ﾀﾞﾒｰｼﾞ合計

TEXT_BATLE					;道場破り

*X_FINALE

MUSIC(2)					;音楽ﾌｪｰﾄﾞｱｳﾄ

IF ( FLAG_CHARNGE=1 )		;1=挑戦を受ける,2=挑戦を断る
	FREAM_GIRL				;娘フレーム表示

E_EV_SKIP=1					;イベントスキップ

IF ( RUN_EV023_MODE = 33333 )	LOAD("TEST02")	;なんでも屋

IF ( GIRL_DAMEG!0 )
	PLAY(8)		;P8 病気
IF ( GIRL_DAMEG=0 )
	IF ( TM_SEASON =1 ) PLAY(4)		;P4 ﾒｲﾝ春
	IF ( TM_SEASON =2 ) PLAY(5)		;P5 ﾒｲﾝ夏
	IF ( TM_SEASON =3 ) PLAY(6)		;P6 ﾒｲﾝ秋
	IF ( TM_SEASON =4 ) PLAY(7)		;P7 ﾒｲﾝ冬

LOAD("RUNNING")

;**************
;	道場破り
;**************

*TEXT_BATLE

MUSIC(2)	;音楽ﾌｪｰﾄﾞｱｳﾄ

TXOPEN(2)
SI=R_FACE	TXFACE		;道場破り・普通
「＊＊＊＊TXA(R_NAME)＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P

PLAY(29)	;P29 緊迫

TXOPEN(10)
道場破りだ！TXA(R_NAME)が訓練所に乗り込んできた！
@P
@2
@C
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
TXSLCT(10)
@C
すでに師範は帰ってしまっている．他の弟子達は恐れて戦おうとしない．
@P
TXCLOSE(10)

TXOPEN(3)
TXGIRL(11)		;娘・不安
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」

SLCTX("名乗り出る,黙っている")
IF ( ISLCT = 1 )		;黙っている場合
	FLAG_CHARNGE=2		;2=挑戦を断る
	@2
	@C
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXA(R_NAME)は道場の看板をはぎ取って去っていった．
	@P
	TXCLOSE(2)
	MUSIC(2)	;音楽ﾌｪｰﾄﾞｱｳﾄ
	@3
	@C
	TXGIRL(11)			;娘・不安
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(3)
	E_ETC[11]=1			;「道場破り」で「剣術」の稽古が出来ない
	RET

;名乗り出る場合
FLAG_CHARNGE=1			;1=挑戦を受ける
@3
@C
TXGIRL(14)		;娘・りりしい
「＊＊＊＊＊＊＊＊＊＊＊＊」
@P
@2
@C
;道場破り・普通
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
TXCLOSE(3)
TXCLOSE(2)
BATLE_WINDOW			;戦闘ｳｲﾝﾄﾞｳ

IF ( CMN_VIC_FLG=2 )	;勝敗 2=敵勝ち
	E_ETC[11]=1			;「道場破り」で「剣術」の稽古が出来ない
RET


;**************
;	戦闘ｳｲﾝﾄﾞｳ
;**************

*BATLE_WINDOW
ANIME_INIT					;
ANIME_OPEN					;
GRAPSHOW					;graph.
TXOPEN(7)					;
ANIME_BATL					;戦闘
RET

*ANIME_BATL

PLAY(23)				;P23 試合

BATLE_MODE				;戦闘モード

@C
IF ( CMN_VIC_FLG=1 )	;勝敗 1=娘勝ち
	SSBATL_GR_VICTORY	;
IF ( CMN_VIC_FLG=2 )	;勝敗 2=敵勝ち
	SSBATL_TK_VICTORY	;
@C
IF (FLAG_BATLE=1) GIRL_VIC_TEXT
IF (FLAG_BATLE=2) TEKI_VIC_TEXT
IF (FLAG_BATLE=3) GIRL_DOWN_TEXT
IF (FLAG_BATLE=4) TEKI_DOWN_TEXT
IF (FLAG_BATLE=5) HIKIWAKE_TEXT
RET

*SSBATL_TAIKI
SLCATK=0
SLCANM=0
ANIME_PLAY(10)
RET

*SSBATL_TK_ATTACK
SLCATK=1
SLCANM=0
ANIME_PLAY(40)
RET

*SSBATL_TK_MAGIC
SE_MAGIC
SLCATK=2
SLCANM=0
ANIME_PLAY(40)
RET

*SSBATL_GR_ATTACK
SLCATK=0
SLCANM=1
ANIME_PLAY(40)
RET

*SSBATL_GR_MAGIC
SE_MAGIC
SLCATK=0
SLCANM=2
ANIME_PLAY(40)
RET

*SSBATL_GR_VICTORY
SLCATK=5
SLCANM=4
ANIME_PLAY(16)
RET

*SSBATL_TK_VICTORY
SLCATK=4
SLCANM=5
ANIME_PLAY(16)
RET

;**************
;	text.
;**************

*GIRL_DOWN_TEXT
FACEOPEN(3)		;娘・悲しい
TXA(NAME_FIRST)は戦意を失った．
@P
@C
GIRL_DOWN_TALK
RET

*TEKI_DOWN_TEXT
FACEOPEN(1)		;娘・嬉しい
TXA(R_NAME)は戦意を失った．
@P
@C
TXA(MONSTR[M_HELP])
@P
@C
GIRL_VIC_TALK
FACECLOSE
RET

*GIRL_VIC_TEXT
FACEOPEN(1)		;娘・嬉しい
TXA(MONSTR[M_DEFEAT])
@P
@C
TXA(NAME_FIRST)はTXA(R_NAME)を打ち破った！
@P
@C
GIRL_VIC_TALK
FACECLOSE
RET

*TEKI_VIC_TEXT
FACEOPEN(3)		;娘・悲しい
TXA(MONSTR[M_VICTRY])
@P
@C
TXA(NAME_FIRST)はTXA(R_NAME)の前に敗れ去った・・・
@P
@C
GIRL_DOWN_TALK
RET

*HIKIWAKE_TEXT
FACEOPEN(14)			;娘・りりしい
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
@C
TXA(R_NAME)は去っていった・・・
@P
FACECLOSE
RET
; -----（作者注：モジュール名が重複しているが処理上問題ない） 
*HIKIWAKE_TEXT
FACEOPEN(14)			;娘・りりしい
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
@C
TXA(R_NAME)は去っていった・・・
@P
FACECLOSE
RET

*FACEOPEN
C2 = AX
PTBK(1)
PTCLR
PTGT(2,148,16,140)
PTGT(62,148,16,140)
SI=R_FACE TXF(5,3,158)	; テキストウインドウ ﾌｪｲｽのみ
TXF(6,C2,63,158)		; テキストウインドウ 娘ﾌｪｲｽのみ
PTBK(0)
RET

PTBK(2)
PTLD("SSFACE")
SI = R_FACE
PTLA
PIC(14,P_NENREI,C2)	;娘の顔ファイル名
PTLA
PTM(0,86,0)
PTX(2,104,2)
PTM(64,86,0)
PTX(66,104,3)
PTBK(0)
RET

*FACECLOSE
PTBK(1)
PTF(0,0,0)
PTF(0,0,1)
PTBK(0)
RET

*GIRL_VIC_TALK
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
@C
GR_OPEN				;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN

;通常ﾀﾞﾒｰｼﾞ合計 : 魔法ﾀﾞﾒｰｼﾞ合計
IF ( E_ETC[100]>E_ETC[101] )	C4 = 1
IF ( E_ETC[100]<E_ETC[101] )	C4 = 2
IF ( E_ETC[100]=E_ETC[101] )
	IF ( H_SENSHI>=H_MAHO )		C4 = 1
	IF ( H_SENSHI< H_MAHO )		C4 = 2

IF ( C4 = 1 )	IGRP_TBL1[0]="戦士評価"	IGRP_TBL3[0]=H_SENSHI
IF ( C4 = 2 )	IGRP_TBL1[0]="魔法評価"	IGRP_TBL3[0]=H_MAHO
IGRP_CNT=1
IGRP_X=55
IGRP_Y=266
GRPOPEN(2,1)		;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
C1 = 15	;戦士評価が１５上がった．
IF ( C4 = 1 )
	TXA(NAME_FIRST)の戦士評価がTXPOINT(C1)上がった．
	H_SENSHI += C1			; 戦士評価
	GRPUPD(1,0,H_SENSHI)	; 戦士評価
IF ( C4 = 2 )
	TXA(NAME_FIRST)の魔法評価がTXPOINT(C1)上がった．
	H_MAHO += C1			; 魔法評価
	GRPUPD(1,0,H_MAHO)		; 魔法評価
@P
GR_CLOSE			;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ CLOSE
RET

*GIRL_DOWN_TALK
「＊＊＊＊＊＊＊＊＊＊＊」
@P
@C
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
@C
TXA(R_NAME)は道場の看板をはぎ取って去っていった．
FACECLOSE
KANBAN_CLEAR
@P
RET

*KANBAN_CLEAR
WWANIME(10,0,0,0,40,128)	;anime window size set.
WWANIME(3,0,"T005BX")		;pictuer load.
WWANIME(4,0)				;put BG.
WWANIME(5,0)				;pictuer copy vvram -> CRT.
RET


;******************
;	Txxx
;******************

*SE_TIME_GR_CHK
AX = 0
IF ( SLCANM=1 )		;通常攻撃
	IF ( ALCC[1]=4 ) AX = 1
IF ( SLCANM=2 )		;魔法攻撃
	IF ( GIRL_SHOT!0 ) AX = 1
RET

*ANIME_INIT
TIMEWAIT1=4;8
;待機
ALCX[0]	=256/8
ALCY[0]	=176-79
ALCC[0]	=0
ALCCNT[0]	=4
.FILM0[4]=1,1,2,2
;通常攻撃
ALCX[1]	=256/8
ALCY[1]	=176-79
ALCC[1]	=0
ALCCNT[1]	=6
.FILM1[6]=1,2, 3,4,4,4
;魔法攻撃
ALCX[2]	=256/8
ALCY[2]	=176-79
ALCC[2]	=0
ALCCNT[2]	=2
.FILM2[2]=10,11
;防御
ALCX[3]	=256/8
ALCY[3]	=176-79
ALCC[3]	=0
ALCCNT[3]	=1
.FILM3[1]=5
;勝ち
ALCX[4]	=256/8
ALCY[4]	=176-79
ALCC[4]	=0
ALCCNT[4]	=4
.FILM4[4]=7,7,6,6
;負け
ALCX[5]	=256/8
ALCY[5]	=176-79
ALCC[5]	=0
ALCCNT[5]	=4
.FILM5[4]=8,8,9,9
;火の玉
ALCX[6]	=216/8
ALCY[6]	=176-79
ALCC[6]	=0
ALCCNT[6]	=4
.FILM6[4]=0,0,12,13

BATLE_SET		;敵設定

RET

*ANIME_OPEN
;;WWANIME(14,0)				;anime window open.(no frame open)
WWANIME(16,0,20,110);154)	;anime window open.(location/no save)
WWANIME(3,0,"T005B")		;pictuer load.

C1 = 0								;43=普段着
IF ( EQUIP_ICON = 11 )	C1 = 1		;11=皮の鎧
IF ( EQUIP_ICON = 12 )	C1 = 2		;12=鎖帷子
IF ( EQUIP_ICON = 13 )	C1 = 3		;13=鉄の鎧
IF ( EQUIP_ICON = 14 )	C1 = 4		;14=ミスリルの鎧
IF ( EQUIP_ICON = 15 )	C1 = 5		;15=シルクの鎧

IF ( C1 = 0 ) WWANIME(6,0,"B001A")
IF ( C1 = 1 ) WWANIME(6,0,"B001B")
IF ( C1 = 2 ) WWANIME(6,0,"B001C")
IF ( C1 = 3 ) WWANIME(6,0,"B001D")
IF ( C1 = 4 ) WWANIME(6,0,"B001E")
IF ( C1 = 5 ) WWANIME(6,0,"B001F")

BATLE_LOAD
WWANIME(10,0,0,0,40,128)	;anime window size set.
WWANIME(4,0)				;put BG.
SLCATK=0					;0=待機
SLCANM=0					;0=待機
GIRL_BATLE					;娘アニメ
BATLE_PLAY					;敵アニメ
WWANIME(5,0)				;pictuer copy vvram -> CRT.
RET

;**************
;	娘データ
;**************

*CHECK_GIRL_DATA
W_HP_MAX = B_TAIRYOKU - S_BYOUKI		;最大ＨＰ 体力−病気指数
IF ( W_HP_MAX < 0 ) W_HP_MAX = 0
W_MP_MAX = B_CHINOH						;最大ＭＰ 知能
W_SENI = H_SENSHI						;戦士評価 -> 戦意
IF ( H_MAHO > H_SENSHI ) W_SENI= H_MAHO	;魔法評価 -> 戦意
W_HP = W_HP_MAX
W_MP = W_MP_MAX
					;娘に当たる(1=皮,2=鉄,3=体毛)
IF ( EQUIP_ICON = 11 )	GIRL_SE_ATTACK=1		;11=皮の鎧
IF ( EQUIP_ICON = 12 )	GIRL_SE_ATTACK=2		;12=鎖帷子
IF ( EQUIP_ICON = 13 )	GIRL_SE_ATTACK=2		;13=鉄の鎧
IF ( EQUIP_ICON = 14 )	GIRL_SE_ATTACK=2		;14=ミスリルの鎧
IF ( EQUIP_ICON = 15 )	GIRL_SE_ATTACK=3		;15=シルクの鎧
RET

;******************
;	SLCANM :
;		0=待機
;		1=通常攻撃
;		2=魔法攻撃
;		3=防御
;		4=勝ち
;		5=負け
;
;	SLCATK :
;		0=待機
;		1=通常攻撃
;		2=魔法攻撃
;		3=防御
;		4=勝ち
;		5=負け
;******************

*ANIME_PLAY
TIMELOPMAX = AX

;;WWANIME(10,0,2,97,50,80)		;anime window size set.高速化の為
;*;WWANIME(10,0,2,73,50,104)	;anime window size set.高速化の為("SSBT09.TXT")

FLAG_BATLE_EXIT = 0
FITER_SHOT = 0
GIRL_SHOT = 0
WAIT_MAGIC = 0
COUNT_ATTACK=0
BOUGYO_TK = 0

C1 = 6  C2 = 0
	ALCC[C2]=0
	ALCX[C2]=256/8
	ALCY[C2]=176-79
	C2++
LOOP C1

ALCC[6]=0
ALCX[6]=216/8
ALCY[6]=176-79

BATLE_PLAY_INIT

TIMELOP=TIMELOPMAX
	WWANIME(4,0)		;put BG.
	TIMER1(TIMEWAIT1)

	IF ( SLCANM=1 )		;通常攻撃
		APUTS_XY(SLCANM)
		APUTK_XY(SLCATK)
		C2 = GIRL_XX - FITER_XX
		IF ( GIRL_SHOT=0  C2<=LENG_ATTACK_GR )
			SLCATK = 3			;敵防御
			GIRL_SHOT = 1
			FITER_XX = GIRL_XX - LENG_ATTACK_GR
			APUTK_XY_WRT(SLCATK)

	IF ( SLCANM=2 )		;魔法攻撃
		SLCATK = 3			;敵防御
		APUTS_XY(6)
		APUTK_XY(SLCATK)
		C2 = GIRL_XX - FITER_XX
		IF ( GIRL_SHOT=0  C2<=LENG_MAGIC_GR )
			GIRL_SHOT = 1
			FITER_XX = GIRL_XX - LENG_MAGIC_GR
			APUTK_XY_WRT(SLCATK)

	IF ( SLCATK=1 )		;敵・通常攻撃
		APUTS_XY(SLCANM)
		APUTK_XY(SLCATK)
		C2 = GIRL_XX - FITER_XX
		IF ( FITER_SHOT=0  C2<=LENG_ATTACK_TK )
			SLCANM = 3			;防御
			FITER_SHOT = 1
			FITER_XX = GIRL_XX - LENG_ATTACK_TK
			APUTK_XY_WRT(SLCATK)

	IF ( SLCATK=2 )		;敵・魔法攻撃
		APUTS_XY(SLCANM)
		APUTK_XY(6)
		C2 = GIRL_XX - FITER_XX
		IF ( FITER_SHOT=0  C2<=LENG_MAGIC_TK )	;敵・魔法攻撃
			SLCANM = 3			;防御
			FITER_SHOT = 1
			GIRL_XX = FITER_XX + LENG_MAGIC_TK + 11
			APUTS_XY_WRT(SLCATK)

	IF ( SLCANM=1 )( SLCANM=2 )	;通常攻撃 魔法攻撃
		BATLE_PLAY			;敵アニメ
		GIRL_BATLE			;娘アニメ
	IF ( SLCANM!1  SLCANM!2 )
		GIRL_BATLE			;娘アニメ
		BATLE_PLAY			;敵アニメ

	WAIT1
	NOW_SE_SET
	WWANIME(5,0)		;pictuer copy vvram -> CRT.

	IF ( FLAG_BATLE_EXIT!0 ) TIMELOP=0
LOOP TIMELOP
RET

*NOW_SE_SET
SE_TIME_GR_CHK
IF ( AX=1 )
	IF ( BATLE_NOW=1 )
		IF ( FLAG_KAISHIN!0 )	;1=命中  会心ﾌﾗｸﾞ
			SE_KAISHIN
		IF ( FLAG_KAISHIN=0 )
			IF ( FLG_HABAM=0 )
				SE_TK_DAMEG
			IF ( FLG_HABAM!0 )
				SE_TK_DEFENS
	IF ( BATLE_NOW=2 )
		SE_TK_DEFENS
SE_TIME_TK_CHK
IF ( AX = 1 )
	IF ( BATLE_NOW=1 )
		IF ( FLAG_TSUKON!0 )	;1=命中  痛恨ﾌﾗｸﾞ
			SE_GR_DAMEG
		IF ( FLAG_TSUKON=0 )
			IF ( FLG_HABAM=0 )
				SE_TSUKON
			IF ( FLG_HABAM!0 )
				SE_GR_DEFENS
	IF ( BATLE_NOW=2 )
		SE_GR_DEFENS
RET

;**************
;	girl batle.
;**************

*GIRL_BATLE

IF ( SLCANM=1 )
	IF ( GIRL_SHOT!0 )
		IF ( COUNT_ATTACK = 0 ) ALCC[1]=2
		COUNT_ATTACK++

IF ( SLCANM=2 )
	IF ( ALCC[2] = 2 ) ALCC[2] = 1
	IF ( ALCC[6]>= 4 ) ALCC[6] = 2
	IF ( GIRL_SHOT=0 )
		APUTS(6)
		ALCX[6] -= 2
	IF ( GIRL_SHOT!0 )
		IF ( WAIT_MAGIC=0 )
			APUTS(6)
		WAIT_MAGIC++
		IF ( WAIT_MAGIC >=3 )
			FLAG_BATLE_EXIT=1

APUTS(SLCANM)

IF ( SLCANM=1 )
	IF ( GIRL_SHOT=0  ALCX[1] > C2 )
		ALCX[1]--
		IF ( ALCC[1] >= 2 ) ALCC[1] = 0
	IF ( ALCC[1]>=6 )
		FLAG_BATLE_EXIT=1
RET

*APUTS_XY
APUTNUM = AX
GIRL_XX = ALCX[APUTNUM]
GIRL_YY = ALCY[APUTNUM]
RET

*APUTS_XY_WRT
APUTNUM = AX
ALCX[APUTNUM] = GIRL_XX
ALCY[APUTNUM] = GIRL_YY
RET

*APUTS
APUTNUM = AX
ANIM_NUM(APUTNUM)
APUT(ALCX[APUTNUM],ALCY[APUTNUM],C1,OFST_GIRL)
ALCC[APUTNUM]++
RET

*ANIM_NUM
;ANIM_NUM(filmnumber)
;-O- DX pattern number.
IF ( ALCC[AX] >= ALCCNT[AX] )
	ALCC[AX] = 0
BX = ALCC[AX]
IF (AX=0) DX=FILM0[BX]
IF (AX=1) DX=FILM1[BX]
IF (AX=2) DX=FILM2[BX]
IF (AX=3) DX=FILM3[BX]
IF (AX=4) DX=FILM4[BX]
IF (AX=5) DX=FILM5[BX]
IF (AX=6) DX=FILM6[BX]
BATLE_ANIM_NUM			;anime number.
DX--	;DX pattern number.
C1 = DX
RET

*APUT
;anime pattern put vvram.
;APUT(x,y,pattern,offset)
IF ( CX < 0 ) RET
SI = CX * 2 + DX
DX = BX-61
CX = AX-7
AX = 7
BX = 0
;	WWANIME(7,0,ALCX[0],ALCY[0],ALCC[0])
WWANIME	;anime pattern put vvram.
RET

;**************
;**************

*STR_GIRL_NAMEX
SI = NAME_FIRST
STRCOPY(STR_WORK)
STRLEN(NAME_FIRST)
C2 = STR_WORK + AX
SI = "･"
STRCOPY(C2)
C2 = C2 + 1
SI = NAME_FAMILY
STRCOPY(C2)
TXF(4,STR_WORK,23)	;漢字文字詰め処理
SI = STR_WORK
RET

*STR_N_SHORT
SI = AX
STRCOPY(STR_WORK)
TXF(4,STR_WORK,23)	;漢字文字詰め処理
SI = STR_WORK
RET

;**************
;	戦闘モード
;	-O- CMN_VIC_FLG : 勝敗 1=娘勝ち,2=敵勝ち
;	    FLAG_BATLE  : 0=戦闘中,1=娘勝ち,2=敵勝ち,3=娘逃げる,4=敵逃げる
;**************

*BATLE_MODE			;戦闘モード
SLCPOS(2)
FLAG_VICTORY=0		;勝利フラグ
FLAG_BATLE = 0		;0=戦闘中
MY_KAISHIN = 3		;会心率(%)
IF ( E_KAISIN ! 0 )	;会心の一撃発生倍率２倍
	MY_KAISHIN = 6	;会心率(%)
BATL_COUNT = 0

TXF(11)			; 戦闘時ﾃｷｽﾄﾓｰﾄﾞ
BTL_LOOP
TXF(10)			; ﾉｰﾏﾙ時ﾃｷｽﾄﾓｰﾄﾞ

VICTORY_SET
RET

;**************
;	graph.
;**************

*GRAPSHOW
;GR_OPEN			;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN
GR_F(1,2)		;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN

STR_N_SHORT(R_NAME)
IGRP_X = 1
IGRP_Y = 296 - 38
GR_F(3)

IGRP_TBL1[0]="　ＨＰ"
IGRP_TBL1[1]="　ＭＰ"
IGRP_TBL1[2]="戦士評価"
IGRP_TBL1[3]="魔法評価"
IGRP_TBL1[4]="　戦意"
IGRP_TBL3[0]=R_HP			;ＨＰ
IGRP_TBL3[1]=R_MP			;ＭＰ
IGRP_TBL3[2]=R_SENSHI		; 戦士評価
IGRP_TBL3[3]=R_MAHO			; 魔法評価
IGRP_TBL3[4]=R_SENI			; 戦意
IGRP_CNT=5
IGRP_X=1
IGRP_Y=296
GRPOPEN(1,2)		;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ

STR_GIRL_NAMEX
IGRP_X = 55
IGRP_Y = 296 - 38
GR_F(3)

IGRP_TBL1[0]="　ＨＰ"
IGRP_TBL1[1]="　ＭＰ"
IGRP_TBL1[2]="戦士評価"
IGRP_TBL1[3]="魔法評価"
IGRP_TBL1[4]="　戦意"
IGRP_TBL3[0]=W_HP			;ＨＰ
IGRP_TBL3[1]=W_MP			;ＭＰ
IGRP_TBL3[2]=H_SENSHI		; 戦士評価
IGRP_TBL3[3]=H_MAHO			; 魔法評価
IGRP_TBL3[4]=W_SENI			; 戦意
IGRP_CNT=5
IGRP_X=55
IGRP_Y=296
GRPOPEN(1,3)		;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
RET

*DISP_HP
GRPUPD(2,0,R_HP)
GRPUPD(2,1,R_MP)
GRPUPD(2,4,R_SENI)
GRPUPD(3,0,W_HP)
GRPUPD(3,1,W_MP)
GRPUPD(3,4,W_SENI)
RET

*RDM
;RDM(start,end)
;-o- AX : randmize
C1 = AX
C2 = BX - AX + 1
;*;RANDAM(C2)
IRAND999X(C2)		;乱数９９９範囲指定
AX = IRND + C1 - 1
RET


;**************
;	戦闘モード
;	FLAG_BATLE 0=戦闘中,1=娘勝ち,2=敵勝ち,3=娘逃げる,4=敵逃げる
;**************

*BTL_LOOP

ATTAKE_GIRL
DISP_HP
IF ( FLAG_BATLE ! 0 ) RET

ATTAKE_MONSTOR
DISP_HP
IF ( FLAG_BATLE ! 0 ) RET

BATL_COUNT++
IF ( BATL_COUNT>=10 )
	FLAG_BATLE = 5	;5=引き分け
	RET

GOTO BTL_LOOP


*ATTAKE_GIRL
SLCTBAK(1)
SLCTX("攻撃する,魔法を使う")
IF ( ISLCT=1   W_MP < 10 )
	ＭＰが足りません．
	GOTO ATTAKE_GIRL

BATLE_GR_SLCT = ISLCT + 1

SSBATL_TAIKI

IF ( BATLE_GR_SLCT=1 )		;1=通常攻撃
	GIRL_ATTAKE
	TEKI_KAISHIN_DMG
	SSBATL_GR_ATTACK
	TXA(NAME_FIRST)の攻撃！
	PPP1
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(V_KOUGEKI,R_BOUGYO)
		TEKI_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		TEKI_DAMEG_NONE
	RET
IF ( BATLE_GR_SLCT=2 )		;2=魔法攻撃
	IF ( E_MAX_MP=0 )		;ＭＰが減らない
		W_MP -= 10
	GIRL_MAGIC
	TEKI_KAISHIN_DMG
	SSBATL_GR_MAGIC
	魔法攻撃をしかけた！！
	PPP1
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(V_MARYOKU,R_KOHMARYOKU)
		TEKI_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		TEKI_DAMEG_NONE
	RET
RET

*ATTAKE_MONSTOR
;魔力を持っている敵は，《現在のＭＰ：現在のＨＰ》の比率で魔法
C1 = R_HP + R_MP		;
P100PAR(R_MP,C1)		;百分率を求める
C2 = AX					;parcent.
RANDAM(100)				;

IF ( C2 >= IRND )
	BATLE_TK_SLCT=2			;2=魔法攻撃
IF ( C2 < IRND )( R_MP < 10 )
	BATLE_TK_SLCT=1			;1=通常攻撃

SSBATL_TAIKI

IF ( BATLE_TK_SLCT=1 )		;1=通常攻撃
	TEKI_ATTAKE
	GIRL_TSUKON_DMG
	SSBATL_TK_ATTACK
	TXA(R_NAME)の攻撃！
	PPP1
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(R_KOUGEKI,V_BOUGYO)
		GIRL_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		GIRL_DAMEG_NONE
	RET

IF ( BATLE_TK_SLCT=2 )		;2=魔法攻撃
	R_MP -= 10
	TEKI_MAGIC
	GIRL_TSUKON_DMG
	SSBATL_TK_MAGIC
	魔法攻撃を受けた！！
	PPP1
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(R_MARYOKU,V_KOHMARYOKU)
		GIRL_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		GIRL_DAMEG_NONE
	RET
RET

*GIRL_ATTAKE
CALC_ATTACK(V_SENTOH)	ATTACK_VAL=AX
CALC_DEFENS(R_SENTOH)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*GIRL_MAGIC
TXA(NAME_FIRST)は呪文を唱えた．
CALC_ATTACK(V_MAHOGIJUTSU)	ATTACK_VAL=AX
CALC_DEFENS(R_MAHOGIJUTSU)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*TEKI_ATTAKE
CALC_ATTACK(R_SENTOH)	ATTACK_VAL=AX
CALC_DEFENS(V_SENTOH)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*TEKI_MAGIC
TXA(R_NAME)は呪文を唱えた！
CALC_ATTACK(R_MAHOGIJUTSU)	ATTACK_VAL=AX
CALC_DEFENS(V_MAHOGIJUTSU)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*TEKI_KAISHIN_DMG
FLAG_KAISHIN=0			;会心ﾌﾗｸﾞ
RANDAM(100)
IF ( MY_KAISHIN >=IRND )	;会心率(%)
	FLAG_KAISHIN=1
RET

*TEKI_DAMEG
T_DAMEG = AX
IF ( FLAG_KAISHIN!0 )
	T_DAMEG = T_DAMEG * 3
	会心の一撃！@@
	PPP1
IF ( FLG_HABAM=0 )
	命中！TXA(R_NAME)にTXPOINT(T_DAMEG)のダメージを与えた！
IF ( FLG_HABAM!0 )
	IF ( R_SOUKOU > 0 )		;装甲
		命中！しかし，TXA(R_NAME)のTXA(R_SKNAME)に阻まれた！
	IF ( R_SOUKOU = 0 )		;装甲
		TXA(R_NAME)は攻撃を受けとめた！
	TXA(R_NAME)はTXPOINT(T_DAMEG)のダメージ．
PPP1
TEKI_DAMEG_CHK
RET

*TEKI_DAMEG_NONE
TXA(R_NAME)は攻撃を回避した！
PPP1
RET

*TEKI_DAMEG_CHK
R_HP -= T_DAMEG
IF ( BATLE_GR_SLCT=1 )	E_ETC[100]+=T_DAMEG		;通常攻撃時ﾀﾞﾒｰｼﾞ合計
IF ( BATLE_GR_SLCT=2 )	E_ETC[101]+=T_DAMEG		;魔法攻撃時ﾀﾞﾒｰｼﾞ合計
IF ( R_HP <= 0 )
	R_HP = 0
	FLAG_BATLE=1	;1=娘勝ち
	RET
R_SENI -= T_DAMEG	;戦意
IF ( R_SENI <= 0 )
	R_SENI = 0
	FLAG_BATLE=4	;4=敵逃げる
RET

*GIRL_TSUKON_DMG
FLAG_TSUKON=0			;痛恨ﾌﾗｸﾞ
RANDAM(100)
IF ( R_TSUKON >=IRND )	;痛恨率(%)
	FLAG_TSUKON=1
RET

*GIRL_DAMEG
G_DAMEG = AX
IF ( FLAG_TSUKON!0 )		;痛恨ﾌﾗｸﾞ
	G_DAMEG = G_DAMEG * 3
	痛恨の一撃！@@
	PPP1
IF ( FLG_HABAM=0 )
	命中！TXA(NAME_FIRST)はTXPOINT(G_DAMEG)のダメージを受けた！
IF ( FLG_HABAM!0 )
	IF ( EQUIP_ICON ! 0 )
		命中！しかし，TXA(NAME_FIRST)のTXA(EQUIP_NAME)に阻まれた！
	IF ( EQUIP_ICON = 0 )
		TXA(NAME_FIRST)は攻撃を受けとめた！
	TXA(NAME_FIRST)はTXPOINT(G_DAMEG)のダメージ．
PPP1
GIRL_DAMEG_CHK
RET

*GIRL_DAMEG_NONE
TXA(NAME_FIRST)は巧みに回避した！
PPP1
RET

*GIRL_DAMEG_CHK
W_HP -= G_DAMEG		;ＨＰ
IF ( W_HP <= 0 )
	W_HP = 0
	FLAG_BATLE=2	;2=敵勝ち
	RET
W_SENI -= G_DAMEG	;戦意
IF ( W_SENI <= 0 )
	W_SENI = 0
	FLAG_BATLE=3	;3=娘逃げる
RET

*CALC_ATTACK	;-I-戦闘技術 -O- AX : 攻撃バリュー
;AX=AX+-30%randam
C1 = AX
IRNDPAR(C1,30)
RET

*CALC_DEFENS	;-I-戦闘技術 -O- AX : 攻撃バリュー
;AX=AX+-30%randam
C1 = AX
IRNDPAR(C1,30)
RET

*CALC_DAMEG		;-I-攻撃力,防御側の防御力 -O- AX : ダメージ
;AX = AX+-20%randam - BX
FLG_HABAM=0
C1 = AX
C2 = BX
IRNDPAR(C1,20)
AX = AX - C2
IF ( AX < 0 )
	FLG_HABAM=1
	AX = 1
RET

*PPP1
TIMER1(10)
WAIT1
RET

*PPP2
@P
RET

*SE_TK_DEFENS
IF(R_SE_ATTACK=1)MUSIC(6,7)	;1=皮
IF(R_SE_ATTACK=2)MUSIC(6,8)	;2=鉄
IF(R_SE_ATTACK=3)MUSIC(6,9)	;3=体毛
IF(R_SE_ATTACK=4)MUSIC(6,10);4=固い体
RET

*SE_GR_DEFENS
IF(GIRL_SE_ATTACK=0)MUSIC(6,9)	;3=体毛
IF(GIRL_SE_ATTACK=1)MUSIC(6,7)	;1=皮
IF(GIRL_SE_ATTACK=2)MUSIC(6,8)	;2=鉄
IF(GIRL_SE_ATTACK=3)MUSIC(6,9)	;3=体毛
RET

*SE_MAGIC
MUSIC(6,4)
RET

*SE_TK_DAMEG
MUSIC(6,11)
RET

*SE_GR_DAMEG
MUSIC(6,12)
RET

*SE_KAISHIN
MUSIC(6,13)
RET

*SE_TSUKON
MUSIC(6,14)
RET

*SE_HOUROKU
MUSIC(6,16)
RET

;**************
;	victory set.
;	-I- FLAG_BATLE  : 0=戦闘中,1=娘勝ち,2=敵勝ち,3=娘逃げる,4=敵逃げる
;	-O- CMN_VIC_FLG : 勝敗 1=娘勝ち,2=敵勝ち
;**************

*VICTORY_SET
IF ( FLAG_BATLE = 1 )( FLAG_BATLE = 4 )		;1=娘勝ち,4=敵逃げる
	CMN_VIC_FLG = 1			;勝敗 1=娘勝ち
IF ( FLAG_BATLE = 2 )( FLAG_BATLE = 3 )		;2=敵勝ち,3=娘逃げる
	CMN_VIC_FLG = 2			;勝敗 2=敵勝ち
RET

;**************
;	娘フレーム表示
;**************

*FREAM_GIRL
EFC(1)				; Ｆ・Ｏ（フェードアウト）
WWFRAME(1)			;
CLENDER(1)			;
WWPROF(1)			:
WWGIRL(3)			; girl quick put.
WWPROF(2)			;ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・再表示
EFC(16)				; ノーマルカラー
RET

*APUTSBG
RET

;
;	end of "SSEV023.TXT"
;
