;"RPGMNS.TXT"
;
;	モンスターバトル
;
;
;	URA_KILL_MONSTR++			; モンスター殺戮数
;
;
;武神は特別です
;E_ETC[31]=1			;武神に勝った
;

STRSCLEAR

.FLAG_VICTORY	;勝利フラグ
.FLAG_KAIWA_OK	;会話条件成立
.FLAG_BATLE = 0	;0=戦闘中,1=娘勝ち,2=敵勝ち,3=娘逃げる,4=敵逃げる
.G_DAMEG
.T_DAMEG
.REC_TBL[50]
.TOOL_BUKI

.ATTACK_VAL		;攻撃バリュー
.DEFENS_VAL		;防御バリュー
.BATLE_NOW		;1=命中,2=失敗

.MY_KAISHIN		;会心率(%)
.FLAG_KAISHIN=0	;会心ﾌﾗｸﾞ
.FLAG_TSUKON=0	;痛恨ﾌﾗｸﾞ
.BATL_COUNT	=0
.FLG_HABAM	=0

.R_HP		=0	;ＨＰ
.R_MP		=0	;ＭＰ

.MNSTR_X
.MNSTR_Y

.R_MS_NUMBER	=0	;モンスター番号
.R_FACE			=0	;フェイス
.R_MONSTOR		=0	;モンスター
.R_NAME			=0	;名前
.R_CHINOH		=0	;知能
.R_SENTOH		=0	;戦闘技術
.R_KOUGEKI		=0	;攻撃力
.R_BOUGYO		=0	;防御力
.R_SOUKOU		=0	;装甲
.R_SKNAME		=0	;装甲名
.R_MAHOGIJUTSU	=0	;魔法技術
.R_MARYOKU		=0	;魔力
.R_KOHMARYOKU	=0	;抗魔力
.R_SENI			=0	;戦意
.R_DASSYUTU		=0	;脱出率(%)
.R_TSUKON		=0	;痛恨率(%)
.R_MAX_HP		=0	;最大ＨＰ
.R_MAX_MP		=0	;最大ＭＰ
.R_GOLD			=0	;所持金

.R_SE_ATTACK	=0	;剣の当たる(1=皮,2=鉄,3=体毛,4=固い体)
.GIRL_SE_ATTACK	=0	;娘に当たる(1=皮,2=鉄,3=体毛)

.R_ITEM_ICON	=0	;所持アイテムＩＣＯＮ番号
.R_ITEM_GOLD	=0	;所持アイテム値段
.R_ITEM_ATTR	=0	;所有アイテム種別
.R_ITEM_NAME	=0	;所持アイテム名前
.R_ITEM_PROG	=0	;所持アイテム効果
.R_ITEM_HELP	=0	;所持アイテム説明
.R_ITEM_PROPA	=0	;アイテム確率(%)

;−モンスター用（６種類）−
.MONSTR[6]
.M_ATTCK	=0	;１威嚇
.M_FRNDLY	=1	;２友好
.M_ADVICE	=2	;３情報
.M_MISS		=3	;４会話失敗
.M_VICTRY	=4	;５勝利宣言
.M_DEFEAT	=5	;６敗北宣言

.S_WORKS			;スケジュール内容
.S_COUNTER			;当日カウンタ
.S_STARTDAY			;スケジュール開始日
.S_ENDDAY			;スケジュール終了日
.TODAY_COUNT		;日付カウンタ
.DAYLIGHT			;1=day,2=night,3=day->night,4=night->day.
.SOUBI_FLG			;装備フラグ
.SOUBI_REC			;装備のアイテム番号
.SOUBI_DAY			;装備の日

S_WORKS		=	BX	;スケジュール内容
S_COUNTER	=	CX	;当日カウンタ
S_STARTDAY	=	C1	;スケジュール開始日
S_ENDDAY	=	C2	;スケジュール終了日
TODAY_COUNT	=	C3	;日付カウンタ
DAYLIGHT	=	C4	;1=day,2=night,3=day->night,4=night->day.
SOUBI_FLG	=	C5	;装備フラグ
SOUBI_REC	=	C6	;装備のアイテム番号
SOUBI_DAY	=	C7	;装備の日

R_ITEM_NAME =""
R_ITEM_PROG	=""
R_ITEM_HELP	=""

BTLE_MAIN			;balte main.

IF ( R_MS_NUMBER ! 7 )	;"武神"を除く
	WWMUSYA(9)			;戦闘後データロード
	IF ( R_MS_NUMBER ! 16 )		;"サンドレイダース"を除く
		WWMUSYA(8,1)			;再描画
	IF ( R_MS_NUMBER = 16 )		;"サンドレイダース"
		EFC(1)		; Ｆ・Ｏ（フェードアウト）
		GRPCLS
		WWFRAME(2)			; 2=武者修行
		WWPROF(5)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･所持金
		WWPROF(6)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･HP
		WWPROF(7)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･MP
		WWPROF(8)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･装備
		CLENDER(6,62,0)		; カレンダー・セットアップ
		CLENDER(1)			; カレンダー・表示
		WWMUSYA(9)			; 戦闘後データロード
		WWMUSYA(5,105,75,13,10,1)	; map relocate. 
		WWMUSYA(18,DAYLIGHT); 昼<->夜操作
		WWMUSYA(8,1)		; 再描画
		WWMUSYA(4,2)		; effect.2=穴から
	BX = S_WORKS		;スケジュール内容
	CX = S_COUNTER		;当日カウンタ
	C1 = S_STARTDAY		;スケジュール開始日
	C2 = S_ENDDAY		;スケジュール終了日
	C3 = TODAY_COUNT	;日付カウンタ
	C4 = DAYLIGHT		;1=day,2=night,3=day->night,4=night->day.
	C5 = SOUBI_FLG		;装備フラグ
	C6 = SOUBI_REC		;装備のアイテム番号
	C7 = SOUBI_DAY		;装備の日
	IF ( S_WORKS = 0 )	LOAD("RPGLOP,RPG000")		;0=東部 森林地帯
	IF ( S_WORKS = 1 )	LOAD("RPGLOP,RPG001")		;1=西部 砂漠地帯
	IF ( S_WORKS = 2 )	LOAD("RPGLOP,RPG002")		;2=南部 水郷地帯
	IF ( S_WORKS = 3 )	LOAD("RPGLOP,RPG003")		;3=北部 氷山地帯

IF ( FLAG_BATLE = 1 )( FLAG_BATLE = 4 )	;1=娘勝ち,2=敵勝ち,3=娘逃げる,4=敵逃げる
	E_ETC[31]=1			;武神に勝った
	WWMUSYA(9)			;戦闘後データロード
	WWMUSYA(8,1)		;再描画
	BX = S_WORKS		;スケジュール内容
	CX = S_COUNTER		;当日カウンタ
	C1 = S_STARTDAY		;スケジュール開始日
	C2 = S_ENDDAY		;スケジュール終了日
	C3 = TODAY_COUNT	;日付カウンタ
	C4 = DAYLIGHT		;1=day,2=night,3=day->night,4=night->day.
	C5 = SOUBI_FLG		;装備フラグ
	C6 = SOUBI_REC		;装備のアイテム番号
	C7 = SOUBI_DAY		;装備の日
	LOAD("RPGLOP,RPG003")		;3=北部 氷山地帯

PTBK(4)						;パーツバンク設定
PTLD("CIPBSN")				;パーツロード
WWMUSYA(9)					;戦闘後データロード
WWMUSYA(8,1)				;再描画
PTMFX(35,152,0,0,0,6,64)	;ﾏｽｸﾊﾟﾀｰﾝ座標ｵﾌｾｯﾄ範囲PUT
PTBK(0)						;パーツバンク設定

WWMUSYA(12,0)				;娘の向き設定 0=front.1=back,2=left,3=right.
;;WWMUSYA(13,0,1,0,0,27)	;マップシフト
;;WWMUSYA(13,0,2,0,0,13)	;マップシフト
;;WWMUSYA(13,0,1,0,0,3)		;マップシフト
;;WWMUSYA(13,0,0,0,1,6)		;マップシフト
WWMUSYA(4,15)				;武神から逃げる
WWMUSYA(4,9)				;effect.

IF ( FLAG_BATLE = 3 )		;3=娘逃げる,4=敵逃げる
	TXOPEN(7)
	TXA(NAME_FIRST)は逃げた．
	@P
	TXCLOSE(7)

;;WWMUSYA(4,9)					;effect.
WWMUSYA(10,5,63)				;マップ再ロード
EFC(1)							;Ｆ・Ｏ（フェードアウト）
WWMUSYA(5,3,0,13,0,2)			;map relocate. 
IF ( DAYLIGHT=1 )( DAYLIGHT=4 )	;1=day,2=night,3=day->night,4=night->day.
	WWMUSYA(18,1)				;昼<->夜操作 1=day,2=night
IF ( DAYLIGHT=2 )( DAYLIGHT=3 )	;1=day,2=night,3=day->night,4=night->day.
	WWMUSYA(18,2)				;昼<->夜操作 1=day,2=night

WWMUSYA(4,10)					;effect.
WWMUSYA(6)						;ＲＰＧモード


BX = S_WORKS		;スケジュール内容
CX = S_COUNTER		;当日カウンタ
C1 = S_STARTDAY		;スケジュール開始日
C2 = S_ENDDAY		;スケジュール終了日
C3 = TODAY_COUNT	;日付カウンタ
C4 = DAYLIGHT		;1=day,2=night,3=day->night,4=night->day.
C5 = SOUBI_FLG		;装備フラグ
C6 = SOUBI_REC		;装備のアイテム番号
C7 = SOUBI_DAY		;装備の日

LOAD("RPGLOP,RPG003")	;3=北部 氷山地帯

TXOPEN(1)致命的エラーWAITU

;**************
;	back to home load.
;**************

*BACK_TO_HOME

PTBK(0)					;パーツバンク設定

BX = S_WORKS		;スケジュール内容
CX = S_COUNTER		;当日カウンタ
C1 = S_STARTDAY		;スケジュール開始日
C2 = S_ENDDAY		;スケジュール終了日
C3 = TODAY_COUNT	;日付カウンタ
C4 = DAYLIGHT		;1=day,2=night,3=day->night,4=night->day.
C5 = SOUBI_FLG		;装備フラグ
C6 = SOUBI_REC		;装備のアイテム番号
C7 = SOUBI_DAY		;装備の日

LOAD("RPGDWN")

;***************************************
;
;	balte main.
;
;***************************************

*BTLE_MAIN
CHECK_MONSTOR			;monstor check.
CHECK_GIRL

PLAY(15)				;P15 警戒

IF ( R_MS_NUMBER = 7 )	;"武神"
	WAIT1				;間を置く "EVNTBSIN.TXT"から

ENCOUNT_MONSTOR			;モンスターとエンカウントした場合
RET

;**************
;	敗戦処理
;**************

*GIRL_DOWN_CHK			;敗戦チェック
URA_STEWARD += 2		; 執事との関係
IF ( URA_STEWARD>100 ) URA_STEWARD=100	; 執事との関係
TXLC(1,14,280,0)		;text window locate.
TXLC(2,28,260,1)		;text window locate.
TXOPEN(2)				;
TXFACE("F046")			;執事・困り
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
IF ( R_MS_NUMBER ! 16 )		;"サンドレイダース"
	WWMUSYA(9)				;戦闘後データロード
	@P
	WWMUSYA(8,2)			;再描画
IF ( R_MS_NUMBER = 16 )		;"サンドレイダース"
	@P
	GRPCLS
TXOPEN(1)				;
WWIVENT(5,0,16,32)		;ivent window locate.
;WWIVENT(1,0,"E024")	;負けて家に帰る
WWIVENT(6,0,2,4,"E024")	;save#2,load#4 ivent window open.(select bank)
TXA(NAME_FIRST)が気を失っている間に，執事はTXA(NAME_FIRST)を家に連れて帰った・・・・・・
@P						;
TXCLOSE(1)				;
IF ( P_TAIJYU < 4000 )	;体重が４０ｋｇ未満の場合
	TXOPEN(2)
	TXFACE("F039")		;執事・普通
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(2)
IF ( P_TAIJYU >= 6000 )	;体重が６０ｋｇ以上の場合
	TXOPEN(2)
	TXFACE("F046")		;執事・困り
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(2)
;WWIVENT(2,0)			;ivent window close.
WWIVENT(7,0,2)			;ivent window close.(select bank)
BACK_TO_HOME
RET


;**************
;	モンスターとエンカウントした場合
;**************

*ENCOUNT_MONSTOR
MNSTR_X=20
MNSTR_Y=20
TXLC(2,28,50,1)			;text window locate.
TXLC(3,13,200,2)		;text window locate.

IF ( R_MS_NUMBER ! 7 )			;"武神"を除く
	IF ( R_MS_NUMBER ! 16 )		;"サンドレイダース"を除く
		TXOPEN(3)
		TXGIRL(17)		;娘・真剣
		「＊＊＊＊＊＊＊＊」
		WWMUSYA(7)				;ノーマルモード
		TXF(2)					;マウスOFF待ち
		@P
		TXCLOSE(3)

;モンスターグラフィック表示

IF ( R_MS_NUMBER ! 16 )		;"サンドレイダース"を除く
	PTBK(4)					;パーツバンク設定
	PTMD(0)					;ＰＵＴモード設定
	SI=R_MONSTOR PTLD		;パーツロード
	PTMF(MNSTR_X,MNSTR_Y,0)	;ﾏｽｸﾊﾟﾀｰﾝ座標ｵﾌｾｯﾄPUT
	PTBK(0)					;パーツバンク設定
	TXOPEN(1)
	TXA(R_NAME)が現れた！どうしよう？
IF ( R_MS_NUMBER = 16 )		;"サンドレイダース"
	WWMUSYA(3,6)		; 6=ﾊﾟﾚｯﾄ割込みOFF
	EFC(1)		; Ｆ・Ｏ（フェードアウト）
	WWMUSYA(7)				;ノーマルモード
	GRPCLS
	CLENDER(6,62,0)		; カレンダー・セットアップ
	CLENDER(1)			; カレンダー・表示
	WWFRAME(50)				; 50=RPG用HPMP
	WWPROF(5)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･所持金
	WWPROF(6)				; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･HP
	WWPROF(7)				; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･MP
	WWPROF(8)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･装備
	WWMUSYA(18,4)			; 昼<->夜操作 4=night->day.
	EFC(16)					; ノーマルカラー
	PTBK(4)					;パーツバンク設定
	PTMD(0)					;ＰＵＴモード設定
	SI=R_MONSTOR PTLD		;パーツロード
	PTMF(MNSTR_X,MNSTR_Y,0)	;ﾏｽｸﾊﾟﾀｰﾝ座標ｵﾌｾｯﾄPUT
	PTBK(0)					;パーツバンク設定
	TXOPEN(1)
	TXA(R_NAME)が現れた！どうしよう？
	TXF(2)					;マウスOFF待ち
SLCPOS(1)
IF ( R_MS_NUMBER ! 7 )		;"武神"を除く
	SLCTX("戦う,話しかける,逃げる,隠れる")
IF ( R_MS_NUMBER = 7 )		;"武神"
	SLCTX("戦う,話しかける,逃げる")

*ENCOUNT_RETRY
IF (ISLCT=0)	;戦う
	TXA(NAME_FIRST)はTXA(R_NAME)に戦いを挑んだ！
	@P
	TXCLOSE(1)
	TXOPEN(2)
	SI=R_FACE TXFACE	;モンスター・普通
	「TXA(MONSTR[M_ATTCK])」	;威嚇の台詞
	@P
	TXCLOSE(2)
	BATLE_MODE		;戦闘モード
	PLAY(14)		;P14 武者修行
	RET
IF (ISLCT=1)		;話しかける
	TXA(NAME_FIRST)はTXA(R_NAME)に話しかけてみた．
	@P
	TXCLOSE(1)
	KAIWA_OK		;会話条件チェック
	FLAG_KAIWA_OK = AX
	IF ( FLAG_KAIWA_OK=1 )	;会話成立の場合
		TXOPEN(2)
		SI=R_FACE TXFACE	;モンスター・普通
		RANDAM(100)
		IF (IRND<=80)					;８０％の確率
			「TXA(MONSTR[M_FRNDLY])」	;２友好
		IF (IRND>80)					;２０％の確率
			「TXA(MONSTR[M_ADVICE])」	;３情報
		MUSIC(2)						;音楽ﾌｪｰﾄﾞｱｳﾄ
		@P
		TXCLOSE(2)
		PLAY(14)			;P14 武者修行
		RET
	;会話不成立の場合
	TXOPEN(2)
	SI=R_FACE TXFACE	;モンスター・普通
	「TXA(MONSTR[M_MISS])」	;４会話失敗
	@P
	TXCLOSE(2)
	TXOPEN(1)
	しかし，TXA(R_NAME)との間に会話は成り立たなかった．
	戦いだ！
	@P
	TXCLOSE(1)
	BATLE_MODE		;戦闘モード
	PLAY(14)		;P14 武者修行
	RET
IF (ISLCT=2)	;逃げる
	IF ( R_MS_NUMBER = 7 )		;"武神"を除く
		TXCLOSE(1)
		PLAY(14)			;P14 武者修行
		RET
	TXA(NAME_FIRST)は一目散に逃げ出した．
	@P
	RANDAM(100)
	IF ( R_DASSYUTU>=IRND )		;脱出に成功した
		なんとか逃げきったようだ．
		@P
		TXCLOSE(1)
		TXOPEN(3)
		TXGIRL(11)	;娘・不安
		「＊＊＊＊＊＊＊＊＊＊」
		MUSIC(2)			;音楽ﾌｪｰﾄﾞｱｳﾄ
		@P
		TXCLOSE(3)
		PLAY(14)			;P14 武者修行
		RET
	;脱出に失敗した場合
	@C
	しかし，敵に回り込まれてしまった．
	逃げられない！
	@P
	TXCLOSE(1)
	BATLE_MODE		;戦闘モード
	PLAY(14)		;P14 武者修行
	RET
IF (ISLCT=3)	;隠れる
	TXA(NAME_FIRST)はとっさに身を隠した・・・・・・
	@P

	;《（１００÷（娘の知能＋モンスターの知能））×娘の知能》
	;の確率で隠れるのに成功

	C1 = B_CHINOH + R_CHINOH
	P100PAR(B_CHINOH,C1)	;百分率
	C1 = AX
	RANDAM(100)
	C2 = R_CHINOH * 3
	IF ( C1 >= IRND )( B_CHINOH >= C2 )		;成功
		愚かなTXA(R_NAME)は，TXA(NAME_FIRST)に気づかず，通りすぎて行った．
		MUSIC(2)			;音楽ﾌｪｰﾄﾞｱｳﾄ
		@P
		TXCLOSE(1)
		PLAY(14)			;P14 武者修行
		RET
	;失敗した場合
	TXA(NAME_FIRST)はTXA(R_NAME)に見つかってしまった！
	どうしよう・・・・・・？
	SLCTX("戦う,話しかける,逃げる")
	GOTO ENCOUNT_RETRY
RET

;**************
;	graph.
;**************

*GRAPSHOW
;GR_OPEN			;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN
GR_F(1,2)			;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN

SI = R_NAME
IGRP_X = 1
IGRP_Y = 248
GR_F(3)
C1 = R_SENTOH + R_KOUGEKI + R_BOUGYO			;戦士評価は《戦闘技術＋攻撃力＋防御力》
C2 = R_MAHOGIJUTSU + R_MARYOKU + R_KOHMARYOKU	;魔法評価は《魔法技術＋魔力＋抗魔力》
IGRP_TBL1[0]="ＨＰ"
IGRP_TBL1[1]="ＭＰ"
IGRP_TBL1[2]="戦士評価"
IGRP_TBL1[3]="魔法評価"
IGRP_TBL1[4]="戦意"
IGRP_TBL3[0]=R_HP			;ＨＰ
IGRP_TBL3[1]=R_MP			;ＭＰ
IGRP_TBL3[2]=C1				;戦闘評価
IGRP_TBL3[3]=C2				;魔法評価
IGRP_TBL3[4]=R_SENI			;戦意
IGRP_CNT=5
IGRP_X=1
IGRP_Y=286
GRPOPEN(1,2)		;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ

SI = NAME_FIRST
IGRP_X = 55
IGRP_Y = 248
GR_F(3)

IGRP_TBL1[0]="ＨＰ"
IGRP_TBL1[1]="ＭＰ"
IGRP_TBL1[2]="戦士評価"
IGRP_TBL1[3]="魔法評価"
IGRP_TBL1[4]="戦意"
IGRP_TBL3[0]=W_HP			;ＨＰ
IGRP_TBL3[1]=W_MP			;ＭＰ
IGRP_TBL3[2]=H_SENSHI		;戦士評価
IGRP_TBL3[3]=H_MAHO			;魔法評価
IGRP_TBL3[4]=W_SENI			;戦意
IGRP_CNT=5
IGRP_X=55
IGRP_Y=286
GRPOPEN(1,3)		;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
RET

*DISP_HP
GRPUPD(2,0,R_HP)
GRPUPD(2,1,R_MP)
GRPUPD(2,4,R_SENI)
GRPUPD(3,0,W_HP)
GRPUPD(3,1,W_MP)
GRPUPD(3,4,W_SENI)
WWPROF(6)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･HP
WWPROF(7)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･MP
RET

*RDM
;RDM(start,end)
;-o- AX : randmize
C1 = AX
C2 = BX - AX + 1
;*;RANDAM(C2)
IRAND999X(C2)		;乱数９９９範囲指定
AX = IRND + C1 - 1
RET


;**************
;	戦闘モード
;**************

*BATLE_MODE			;戦闘モード
PLAY(17)			;P17 戦闘

SLCPOS(2)
R_HP = R_MAX_HP		;最大ＨＰ
R_MP = R_MAX_MP		;最大ＭＰ
FLAG_VICTORY=0		;勝利フラグ
FLAG_BATLE = 0		;0=戦闘中
MY_KAISHIN = 3		;会心率(%)
IF ( E_KAISIN ! 0 )	;会心の一撃発生倍率２倍
	MY_KAISHIN = 6	;会心率(%)
BATL_COUNT = 0

GRAPSHOW
TXLC(7,26,284,0)	;text window locate.
TXOPEN(7)
戦闘開始
@P

TXF(11)			; 戦闘時ﾃｷｽﾄﾓｰﾄﾞ
BTL_LOOP
TXF(10)			; ﾉｰﾏﾙ時ﾃｷｽﾄﾓｰﾄﾞ

GR_CLOSE		;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ CLOSE

MUSIC(2)			;音楽ﾌｪｰﾄﾞｱｳﾄ

IF ( FLAG_BATLE=1 )		;1=娘勝ち
	IF ( R_MS_NUMBER = 16 )		;"サンドレイダース"
		E_ETC[33] = 1			;武者修行中に１度ｻﾝﾄﾞﾚｲﾀﾞｰｽを倒した
	TXA(NAME_FIRST)はTXA(R_NAME)を倒した．
	@P
	TXCLOSE(7)
	PLAY(14)			;P14 武者修行
	TXOPEN(2)
	SI=R_FACE TXFACE	;モンスター・普通
	「TXA(MONSTR[M_DEFEAT])」		;６敗北宣言

	GR_OPEN			; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN
	IGRP_TBL1[0]="因業"
	IGRP_TBL3[0]=B_INGYO
	IGRP_CNT=1
	IGRP_X=35
	IGRP_Y=324;194
	GRPOPEN(1,1)	; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
	B_INGYO   += 3	; 因業
	GRPUPD(1,0,B_INGYO)
	@P
	GR_CLOSE		; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ CLOSE
	FLAG_VICTORY=1		;勝利フラグ
	TXCLOSE(2)
	GIRL_VICTORY_CHK	;勝利チェック
	URA_KILL_MONSTR++			; モンスター殺戮数
	RET
IF ( FLAG_BATLE=2 )		;2=敵勝ち
	TXA(NAME_FIRST)は負けた．
	@P
	TXCLOSE(7)
	PLAY(14)			;P14 武者修行
	TXOPEN(2)
	SI=R_FACE TXFACE	;モンスター・普通
	「TXA(MONSTR[M_VICTRY])」		;５勝利宣言
	@P
	TXCLOSE(2)
	GIRL_DOWN_CHK		;敗戦チェック
	RET
IF ( FLAG_BATLE=3 )		;3=娘逃げる
	IF ( R_MS_NUMBER = 7 )	;"武神"を除く
		TXCLOSE(7)
		PLAY(14)			;P14 武者修行
		RET
	なんとか逃げきったようだ．
	@P
	TXCLOSE(7)
	PLAY(14)			;P14 武者修行
	TXOPEN(3)
	TXGIRL(11)	;娘・不安
	「＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(3)
	RET
IF ( FLAG_BATLE=4 )		;4=敵逃げる
	;;TXA(R_NAME)は逃げた．
	TXA(R_NAME)は去っていった・・・
	@P
	TXCLOSE(7)
	RET
RET


;**************
;	勝利チェック
;**************

*GIRL_VICTORY_CHK	;勝利チェック
IF ( R_GOLD > 0 )
	TXOPEN(1)
	金TXGOLD(R_GOLD)を手に入れた．
	GOLDADD(R_GOLD)		;娘の所持金
	WWPROF(5)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･所持金

IF ( R_ITEM_ICON ! 0 )				;所持アイテムＩＣＯＮ番号
	RANDAM(100)						;
	IF ( R_ITEM_PROPA >= IRND )		;アイテム確率(%)
		IF ( R_GOLD = 0 )
			TXOPEN(1)
		ITEMPRN(3,28,194)			;マルチアイテム TYPE 5 位置
		ITEMID_TBL1[0]=R_ITEM_NAME	;所持アイテム名前
		ITEMID_TBL2[0]=R_ITEM_ICON	;所持アイテムＩＣＯＮ番号
		ITEMID_TBL3[0]=R_ITEM_GOLD	;所持アイテム値段
		ITEMID_TBL4[0]=R_ITEM_HELP	;所持アイテム説明
		ITEMID_TBL5[0]=0			;
		ITEMID_TBL6[0]=""			;
		ITEMID_TBL7[0]=0			;
		ITEMID_CNT=1				;アイテムＩＤ数
		ITEMPRN(1,5)				;マルチアイテム表示
		TXA(R_ITEM_NAME)を手に入れた．
		@P
		IF ( ITEM_MAX >= ITEM_DIM )	;所有アイテム数
			しかし、持ち物がいっぱいだったのでTXA(R_ITEM_NAME)を捨てた。
			@P
			ITEMPRN(2)				;マルチアイテム復帰
			TXCLOSE(1)
			RET
		ITEMPRN(2)				;マルチアイテム復帰
		BX=R_ITEM_ICON			;所持アイテムＩＣＯＮ番号
		CX=R_ITEM_GOLD			;所持アイテム値段
		DX=R_ITEM_ATTR			;所有アイテム種別
		SI=R_ITEM_NAME			;所持アイテム名前
		DI=R_ITEM_PROG			;所持アイテム効果
		BP=R_ITEM_HELP			;所持アイテム説明
		ITEM_INST(1)
		TXCLOSE(1)
		RET
IF ( R_GOLD > 0 )
	@P
	TXCLOSE(1)
RET

;**************
;	戦闘モード
;**************

*BTL_LOOP

ATTAKE_GIRL
DISP_HP
IF ( FLAG_BATLE ! 0 ) RET

@P
@C

ATTAKE_MONSTOR
DISP_HP
IF ( FLAG_BATLE ! 0 ) RET

BATL_COUNT++
IF ( BATL_COUNT>=20 )
	FLAG_BATLE=4		;4=敵逃げる
	RET

GOTO BTL_LOOP

*ATTAKE_GIRL
SLCTX("攻撃する,魔法を使う,道具を使う")
@C
IF ( ISLCT=1   W_MP < 10 )
	ＭＰが足りません．
	GOTO ATTAKE_GIRL
IF ( ISLCT=0 )
	GIRL_ATTAKE
	TEKI_KAISHIN_DMG
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(V_KOUGEKI,R_BOUGYO)
		TEKI_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		TEKI_DAMEG_NONE
	RET
IF ( ISLCT=1 )
	IF ( E_MAX_MP=0 )		;ＭＰが減らない
		W_MP -= 10
	GIRL_MAGIC
	TEKI_KAISHIN_DMG
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(V_MARYOKU,R_KOHMARYOKU)
		TEKI_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		TEKI_DAMEG_NONE
	RET
IF ( ISLCT=2 )
	USE_TOOL			;道具
	IF ( TOOL_BUKI=0 )	GOTO ATTAKE_GIRL	;0=NONE
RET

*ATTAKE_MONSTOR
;魔力を持っている敵は，《現在のＭＰ：現在のＨＰ》の比率で魔法
C1 = R_HP + R_MP		;
P100PAR(R_MP,C1)		;百分率を求める
C2 = AX					;parcent.
RANDAM(100)				;
IF ( C2 < IRND )( R_MP < 10 )
	TEKI_ATTAKE
	GIRL_TSUKON_DMG
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(R_KOUGEKI,V_BOUGYO)
		GIRL_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		GIRL_DAMEG_NONE
	RET
IF ( C2 >= IRND )
	R_MP -= 10
	TEKI_MAGIC
	GIRL_TSUKON_DMG
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(R_MARYOKU,V_KOHMARYOKU)
		GIRL_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		GIRL_DAMEG_NONE
	RET
RET

*GIRL_ATTAKE
TXA(NAME_FIRST)の攻撃！@@
PPP1
CALC_ATTACK(V_SENTOH)	ATTACK_VAL=AX
CALC_DEFENS(R_SENTOH)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*GIRL_MAGIC
TXA(R_NAME)は呪文を唱えた．
SE_MAGIC
魔法攻撃をしかけた！！
PPP1
CALC_ATTACK(V_MAHOGIJUTSU)	ATTACK_VAL=AX
CALC_DEFENS(R_MAHOGIJUTSU)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*TEKI_ATTAKE
TXA(R_NAME)の攻撃！@@
PPP1
CALC_ATTACK(R_SENTOH)	ATTACK_VAL=AX
CALC_DEFENS(V_SENTOH)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*TEKI_MAGIC
TXA(R_NAME)は呪文を唱えた！
SE_MAGIC
魔法攻撃を受けた！！
PPP1
CALC_ATTACK(R_MAHOGIJUTSU)	ATTACK_VAL=AX
CALC_DEFENS(V_MAHOGIJUTSU)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*TEKI_KAISHIN_DMG
FLAG_KAISHIN=0			;会心ﾌﾗｸﾞ
RANDAM(100)
IF ( MY_KAISHIN >=IRND )	;会心率(%)
	FLAG_KAISHIN=1
RET

*TEKI_DAMEG
T_DAMEG = AX
IF ( FLAG_KAISHIN!0 )
	T_DAMEG = T_DAMEG * 3
	会心の一撃！
	PPP1
IF ( FLG_HABAM=0 )
	IF ( FLAG_KAISHIN=0 )	SE_TK_DAMEG
	IF ( FLAG_KAISHIN!0 )	SE_KAISHIN
	命中！TXA(R_NAME)にTXPOINT(T_DAMEG)のダメージを与えた！
IF ( FLG_HABAM!0 )
	IF ( R_SOUKOU > 0 )		;装甲
		SE_TK_DEFENS
		命中！しかし，TXA(R_NAME)のTXA(R_SKNAME)に阻まれた！
	IF ( R_SOUKOU = 0 )		;装甲
		SE_TK_DEFENS
		TXA(R_NAME)は攻撃を受けとめた！
	TXA(R_NAME)はTXPOINT(T_DAMEG)のダメージ．
PPP1
TEKI_DAMEG_CHK
RET

*TEKI_DAMEG_NONE
TXA(R_NAME)は攻撃を回避した！
PPP1
RET

*TEKI_DAMEG_CHK
R_HP -= T_DAMEG
IF ( R_HP <= 0 )
	R_HP = 0
	FLAG_BATLE=1	;1=娘勝ち
	RET
IF ( R_MS_NUMBER ! 7 )	;"武神"を除く
	R_SENI -= T_DAMEG	;戦意
IF ( R_SENI <= 0 )
	R_SENI = 0
	RANDAM(30)
	IF ( IRND<=10 )
		FLAG_BATLE=4	;4=敵逃げる
RET

*GIRL_TSUKON_DMG
FLAG_TSUKON=0			;痛恨ﾌﾗｸﾞ
RANDAM(100)
IF ( R_TSUKON >=IRND )	;痛恨率(%)
	FLAG_TSUKON=1
RET

*GIRL_DAMEG
G_DAMEG = AX
IF ( FLAG_TSUKON!0 )		;痛恨ﾌﾗｸﾞ
	G_DAMEG = G_DAMEG * 3
	痛恨の一撃！
	PPP1
IF ( FLG_HABAM=0 )
	IF ( FLAG_TSUKON=0 )	SE_GR_DAMEG
	IF ( FLAG_TSUKON!0 )	SE_TSUKON
	命中！TXA(NAME_FIRST)はTXPOINT(G_DAMEG)のダメージを受けた！
IF ( FLG_HABAM!0 )
	IF ( EQUIP_ICON ! 0 )
		SE_GR_DEFENS
		命中！しかし，TXA(NAME_FIRST)のTXA(EQUIP_NAME)に阻まれた！
	IF ( EQUIP_ICON = 0 )
		SE_GR_DEFENS
		TXA(NAME_FIRST)は攻撃を受けとめた！
	TXA(NAME_FIRST)はTXPOINT(G_DAMEG)のダメージ．
PPP1
GIRL_DAMEG_CHK
RET

*GIRL_DAMEG_NONE
TXA(NAME_FIRST)は攻撃をかわした。
PPP1
RET

*GIRL_DAMEG_CHK
W_HP -= G_DAMEG		; ＨＰ
IF ( W_HP <= 0 )
	W_HP = 0
	FLAG_BATLE=2	;2=敵勝ち
	RET
W_SENI -= G_DAMEG	; 戦意
IF ( W_SENI <= 0 )
	W_SENI = 0
	RANDAM(30)
	IF ( IRND<=10 )
		TXA(NAME_FIRST)は一目散に逃げ出した．
		RANDAM(100)
		IF ( R_DASSYUTU>=IRND )		;脱出に成功した
			FLAG_BATLE=3	;3=娘逃げる
			RET
		しかし，敵に回り込まれてしまった．
		逃げられない！
RET

*USE_TOOL			; 道具
SLCT_TOOLS
IF (TOOL_BUKI=0) RET	;0=NONE
IF (TOOL_BUKI=1)		;1=快癒丸
	TXA(NAME_FIRST)は快癒丸を一粒飲んだ．
	IRAND2(20,60)		;20〜60まで
	C2 = AX
	C1 = W_HP_MAX - W_HP
	IF ( C1 < C2 ) C2 = C1
	W_HP = W_HP + C2
	ＨＰがTXPOINT(C2)回復した．
	PPP1
	RET
IF (TOOL_BUKI=2)		;2=焙碌
	TXA(NAME_FIRST)は手投げ焙碌を投げつけた．
	SE_HOUROKU
	ドッカーン！！
	RANDAM(80)			;1〜80のダメージ
	R_HP -= IRND
	IF ( R_HP<0 ) R_HP = 0
	TXA(R_NAME)にTXPOINT(IRND)のダメージを与えた！
	PPP1
	TEKI_DAMEG_CHK
	RET

*SLCT_TOOLS			; -O- TOOL_BUKI:0=NONE,1=快癒丸,2=焙碌
ITEM_SRCH(1,16)		;快癒丸
C2 = DX				;item record.
C1 = 0				;
IF ( AX = 1 )		;1=search
	ITEMID_TBL1[C1]=ITEM_NAME[C2]	;所有アイテム名前
	ITEMID_TBL2[C1]=ITEM_ICON[C2]	;ＩＣＯＮ
	ITEMID_TBL3[C1]=ITEM_GOLD[C2]	;金
	ITEMID_TBL4[C1]=ITEM_HELP[C2]	;所有アイテム説明
	ITEMID_TBL5[C1]=0
	ITEMID_TBL6[C1]=""
	ITEMID_TBL7[C1]=0
	REC_TBL[C1]=C2
	C1++
ITEM_SRCH(1,17)		;投げ焙碌（ﾎｳﾛｸ）
C2 = DX				;item record.
IF ( AX = 1 )		;1=search
	ITEMID_TBL1[C1]=ITEM_NAME[C2]	;所有アイテム名前
	ITEMID_TBL2[C1]=ITEM_ICON[C2]	;ＩＣＯＮ
	ITEMID_TBL3[C1]=ITEM_GOLD[C2]	;金
	ITEMID_TBL4[C1]=ITEM_HELP[C2]	;所有アイテム説明
	ITEMID_TBL5[C1]=0
	ITEMID_TBL6[C1]=""
	ITEMID_TBL7[C1]=0
	REC_TBL[C1]=C2
	C1++
ITEMID_CNT = C1			;アイテムＩＤ数
IF ( ITEMID_CNT=0 )
	TOOL_BUKI = 0
	戦闘に使用できる道具がありません
	PPP2
	RET
ITEMSLCT(1)				;マルチアイテム選択
IF ( ISITM = -1 )
	TOOL_BUKI = 0
	RET
IF ( ITEMID_TBL2[ISITM]=16 ) ;快癒丸
	TOOL_BUKI=1	;快癒丸
IF ( ITEMID_TBL2[ISITM]=17 ) ;投げ焙碌（ﾎｳﾛｸ）
	TOOL_BUKI=2	;焙碌
ITEM_KILL(REC_TBL[ISITM])	; アイテム削除
RET

*CALC_ATTACK	;-I-戦闘技術 -O- AX : 攻撃バリュー
;AX=AX+-30%randam
C1 = AX
IRNDPAR(C1,30)
RET

*CALC_DEFENS	;-I-戦闘技術 -O- AX : 攻撃バリュー
;AX=AX+-30%randam
C1 = AX
IRNDPAR(C1,30)
RET

*CALC_DAMEG		;-I-攻撃力,防御側の防御力 -O- AX : ダメージ
;AX = AX+-20%randam - BX
FLG_HABAM=0
C1 = AX
C2 = BX
IRNDPAR(C1,20)
AX = AX - C2
IF ( AX < 0 )
	FLG_HABAM=1
	AX = 1
RET

*PPP1
TIMER1(10)
WAIT1
RET

*PPP2
@P
RET

*SE_TK_DEFENS
IF(R_SE_ATTACK=1)MUSIC(6,7)	;1=皮
IF(R_SE_ATTACK=2)MUSIC(6,8)	;2=鉄
IF(R_SE_ATTACK=3)MUSIC(6,9)	;3=体毛
IF(R_SE_ATTACK=4)MUSIC(6,10);4=固い体
IF(R_SE_ATTACK=5)MUSIC(6,15);5=武神
RET

*SE_GR_DEFENS
IF(GIRL_SE_ATTACK=1)MUSIC(6,7)	;1=皮
IF(GIRL_SE_ATTACK=2)MUSIC(6,8)	;2=鉄
IF(GIRL_SE_ATTACK=3)MUSIC(6,9)	;3=体毛
RET

*SE_MAGIC
MUSIC(6,4)
RET

*SE_TK_DAMEG
MUSIC(6,11)
RET

*SE_GR_DAMEG
MUSIC(6,12)
RET

*SE_KAISHIN
MUSIC(6,13)
RET

*SE_TSUKON
MUSIC(6,14)
RET

*SE_HOUROKU
MUSIC(6,16)
RET

*CHECK_GIRL
					;娘に当たる(1=皮,2=鉄,3=体毛)
IF ( EQUIP_ICON = 11 )	GIRL_SE_ATTACK=1		;11=皮の鎧
IF ( EQUIP_ICON = 12 )	GIRL_SE_ATTACK=2		;12=鎖帷子
IF ( EQUIP_ICON = 13 )	GIRL_SE_ATTACK=2		;13=鉄の鎧
IF ( EQUIP_ICON = 14 )	GIRL_SE_ATTACK=2		;14=ミスリルの鎧
IF ( EQUIP_ICON = 15 )	GIRL_SE_ATTACK=3		;15=シルクの鎧
RET

;
;	end of "RPGMNS.TXT"
;
