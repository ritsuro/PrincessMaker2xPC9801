; "EVNT029.TXT"
;
; 29 お尋ね者と遭遇
;【 概　要 】武者修行の最中，偶然バッタリとお尋ね者に遭遇して，戦いになります．
;	　　お尋ね者Ａ（バナザード）　→東マップ
;	　　　　〃　Ｂ（バニスター）　→南マップ
;	　　　　〃　Ｃ（カスティーヨ）→北マップ
;【 いつ？ 】武者修行実行中．
;【発生条件】武者修行中画面の小屋，洞窟に入ったとき，《１／２０》の確率で発生．
;【 効　果 】お尋ね者は全部で３人．お尋ね者は弱い順番に登場．戦ってやっつけない限
;				り，同じお尋ね者しか登場しない．
;				お尋ね者は，見逃すよう頼んでくる．同意すれば，規定の金額（お尋ね者の
;				データ参照）をもらい，イベントから抜ける．
;				見逃さなければ，戦闘になる．勝てばイベント「表彰」が発生．
;				お尋ね者ごとに設定されている賞金が貰える．
;				一度勝ったお尋ね者は，もう登場しない．お尋ね者３人ともやっつけると，
;				もう発生しない．
;				見逃した場合はお金がもらえるが，因業値が５０上がる．
;
; 53 Ｈなお尋ね者
;【 概　要 】お尋ね者と遭遇(29)の派生イベント．お尋ね者と戦って負けると，
;		気絶している隙に，いたずらされてしまう・・・乙女のピーンチ！
;		というところで，キューブが乱入．貞操の危機は守られる・・・
;		色気と執事との関係上昇．
;		エンディングに影響．
;【 いつ？ 】武者修行実行中
;【発生条件】色気が１００以上で，イベント「お尋ね者と遭遇」で敵と負ける．
;【 効　果 】色気が２５上昇．執事との関係が２０上昇．
;
;
;RUN_MONTH[29]「お尋ね者と遭遇」実行回数
;E_ETC[19]お尋ね者・捕縛・バナザード(1=逮捕,2=賞金)
;E_ETC[20]お尋ね者・捕縛・バニスター(1=逮捕,2=賞金)
;E_ETC[21]お尋ね者・捕縛・カスティーヨ(1=逮捕,2=賞金)
;E_ETC[22]お尋ね者・賞金額・バナザード
;E_ETC[23]お尋ね者・賞金額・バニスター
;E_ETC[24]お尋ね者・賞金額・カスティーヨ
;RUN_MONTH[53]「Ｈなお尋ね者」実行回数
;E_ETC[94]武者修行中に１度お尋ね者と遭遇
;E_ETC[102]お尋ね者通常攻撃ﾀﾞﾒｰｼﾞ合計
;E_ETC[103]お尋ね者魔法攻撃ﾀﾞﾒｰｼﾞ合計
;

.FLAG_VICTORY	;勝利フラグ
.FLAG_KAIWA_OK	;会話条件成立
.FLAG_BATLE		;0=戦闘中,1=娘勝ち,2=敵勝ち,3=娘逃げる,4=敵逃げる
.G_DAMEG
.T_DAMEG
.REC_TBL[50]
.TOOL_BUKI

.ATTACK_VAL		;攻撃バリュー
.DEFENS_VAL		;防御バリュー
.BATLE_NOW		;1=命中,2=失敗

.MY_KAISHIN		;会心率(%)
.FLAG_KAISHIN=0	;会心ﾌﾗｸﾞ
.FLAG_TSUKON=0	;痛恨ﾌﾗｸﾞ
.BATL_COUNT	=0
.FLG_HABAM	=0

.R_HP		=0	;ＨＰ
.R_MP		=0	;ＭＰ

.BATLE_GR_SLCT		;1=通常攻撃,2=魔法攻撃

.R_MS_NUMBER	=0	;モンスター番号
.R_FACE			=0	;フェイス
.R_MONSTOR		=0	;モンスター
.R_NAME			=0	;名前
.R_SENTOH		=0	;戦闘技術
.R_KOUGEKI		=0	;攻撃力
.R_BOUGYO		=0	;防御力
.R_SOUKOU		=0	;装甲
.R_SKNAME		=0	;装甲名
.R_MAHOGIJUTSU	=0	;魔法技術
.R_MARYOKU		=0	;魔力
.R_KOHMARYOKU	=0	;抗魔力
.R_SENI			=0	;戦意
.R_DASSYUTU		=0	;脱出率(%)
.R_TSUKON		=0	;痛恨率(%)
.R_MAX_HP		=0	;最大ＨＰ
.R_MAX_MP		=0	;最大ＭＰ
.R_GOLD			=0	;所持金

.R_SE_ATTACK	=0	;剣の当たる(1=皮,2=鉄,3=体毛,4=固い体)
.GIRL_SE_ATTACK	=0	;娘に当たる(1=皮,2=鉄,3=体毛)

.R_GOLD_GAKU	=0	;提示金額
.R_GOLD_REWARD	=0	;賞金額

.R_ITEM_ICON	=0	;所持アイテムＩＣＯＮ番号
.R_ITEM_GOLD	=0	;所持アイテム値段
.R_ITEM_ATTR	=0	;所有アイテム種別
.R_ITEM_NAME	=0	;所持アイテム名前
.R_ITEM_PROG	=0	;所持アイテム効果
.R_ITEM_HELP	=0	;所持アイテム説明
.R_ITEM_PROPA	=0	;アイテム確率(%)

.MSG_01			;台詞１（出会い）
.MSG_02			;台詞２（交渉）
.MSG_03			;台詞３（交渉成立）
.MSG_04			;台詞４（交渉決裂）

.S_WORKS		;スケジュール内容
.S_COUNTER		;当日カウンタ
.S_STARTDAY		;スケジュール開始日
.S_ENDDAY		;スケジュール終了日
.TODAY_COUNT	;日付カウンタ
.DAYLIGHT		;1=day,2=night,3=day->night,4=night->day.
.SOUBI_FLG		;装備フラグ
.SOUBI_REC		;装備のアイテム番号
.SOUBI_DAY		;装備の日

S_WORKS		=	BX	;スケジュール内容
S_COUNTER	=	CX	;当日カウンタ
S_STARTDAY	=	C1	;スケジュール開始日
S_ENDDAY	=	C2	;スケジュール終了日
TODAY_COUNT	=	C3	;日付カウンタ
DAYLIGHT	=	C4	;1=day,2=night,3=day->night,4=night->day.
SOUBI_FLG	=	C5	;装備フラグ
SOUBI_REC	=	C6	;装備のアイテム番号
SOUBI_DAY	=	C7	;装備の日

STRSCLEAR

TXLC(1,17,280,0)		; text window locate.
TXLC(2,28,130,1)		; text window locate.
TXLC(3,13,260,2)		; text window locate.
WWIVENT(5,0,24,60)		; ivent window locate.

EVNT029					; お尋ね者と遭遇

WWMUSYA(9)				; 戦闘後データロード
WWMUSYA(8,1)			; 再描画

WWMUSYA(17) ;ステータス
IF ( AX=4 )	;4=North1
	WWMUSYA(13,0,0,0,1,2)		;マップシフト

BX = S_WORKS		;スケジュール内容
CX = S_COUNTER		;当日カウンタ
C1 = S_STARTDAY		;スケジュール開始日
C2 = S_ENDDAY		;スケジュール終了日
C3 = TODAY_COUNT	;日付カウンタ
C4 = DAYLIGHT		;1=day,2=night,3=day->night,4=night->day.
C5 = SOUBI_FLG		;装備フラグ
C6 = SOUBI_REC		;装備のアイテム番号
C7 = SOUBI_DAY		;装備の日

IF ( S_WORKS = 0 )	LOAD("RPGLOP,RPG000")		; 0=東部 森林地帯
IF ( S_WORKS = 1 )	LOAD("RPGLOP,RPG001")		; 1=西部 砂漠地帯
IF ( S_WORKS = 2 )	LOAD("RPGLOP,RPG002")		; 2=南部 水郷地帯
IF ( S_WORKS = 3 )	LOAD("RPGLOP,RPG003")		; 3=北部 氷山地帯

TXOPEN(1)致命的エラーWAITU

;**************
;	back to home load.
;**************

*BACK_TO_HOME

BX = S_WORKS		;スケジュール内容
CX = S_COUNTER		;当日カウンタ
C1 = S_STARTDAY		;スケジュール開始日
C2 = S_ENDDAY		;スケジュール終了日
C3 = TODAY_COUNT	;日付カウンタ
C4 = DAYLIGHT		;1=day,2=night,3=day->night,4=night->day.
C5 = SOUBI_FLG		;装備フラグ
C6 = SOUBI_REC		;装備のアイテム番号
C7 = SOUBI_DAY		;装備の日

LOAD("RPGDWN")

;************************************
;	お尋ね者と遭遇
;************************************

*EVNT029
OTAZUNEMONO		;お尋ね者達のデータ
CHECK_GIRL

;お尋ね物がいる場合

RUN_MONTH[29]++	;「お尋ね者と遭遇」実行回数

E_ETC[94]=1		;武者修行中に１度お尋ね者と遭遇

MUSIC(2)		;音楽ﾌｪｰﾄﾞｱｳﾄ

TXOPEN(3)
TXGIRL(9)		;娘・不思議
「＊＊＊＊＊＊＊＊＊＊＊」
WWMUSYA(7)				; ノーマルモード
TXF(2)					;マウスOFF待ち
@P
TXOPEN(2)
SI = R_FACE TXFACE		;お尋ね物（Ａ．Ｂ．Ｃ）・普通
TXA(MSG_01)				;台詞１（出会い）

PLAY(29)			;P29 緊迫

@P
@3
@C
TXGIRL(15)				;娘・驚き
「＊＊＊」
@P
@2
@C
TXA(MSG_02)				;台詞２（交渉）
@P
SLCPOS(1)SLCTX("見逃す,断る")

IF ( ISLCT=0 )			;見逃す場合
	@3
	@C
	TXGIRL(0)			;娘・普通
	「＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	@2
	@C
	TXA(MSG_03)			;台詞３（交渉成立）
	@P
	TXCLOSE(2)
	TXCLOSE(3)
	GOLDADD(R_GOLD_GAKU)	;提示金額
	WWPROF(5)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･所持金
	TXOPEN(1)
	TXA(R_NAME)は，TXA(NAME_FIRST)に金を渡して立ち去った・・・

	GR_OPEN				; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN
	STRSCLEAR
	IGRP_TBL1[0]="因業"
	IGRP_TBL3[0]=B_INGYO
	IGRP_CNT=1
	IGRP_X=28
	IGRP_Y=240
	GRPOPEN(1,1)			; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
	B_INGYO += 50			;因業値が５０上がる．
	GRPUPD(1,0,B_INGYO)
	MUSIC(2)			;音楽ﾌｪｰﾄﾞｱｳﾄ
	@P
	GR_CLOSE			; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ CLOSE
	TXCLOSE(1)
	PLAY(14)	;P14 武者修行
	RET
;断る場合
@3
@C
TXGIRL(12)			;娘・不愉快
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
@2
@C
TXA(MSG_04)			;台詞４（交渉決裂）
@P
TXCLOSE(2)
TXCLOSE(3)

TXOPEN(1)
TXA(R_NAME)はいきなり襲いかかってきた！
TXCLOSE(1)

BATLE_MODE			;戦闘モード
WWMUSYA(16)			;キャンプ後ロード

PLAY(14)			;P14 武者修行

RET

;************************************
;	戦闘モード
;************************************

*BATLE_MODE			;戦闘モード
PLAY(17)			;P17 戦闘

SLCPOS(2)
R_HP = R_MAX_HP		;最大ＨＰ
R_MP = R_MAX_MP		;最大ＭＰ
FLAG_VICTORY=0		;勝利フラグ
FLAG_BATLE = 0		;0=戦闘中
MY_KAISHIN = 3		;会心率(%)
IF ( E_KAISIN ! 0 )	;会心の一撃発生倍率２倍
	MY_KAISHIN = 6	;会心率(%)
BATL_COUNT = 0

GRAPSHOW
IGRP_X=55
IGRP_Y=126
GR_F(4,17)			; 17=真剣 娘フェイス

IGRP_X=11
IGRP_Y=126
SI=R_FACE GR_F(2)	;人間・普通

TXLC(7,26,284,0)	;text window locate.
TXOPEN(7)
戦闘開始
@P

TXF(11)			; 戦闘時ﾃｷｽﾄﾓｰﾄﾞ
BTL_LOOP
TXF(10)			; ﾉｰﾏﾙ時ﾃｷｽﾄﾓｰﾄﾞ
GR_CLOSE			;ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ CLOSE

MUSIC(2)			;音楽ﾌｪｰﾄﾞｱｳﾄ

IF ( FLAG_BATLE=1 )		;1=娘勝ち

	IF ( R_MS_NUMBER=1 ) E_ETC[19]=1	;バナザード(1=逮捕,2=賞金)
	IF ( R_MS_NUMBER=2 ) E_ETC[20]=1	;バニスター(1=逮捕,2=賞金)
	IF ( R_MS_NUMBER=3 ) E_ETC[21]=1	;カスティーヨ(1=逮捕,2=賞金)

	TXA(NAME_FIRST)はTXA(R_NAME)を倒した．
	@P
	TXCLOSE(7)
	;;TXOPEN(2)
	;;SI=R_FACE TXFACE	;モンスター・普通
	;;TXA(MONSTR[M_DEFEAT])	;６敗北宣言
	;;@P
	FLAG_VICTORY=1		;勝利フラグ
	;;TXCLOSE(2)
	GIRL_VICTORY_CHK	;勝利チェック
	RET
IF ( FLAG_BATLE=2 )		;2=敵勝ち
	TXA(NAME_FIRST)は負けた．
	@P
	TXCLOSE(7)
	;;TXOPEN(2)
	;;SI=R_FACE TXFACE	;モンスター・普通
	;;TXA(MONSTR[M_VICTRY])	;５勝利宣言
	;;@P
	;;TXCLOSE(2)
	GIRL_DOWN_CHK		;敗戦チェック
	RET
IF ( FLAG_BATLE=3 )		;3=娘逃げる
	なんとか逃げきったようだ．
	@P
	TXCLOSE(7)
	TXOPEN(3)
	TXGIRL(11)	;娘・不安
	「＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(3)
	RET
IF ( FLAG_BATLE=4 )		;4=敵逃げる
	;;TXA(R_NAME)は逃げた．
	TXA(R_NAME)は去っていった・・・
	@P
	TXCLOSE(7)
	RET
RET

;**************
;	敗戦処理
;**************

*GIRL_DOWN_CHK				;敗戦チェック
IF ( FLAG_VICTORY = 1 ) RET	;勝利フラグ

IF ( B_IROKE >= 100 )		;色気
	EVNT053					;Ｈなお尋ね者
	WWMUSYA(9)				;戦闘後データロード
	WWMUSYA(8,2)			;再描画
	GOTO GIRL_DOWN_2		;敗戦処理

*GIRL_DOWN				;敗戦処理
TXLC(1,14,280,0)		;text window locate.
TXLC(2,28,260,1)		;text window locate.
TXOPEN(2)				;
TXFACE("F046")			;執事・困り
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
WWMUSYA(9)				; 戦闘後データロード
@P
WWMUSYA(8,2)			; 再描画

*GIRL_DOWN_2		;敗戦処理
URA_STEWARD += 2	;執事との関係
IF ( URA_STEWARD>100 ) URA_STEWARD=100	; 執事との関係
TXLC(1,14,280,0)		;text window locate.
TXLC(2,28,260,1)		;text window locate.
TXOPEN(1)				;
WWIVENT(5,0,16,32)		;ivent window locate.
;WWIVENT(1,0,"E024")	;負けて家に帰る
WWIVENT(6,0,2,4,"E024")	;save#2,load#4 ivent window open.(select bank)
TXA(NAME_FIRST)が気を失っている間に，執事はTXA(NAME_FIRST)を家に連れて帰った・・・・・・
@P						;
TXCLOSE(1)				;
IF ( P_TAIJYU < 4000 )	;体重が４０ｋｇ未満の場合
	TXOPEN(2)
	TXFACE("F039")		;執事・普通
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(2)
IF ( P_TAIJYU >= 6000 )	;体重が６０ｋｇ以上の場合
	TXOPEN(2)
	TXFACE("F046")		;執事・困り
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(2)
;WWIVENT(2,0)			; ivent window close.
WWIVENT(7,0,2)			; ivent window close.(select bank)
BACK_TO_HOME
RET

;**************
;	勝利チェック
;**************

*GIRL_VICTORY_CHK	;勝利チェック
IF ( R_GOLD > 0 )
	TXOPEN(1)
	金TXGOLD(R_GOLD)を手に入れた．
	GOLDADD(R_GOLD)			;娘の所持金
	WWPROF(5)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･所持金

IF ( R_ITEM_ICON ! 0 )				;所持アイテムＩＣＯＮ番号
	RANDAM(100)						;
	IF ( R_ITEM_PROPA >= IRND )		;アイテム確率(%)
		IF ( R_GOLD = 0 )
			TXOPEN(1)
		ITEMPRN(3,28,194)			;マルチアイテム TYPE 5 位置
		ITEMID_TBL1[0]=R_ITEM_NAME	;所持アイテム名前
		ITEMID_TBL2[0]=R_ITEM_ICON	;所持アイテムＩＣＯＮ番号
		ITEMID_TBL3[0]=R_ITEM_GOLD	;所持アイテム値段
		ITEMID_TBL4[0]=R_ITEM_HELP	;所持アイテム説明
		ITEMID_TBL5[0]=0			;
		ITEMID_TBL6[0]=""			;
		ITEMID_TBL7[0]=0			;
		ITEMID_CNT=1				;アイテムＩＤ数
		ITEMPRN(1,5)				;マルチアイテム表示
		TXA(R_ITEM_NAME)を手に入れた．
		@P
		IF ( ITEM_MAX >= ITEM_DIM )	; 所有アイテム数
			しかし、持ち物がいっぱいだったのでTXA(R_ITEM_NAME)捨てた。
			@P
			ITEMPRN(2)				;マルチアイテム復帰
			TXCLOSE(1)
			RET
		ITEMPRN(2)				;マルチアイテム復帰
		BX=R_ITEM_ICON			;所持アイテムＩＣＯＮ番号
		CX=R_ITEM_GOLD			;所持アイテム値段
		DX=R_ITEM_ATTR			;所有アイテム種別
		SI=R_ITEM_NAME			;所持アイテム名前
		DI=R_ITEM_PROG			;所持アイテム効果
		BP=R_ITEM_HELP			;所持アイテム説明
		ITEM_INST(1)
		TXCLOSE(1)
		RET
IF ( R_GOLD > 0 )
	@P
	TXCLOSE(1)
RET


;**************
;	graph.
;**************

*GRAPSHOW
;GR_OPEN			; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN
GR_F(1,9)			; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN

SI = R_NAME
IGRP_X = 1
IGRP_Y = 248
GR_F(3)

C1 = R_SENTOH + R_KOUGEKI + R_BOUGYO			;戦士評価は《戦闘技術＋攻撃力＋防御力》
C2 = R_MAHOGIJUTSU + R_MARYOKU + R_KOHMARYOKU	;魔法評価は《魔法技術＋魔力＋抗魔力》

IGRP_TBL1[0]="ＨＰ"
IGRP_TBL1[1]="ＭＰ"
IGRP_TBL1[2]="戦士評価"
IGRP_TBL1[3]="魔法評価"
IGRP_TBL1[4]="戦意"
IGRP_TBL3[0]=R_HP			;ＨＰ
IGRP_TBL3[1]=R_MP			;ＭＰ
IGRP_TBL3[2]=C1
IGRP_TBL3[3]=C2
IGRP_TBL3[4]=R_SENI			;戦意
IGRP_CNT=5
IGRP_X=1
IGRP_Y=286
GRPOPEN(1,2)		; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ

SI = NAME_FIRST
IGRP_X = 55
IGRP_Y = 248
GR_F(3)

IGRP_TBL1[0]="ＨＰ"
IGRP_TBL1[1]="ＭＰ"
IGRP_TBL1[2]="戦士評価"
IGRP_TBL1[3]="魔法評価"
IGRP_TBL1[4]="戦意"
IGRP_TBL3[0]=W_HP			;ＨＰ
IGRP_TBL3[1]=W_MP			;ＭＰ
IGRP_TBL3[2]=H_SENSHI		;戦士評価
IGRP_TBL3[3]=H_MAHO			;魔法評価
IGRP_TBL3[4]=W_SENI			;戦意
IGRP_CNT=5
IGRP_X=55
IGRP_Y=286
GRPOPEN(1,3)		; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
RET

*DISP_HP
GRPUPD(2,0,R_HP)
GRPUPD(2,1,R_MP)
GRPUPD(2,4,R_SENI)
GRPUPD(3,0,W_HP)
GRPUPD(3,1,W_MP)
GRPUPD(3,4,W_SENI)
WWPROF(6)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･HP
WWPROF(7)			; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・RPG･MP
RET

;**************
;	戦闘モード
;	ほかの戦闘プログラムと違うのは
;	E_ETC[102]お尋ね者通常攻撃ﾀﾞﾒｰｼﾞ合計
;	E_ETC[103]お尋ね者魔法攻撃ﾀﾞﾒｰｼﾞ合計
;	の計算を含んでいることです。
;**************

*BTL_LOOP

ATTAKE_GIRL
DISP_HP
IF ( FLAG_BATLE ! 0 ) RET

@P
@C

ATTAKE_MONSTOR
DISP_HP
IF ( FLAG_BATLE ! 0 ) RET

BATL_COUNT++
IF ( BATL_COUNT>=20 )
	IF ( W_HP >= R_HP )	FLAG_BATLE=4		;4=敵逃げる
	IF ( W_HP <  R_HP )	FLAG_BATLE=3		;3=娘逃げる
	RET

GOTO BTL_LOOP

*ATTAKE_GIRL
SLCTX("攻撃する,魔法を使う,道具を使う")
@C
IF ( ISLCT=1   W_MP < 10 )
	ＭＰが足りません．
	GOTO ATTAKE_GIRL

BATLE_GR_SLCT = ISLCT + 1	;<==========================

IF ( ISLCT=0 )
	GIRL_ATTAKE
	TEKI_KAISHIN_DMG
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(V_KOUGEKI,R_BOUGYO)
		TEKI_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		TEKI_DAMEG_NONE
	RET
IF ( ISLCT=1 )
	IF ( E_MAX_MP=0 )		;ＭＰが減らない
		W_MP -= 10
	GIRL_MAGIC
	TEKI_KAISHIN_DMG
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(V_MARYOKU,R_KOHMARYOKU)
		TEKI_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		TEKI_DAMEG_NONE
	RET
IF ( ISLCT=2 )
	USE_TOOL			;道具
	IF ( TOOL_BUKI=0 )	GOTO ATTAKE_GIRL	;0=NONE
RET

*ATTAKE_MONSTOR
;魔力を持っている敵は，《現在のＭＰ：現在のＨＰ》の比率で魔法
C1 = R_HP + R_MP		;
P100PAR(R_MP,C1)		;百分率を求める
C2 = AX					;parcent.
RANDAM(100)				;
IF ( C2 < IRND )( R_MP < 10 )
	TEKI_ATTAKE
	GIRL_TSUKON_DMG
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(R_KOUGEKI,V_BOUGYO)
		GIRL_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		GIRL_DAMEG_NONE
	RET
IF ( C2 >= IRND )
	R_MP -= 10
	TEKI_MAGIC
	GIRL_TSUKON_DMG
	IF ( BATLE_NOW=1 )		;1=命中
		CALC_DAMEG(R_MARYOKU,V_KOHMARYOKU)
		GIRL_DAMEG
	IF ( BATLE_NOW=2 )		;2=失敗
		GIRL_DAMEG_NONE
	RET
RET

*GIRL_ATTAKE
TXA(NAME_FIRST)の攻撃！@@
PPP1
CALC_ATTACK(V_SENTOH)	ATTACK_VAL=AX
CALC_DEFENS(R_SENTOH)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*GIRL_MAGIC
TXA(NAME_FIRST)は呪文を唱えた．
SE_MAGIC
魔法攻撃をしかけた！！
PPP1
CALC_ATTACK(V_MAHOGIJUTSU)	ATTACK_VAL=AX
CALC_DEFENS(R_MAHOGIJUTSU)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*TEKI_ATTAKE
TXA(R_NAME)の攻撃！@@
PPP1
CALC_ATTACK(R_SENTOH)	ATTACK_VAL=AX
CALC_DEFENS(V_SENTOH)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*TEKI_MAGIC
TXA(R_NAME)は呪文を唱えた！
SE_MAGIC
魔法攻撃を受けた！！
PPP1
CALC_ATTACK(R_MAHOGIJUTSU)	ATTACK_VAL=AX
CALC_DEFENS(V_MAHOGIJUTSU)	DEFENS_VAL=AX
BATLE_NOW = 2			;2=失敗
IF ( ATTACK_VAL > DEFENS_VAL )
	BATLE_NOW = 1		;1=命中
RET

*TEKI_KAISHIN_DMG
FLAG_KAISHIN=0			;会心ﾌﾗｸﾞ
RANDAM(100)
IF ( MY_KAISHIN >=IRND )	;会心率(%)
	FLAG_KAISHIN=1
RET

*TEKI_DAMEG
T_DAMEG = AX
IF ( FLAG_KAISHIN!0 )
	T_DAMEG = T_DAMEG * 3
	会心の一撃！
	PPP1
IF ( FLG_HABAM=0 )
	IF ( FLAG_KAISHIN=0 )	SE_TK_DAMEG
	IF ( FLAG_KAISHIN!0 )	SE_KAISHIN
	命中！TXA(R_NAME)にTXPOINT(T_DAMEG)のダメージを与えた！
IF ( FLG_HABAM!0 )
	IF ( R_SOUKOU > 0 )		;装甲
		SE_TK_DEFENS
		命中！しかし，TXA(R_NAME)のTXA(R_SKNAME)に阻まれた！
	IF ( R_SOUKOU = 0 )		;装甲
		SE_TK_DEFENS
		TXA(R_NAME)は攻撃を受けとめた！
	TXA(R_NAME)はTXPOINT(T_DAMEG)のダメージ．
PPP1
TEKI_DAMEG_CHK
RET

*TEKI_DAMEG_NONE
TXA(R_NAME)は攻撃を回避した！
PPP1
RET

*TEKI_DAMEG_CHK
R_HP -= T_DAMEG
IF ( BATLE_GR_SLCT=1 )	E_ETC[102]+=T_DAMEG		;お尋ね者通常攻撃ﾀﾞﾒｰｼﾞ合計
IF ( BATLE_GR_SLCT=2 )	E_ETC[103]+=T_DAMEG		;お尋ね者魔法攻撃ﾀﾞﾒｰｼﾞ合計
IF ( R_HP <= 0 )
	R_HP = 0
	FLAG_BATLE=1	;1=娘勝ち
	RET
R_SENI -= T_DAMEG	;戦意
IF ( R_SENI <= 0 )
	R_SENI = 0
	RANDAM(30)
	IF ( IRND<=10 )
		FLAG_BATLE=4	;4=敵逃げる
RET

*GIRL_TSUKON_DMG
FLAG_TSUKON=0			;痛恨ﾌﾗｸﾞ
RANDAM(100)
IF ( R_TSUKON >=IRND )	;痛恨率(%)
	FLAG_TSUKON=1
RET

*GIRL_DAMEG
G_DAMEG = AX
IF ( FLAG_TSUKON!0 )		;痛恨ﾌﾗｸﾞ
	G_DAMEG = G_DAMEG * 3
	痛恨の一撃！
	PPP1
IF ( FLG_HABAM=0 )
	IF ( FLAG_TSUKON=0 )	SE_GR_DAMEG
	IF ( FLAG_TSUKON!0 )	SE_TSUKON
	命中！TXA(NAME_FIRST)はTXPOINT(G_DAMEG)のダメージを受けた！
IF ( FLG_HABAM!0 )
	IF ( EQUIP_ICON ! 0 )
		SE_GR_DEFENS
		命中！しかし，TXA(NAME_FIRST)のTXA(EQUIP_NAME)に阻まれた！
	IF ( EQUIP_ICON = 0 )
		SE_GR_DEFENS
		TXA(NAME_FIRST)は攻撃を受けとめた！
	TXA(NAME_FIRST)はTXPOINT(G_DAMEG)のダメージ．
PPP1
GIRL_DAMEG_CHK
RET

*GIRL_DAMEG_NONE
TXA(NAME_FIRST)は攻撃をかわした。
PPP1
RET

*GIRL_DAMEG_CHK
W_HP -= G_DAMEG		; ＨＰ
IF ( W_HP <= 0 )
	W_HP = 0
	FLAG_BATLE=2	;2=敵勝ち
	RET
W_SENI -= G_DAMEG	; 戦意
IF ( W_SENI <= 0 )
	W_SENI = 0
	RANDAM(30)
	IF ( IRND<=10 )
		TXA(NAME_FIRST)は一目散に逃げ出した．
		RANDAM(100)
		IF ( R_DASSYUTU>=IRND )		;脱出に成功した
			FLAG_BATLE=3	;3=娘逃げる
			RET
		しかし，敵に回り込まれてしまった．
		逃げられない！
RET

*USE_TOOL			; 道具
SLCT_TOOLS
IF (TOOL_BUKI=0) RET	;0=NONE
IF (TOOL_BUKI=1)		;1=快癒丸
	TXA(NAME_FIRST)は快癒丸を一粒飲んだ．
	IRAND2(20,60)		;20〜60まで
	C2 = AX
	C1 = W_HP_MAX - W_HP
	IF ( C1 < C2 ) C2 = C1
	W_HP = W_HP + C2
	ＨＰがTXPOINT(C2)回復した．
	PPP1
	RET
IF (TOOL_BUKI=2)		;2=焙碌
	TXA(NAME_FIRST)は手投げ焙碌を投げつけた．
	SE_HOUROKU
	ドッカーン！！
	RANDAM(80)			;1〜80のダメージ
	R_HP -= IRND
	IF ( R_HP<0 ) R_HP = 0
	TXA(R_NAME)にTXPOINT(IRND)のダメージを与えた！
	PPP1
	TEKI_DAMEG_CHK
	RET

*SLCT_TOOLS			; -O- TOOL_BUKI:0=NONE,1=快癒丸,2=焙碌
ITEM_SRCH(1,16)		;快癒丸
C2 = DX				;item record.
C1 = 0				;
IF ( AX = 1 )		;1=search
	ITEMID_TBL1[C1]=ITEM_NAME[C2]	;所有アイテム名前
	ITEMID_TBL2[C1]=ITEM_ICON[C2]	;ＩＣＯＮ
	ITEMID_TBL3[C1]=ITEM_GOLD[C2]	;金
	ITEMID_TBL4[C1]=ITEM_HELP[C2]	;所有アイテム説明
	ITEMID_TBL5[C1]=0
	ITEMID_TBL6[C1]=""
	ITEMID_TBL7[C1]=0
	REC_TBL[C1]=C2
	C1++
ITEM_SRCH(1,17)		;投げ焙碌（ﾎｳﾛｸ）
C2 = DX				;item record.
IF ( AX = 1 )		;1=search
	ITEMID_TBL1[C1]=ITEM_NAME[C2]	;所有アイテム名前
	ITEMID_TBL2[C1]=ITEM_ICON[C2]	;ＩＣＯＮ
	ITEMID_TBL3[C1]=ITEM_GOLD[C2]	;金
	ITEMID_TBL4[C1]=ITEM_HELP[C2]	;所有アイテム説明
	ITEMID_TBL5[C1]=0
	ITEMID_TBL6[C1]=""
	ITEMID_TBL7[C1]=0
	REC_TBL[C1]=C2
	C1++
ITEMID_CNT = C1			;アイテムＩＤ数
IF ( ITEMID_CNT=0 )
	TOOL_BUKI = 0
	戦闘に使用できる道具がありません
	PPP2
	RET
ITEMSLCT(1)				;マルチアイテム選択
IF ( ISITM = -1 )
	TOOL_BUKI = 0
	RET
IF ( ITEMID_TBL2[ISITM]=16 ) ;快癒丸
	TOOL_BUKI=1	;快癒丸
IF ( ITEMID_TBL2[ISITM]=17 ) ;投げ焙碌（ﾎｳﾛｸ）
	TOOL_BUKI=2	;焙碌
ITEM_KILL(REC_TBL[ISITM])	; アイテム削除
RET

*CALC_ATTACK	;-I-戦闘技術 -O- AX : 攻撃バリュー
;AX=AX+-30%randam
C1 = AX
IRNDPAR(C1,30)
RET

*CALC_DEFENS	;-I-戦闘技術 -O- AX : 攻撃バリュー
;AX=AX+-30%randam
C1 = AX
IRNDPAR(C1,30)
RET

*CALC_DAMEG		;-I-攻撃力,防御側の防御力 -O- AX : ダメージ
;AX = AX+-20%randam - BX
FLG_HABAM=0
C1 = AX
C2 = BX
IRNDPAR(C1,20)
AX = AX - C2
IF ( AX < 0 )
	FLG_HABAM=1
	AX = 1
RET

*PPP1
TIMER1(10)
WAIT1
RET

*PPP2
@P
RET

*SE_TK_DEFENS
IF(R_SE_ATTACK=1)MUSIC(6,7)	;1=皮
IF(R_SE_ATTACK=2)MUSIC(6,8)	;2=鉄
IF(R_SE_ATTACK=3)MUSIC(6,9)	;3=体毛
IF(R_SE_ATTACK=4)MUSIC(6,10);4=固い体
IF(R_SE_ATTACK=5)MUSIC(6,15);5=武神
RET

*SE_GR_DEFENS
IF(GIRL_SE_ATTACK=0)MUSIC(6,9)	;3=体毛
IF(GIRL_SE_ATTACK=1)MUSIC(6,7)	;1=皮
IF(GIRL_SE_ATTACK=2)MUSIC(6,8)	;2=鉄
IF(GIRL_SE_ATTACK=3)MUSIC(6,9)	;3=体毛
RET

*SE_MAGIC
MUSIC(6,4)
RET

*SE_TK_DAMEG
MUSIC(6,11)
RET

*SE_GR_DAMEG
MUSIC(6,12)
RET

*SE_KAISHIN
MUSIC(6,13)
RET

*SE_TSUKON
MUSIC(6,14)
RET

*SE_HOUROKU
MUSIC(6,16)
RET

*CHECK_GIRL
					;娘に当たる(1=皮,2=鉄,3=体毛)
IF ( EQUIP_ICON = 11 )	GIRL_SE_ATTACK=1		;11=皮の鎧
IF ( EQUIP_ICON = 12 )	GIRL_SE_ATTACK=2		;12=鎖帷子
IF ( EQUIP_ICON = 13 )	GIRL_SE_ATTACK=2		;13=鉄の鎧
IF ( EQUIP_ICON = 14 )	GIRL_SE_ATTACK=2		;14=ミスリルの鎧
IF ( EQUIP_ICON = 15 )	GIRL_SE_ATTACK=3		;15=シルクの鎧
RET

;************************************
;	お尋ね者達のデータ
;************************************

*OTAZUNEMONO

R_MS_NUMBER = 0

IF ( E_ETC[19]=0 )	;お尋ね者・捕縛・バナザード(1=逮捕,2=賞金)
	ATTACK_MAN_A
	RET
IF ( E_ETC[20]=0 )	;お尋ね者・捕縛・バニスター(1=逮捕,2=賞金)
	ATTACK_MAN_B
	RET
IF ( E_ETC[21]=0 )	;お尋ね者・捕縛・カスティーヨ(1=逮捕,2=賞金)
	ATTACK_MAN_C
	RET
RET

;お尋ね者Ａ 一番弱い奴．

*ATTACK_MAN_A

R_MS_NUMBER		=1
R_FACE			="F150"
R_NAME			="バナザード"
R_SENTOH		=55
R_KOUGEKI		=65
R_BOUGYO		=35
R_SOUKOU		=30
R_SKNAME		="皮の鎧"
R_MAHOGIJUTSU	=0
R_MARYOKU		=0
R_KOHMARYOKU	=7
R_SENI			=850
R_DASSYUTU		=0
R_TSUKON		=15
R_MAX_HP		=319
R_MAX_MP		=0
R_GOLD			=30

R_GOLD_GAKU		=80		;提示金額
R_GOLD_REWARD	=500	;賞金額
E_ETC[22]		=500	;お尋ね者・賞金額・バナザード

R_ITEM_NAME ="皮の鎧"
R_ITEM_ICON	=11
R_ITEM_GOLD	=98
R_ITEM_ATTR	=1000		;1000番台=装備
R_ITEM_PROG	="V_BOUGYO+8"
R_ITEM_HELP	="防御+8"
R_ITEM_PROPA=100	;アイテム確率(%)

MSG_01="「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」"
MSG_02="「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」"
MSG_03="「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」"
MSG_04="「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」"
RET



;お尋ね者Ｂ 女盗賊

*ATTACK_MAN_B

R_MS_NUMBER		=2
R_FACE			="F151"
R_NAME			="バニスター"
R_SENTOH		=75
R_KOUGEKI		=70
R_BOUGYO		=40
R_SOUKOU		=35
R_SKNAME		="鎖帷子"
R_MAHOGIJUTSU	=18
R_MARYOKU		=0
R_KOHMARYOKU	=45
R_SENI			=880
R_DASSYUTU		=0
R_TSUKON		=25
R_MAX_HP		=380
R_MAX_MP		=0
R_GOLD			=286

R_GOLD_GAKU		=200	;提示金額
R_GOLD_REWARD	=1500	;賞金額
E_ETC[23]		=1500	;お尋ね者・賞金額・バニスター

R_ITEM_NAME ="鎖帷子"
R_ITEM_ICON	=12
R_ITEM_GOLD	=498
R_ITEM_ATTR	=1000		;1000番台=装備
R_ITEM_PROG	="V_BOUGYO+14 V_SENTOH-3"
R_ITEM_HELP	="防御+14 戦技-3"
R_ITEM_PROPA=100	;アイテム確率(%)

MSG_01="＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊"
MSG_02="＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊"
MSG_03="＊＊＊＊＊＊＊＊＊＊＊＊＊＊"
MSG_04="＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊"
RET


;お尋ね者Ｃ	最強のお尋ね者

*ATTACK_MAN_C

R_MS_NUMBER		=3
R_FACE			="F152"
R_NAME			="カスティーヨ"
R_SENTOH		=86
R_KOUGEKI		=82
R_BOUGYO		=55
R_SOUKOU		=45
R_SKNAME		="ミスリルの鎧"
R_MAHOGIJUTSU	=58
R_MARYOKU		=50
R_KOHMARYOKU	=60
R_SENI			=970
R_DASSYUTU		=0
R_TSUKON		=20
R_MAX_HP		=492
R_MAX_MP		=80
R_GOLD			=386

R_GOLD_GAKU		=500	;提示金額
R_GOLD_REWARD	=3500	;賞金額
E_ETC[24]		=3500	;お尋ね者・賞金額・カスティーヨ

R_ITEM_NAME ="ミスリルの鎧"
R_ITEM_ICON	=14
R_ITEM_GOLD	=2980
R_ITEM_ATTR	=1000		;1000番台=装備
R_ITEM_PROG	="V_BOUGYO+22 V_KOHMARYOKU+15"
R_ITEM_HELP	="防御+22 抗魔+15"
R_ITEM_PROPA=100	;アイテム確率(%)


MSG_01="「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」"
MSG_02="「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」"
MSG_03="「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」"
MSG_04="「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」"
RET


;************************************
;	Ｈなお尋ね者
;************************************

*EVNT053
RUN_MONTH[53]++			;「Ｈなお尋ね者」実行回数
TXLC(3,13,200,2)		; text window locate.
TXOPEN(3)
TXGIRL(6)	;娘・大怪我
「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P

MUSIC(2)			;音楽ﾌｪｰﾄﾞｱｳﾄ

;TXLC(1,17,280,0)		; text window locate.
TXLC(1,17,60,0)		; text window locate.
TXOPEN(1)
TXA(NAME_FIRST)は戦いに敗れ，気を失った・・・・・・
@P
TXCLOSE(1)
TXCLOSE(3)

PLAY(29)			;P29 緊迫

IF ( R_MS_NUMBER = 1 )		;バナザードの場合
	TXLC(2,28,260,1)		; text window locate.
	TXOPEN(2)
	TXFACE("F150")			;バナザード・普通（お尋ね者Ａ）
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	WWIVENT(1,0,"E025")		;Ｈなお尋ね者
	@P
	TXCLOSE(2)

	TXOPEN(3)
	TXGIRL(7)	;娘・苦しい
	「＊＊＊＊＊＊＊＊＊」
	@P
	TXLC(2,28,130,1)		; text window locate.
	TXOPEN(2)
	TXFACE("F150")			;バナザード・普通
	「＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(2)
	MUSIC(6,11);SE11打撃音１
	EFC(2)		; ＷＨＩＴＥ・ＦＬＡＳＨ
	;＊画面振動＋サブリナ
	MUSIC(2)			;音楽ﾌｪｰﾄﾞｱｳﾄ
	TXLC(1,17,160,0)		; text window locate.
	TXOPEN(1)
	「＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(1)

	TXLC(2,14,116,2)		; text window locate.
	TXOPEN(2)
	TXFACE("F045")	;執事・怒り
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	GOTO EVNT053_END

IF ( R_MS_NUMBER = 2 )		;バニスターの場合
	TXLC(2,28,260,1)		; text window locate.
	TXOPEN(2)
	TXFACE("F151")			;バニスター・普通（お尋ね者Ｂ）
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	WWIVENT(1,0,"E025")		;Ｈなお尋ね者
	@P
	TXCLOSE(2)

	TXOPEN(3)
	TXGIRL(7)	;娘・苦しい
	「＊＊＊＊＊＊＊＊＊」
	@P
	TXLC(2,28,130,1)		; text window locate.
	TXOPEN(2)
	TXFACE("F151")			;バニスター・普通
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(2)
	MUSIC(6,11);SE11打撃音１
	EFC(2)		; ＷＨＩＴＥ・ＦＬＡＳＨ
	;＊画面振動＋サブリナ
	MUSIC(2)			;音楽ﾌｪｰﾄﾞｱｳﾄ
	TXLC(1,17,160,0)		; text window locate.
	TXOPEN(1)
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(1)

	TXLC(2,14,116,2)		; text window locate.
	TXOPEN(2)
	TXFACE("F045")	;執事・怒り
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	GOTO EVNT053_END

IF ( R_MS_NUMBER = 3 )		;カスティーヨの場合
	TXLC(2,28,260,1)		; text window locate.
	TXOPEN(2)
	TXFACE("F152")		;カスティーヨ・普通（お尋ね者Ｃ）
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	WWIVENT(1,0,"E025")		;Ｈなお尋ね者
	@P
	TXCLOSE(2)

	TXOPEN(3)
	TXGIRL(7)	;娘・苦しい
	「＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXLC(2,28,130,1)		; text window locate.
	TXOPEN(2)
	TXFACE("F152")	;カスティーヨ・普通
	「＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(2)
	MUSIC(6,11);SE11打撃音１
	EFC(2)		; ＷＨＩＴＥ・ＦＬＡＳＨ
	;＊画面振動＋サブリナ
	MUSIC(2)			;音楽ﾌｪｰﾄﾞｱｳﾄ
	TXLC(1,17,160,0)		; text window locate.
	TXOPEN(1)
	「＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	TXCLOSE(1)

	TXLC(2,14,116,2)		; text window locate.
	TXOPEN(2)
	TXFACE("F045")	;執事・怒り
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	@P
	GOTO EVNT053_END

*EVNT053_END	;共通

GR_OPEN				; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN
STRSCLEAR
IGRP_TBL1[0]="色気"
IGRP_TBL3[0]=B_IROKE
IGRP_CNT=1
IGRP_X=28
IGRP_Y=240
GRPOPEN(1,1)			; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ

@2
TXFACE("F042")	;執事・心配
@C
「＊＊＊＊＊＊＊＊＊＊＊＊＊」

URA_STEWARD += 20		;執事との関係が２０上昇．
IF ( URA_STEWARD>100 ) URA_STEWARD=100	; 執事との関係
B_IROKE += 25			;色気が２５上昇．

GRPUPD(1,0,B_IROKE)
@P
GR_CLOSE				; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ CLOSE

TXCLOSE(2)
TXCLOSE(3)
WWIVENT(2,0)			; ivent window close.
RET

;
;	end of "EVNT029.TXT"
;
