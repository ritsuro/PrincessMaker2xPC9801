; "JOB013.TXT"

.S_STARTDAY		; スケジュール開始日
.S_ENDDAY		; スケジュール終了日

.GG_MAX
.GG_STR[6]
.GG_VAL[6]
.GG_GOLD_HDL

.DAY_COUNTER	=0	; 日付カウンタ
.NOW_COUNT		=0	; 実行回数

.FLAG_UP		=0	; 待遇アップフラグ
.GUARA_BASE		=0	; 基本給
.GUARA_ONE		=0	; バイト料
.GUARA_ALL		=0	; バイト料合計
.WORK_DAYS		=0	; 仕事日数
.SUCCESS_DAYS	=0	; 成功日数
.SUCCESS_LINE	=0	; 成功基準値
.SUCCESS_LINE2	=0	; 成功基準値
.SUCCESS_FLAG	=0	; 成功フラグ

.FLAG_ALLOPEN=0

.A_TEXT_HDL=6		;アニメ中テキストハンドル

.SLCANM
.TIMELOP
.TIMELOPMAX
.TIMEWAIT1
.GIRX
.GIRY
.ALOCX[14]
.ALOCY[14]
.ALOCC[14]
.ALOCF[14]
.ALOCCNT[14]
.AFWORK=0
.AFWORKCNT=0
.AFMISS=0
.AFMISSCNT=0
.TITLES="ヤミ酒場"

.SLCANM_OLD=999

ANIME_INIT

S_STARTDAY	= SCH_STARTDAY[SCH_NOW]		; スケジュール開始日
S_ENDDAY	= SCH_ENDDAY[SCH_NOW]		; スケジュール終了日

SCH_NOW++		; スケジュール実行中項目

R_DARKBAR		; 13=ヤミ酒場

PTBK(0)
LOAD("RUNNING")


;***************************************
;
;		ヤミ酒場
;
;***************************************

*R_DARKBAR		; 13=ヤミ酒場

TXOPEN(1)
TXA(NAME_FIRST)は今日から，
裏通りのヤミ酒場でアルバイトです．
@P
TXCLOSE(1)

TXOPEN(2)
TXFACE("F072")

GUARA_BASE	=45						; 基本給
GUARA_ALL	=0						; バイト料合計
WORK_DAYS	=0						; 仕事日数
SUCCESS_DAYS=0						; 成功日数

CK_GUARA(RUN_ARBEIT[14],GUARA_BASE)	; バイト料
FLAG_UP		= AX					; 待遇アップ
GUARA_ONE	= DX					; バイト料

支配人サバ

RUN_ARBEIT[30]++	;「ヤミ酒場」訪問回数

IF ( GRA_ARBEIT[14] = FLAG_UP )		; 待遇

	IF ( RUN_ARBEIT[30]=1 )		;１回目
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
		@P
		@C
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」

	IF ( RUN_ARBEIT[30]>1 )		;２回目以降
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」

IF ( GRA_ARBEIT[14] ! FLAG_UP )		; 待遇

	GRA_ARBEIT[14] = FLAG_UP		; 待遇

	IF ( FLAG_UP = 1 )				; 待遇アップ１回目
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊TXGOLD(GUARA_ONE)＊＊＊＊＊＊＊＊＊＊」

	IF ( FLAG_UP ! 1 )				; 待遇アップ２回目以降
		「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊TXGOLD(GUARA_ONE)＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
@P
TXCLOSE(2)

ALL_OPEN
DAY_COUNTER	= S_ENDDAY - S_STARTDAY + 1	; 日付カウンタ

	TXF(11)

	IF ( VAL_WEEK!0 )					; Sunday

		B_IROKE		+=	2				; 色気

		B_INGYO		+=	2				; 因業

		B_MORALS	-=	3				; モラル
		IF ( B_MORALS < 0 ) B_MORALS=0	; モラル

		B_SHINKOH	-=	3				; 信仰
		IF ( B_SHINKOH < 0 ) B_SHINKOH=0 ; 信仰

		RANDAM(2) AX--
		T_KIDATE	-= AX				; 気だて
		IF ( T_KIDATE < 0 ) T_KIDATE=0

		URA_FATHER--					; 父との関係
		IF ( URA_FATHER < 0 ) URA_FATHER=0

		B_STRESS	+=	12				; ストレス

		CLENDER(13)	;訓練技能値計算

		WORK_DAYS++						; 仕事日数
		TXCLR TXA(TITLES)・TXPOINT(WORK_DAYS)日目

		IRAND999X(B_STRESS)				; ストレス
		SUCCESS_LINE= B_IROKE - IRND	; 色気

		CHK_RENAI
		CHK_HIKOUKA

		IF ( SUCCESS_LINE >= 70 )		; 色気７０ 成功
			今日は無事仕事を済ませた・・・
			SUCCESS_DAYS++				; 成功日数
			GUARA_ALL += GUARA_ONE		; バイト料合計
			GXXDGOLD
			SUCCESS_FLAG = 1			; 成功フラグ
			ANIME_WIDW					;

		IF ( SUCCESS_LINE < 70 )		; 色気７０ 失敗
			IF ( S_HIKOUKA < 10 )
				今日はお客とトラブルがあった．
			IF ( S_HIKOUKA >= 10 )
				TXA(NAME_FIRST)は仕事をサボっている・・・
			SUCCESS_FLAG = 0			; 成功フラグ
			ANIME_WIDW					;

		GXXD(0,B_IROKE)					; 色気
		GXXD(1,B_INGYO)					; 因業
		GXXD(2,B_MORALS)				; モラル
		GXXD(3,B_SHINKOH)				; 信仰
		GXXD(4,V_KIDATE)				; 気だて
		GXXD(5,B_STRESS)				; ストレス

	IF ( VAL_WEEK=0 )				; Sunday
		@C
		今日は日曜日です．
		ANMTSUNDAY

	TXF(10)

	BORTH_CHECK

	IF ( DAY_COUNTER ! 1 )
		CLENDER(3)					; カレンダー・更新処理
		CLENDER(2)					; カレンダー・再表示
LOOP DAY_COUNTER
ALL_CLOSE

RUN_ARBEIT[0]++					;「娘を働かせる」実行回数
IF ( SUCCESS_DAYS>0 )			; 成功日数
	RUN_ARBEIT[14]++			;「ヤミ酒場」実行回数

TXOPEN(2)
TXFACE("F072")

IF ( SUCCESS_DAYS = WORK_DAYS )		; バイト終了時（パーフェクト）
	P1X100(GUARA_ALL,50)
	GUARA_ALL += AX
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊TXPOINT(SUCCESS_DAYS)＊＊TXGOLD(GUARA_ALL)＊＊＊＊＊＊＊」
	GOLDADD(GUARA_ALL)			; バイト料合計

IF ( SUCCESS_DAYS < WORK_DAYS   SUCCESS_DAYS > 0 );（失敗があった場合）
	「TXPOINT(SUCCESS_DAYS)＊＊＊TXGOLD(GUARA_ALL)＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
	GOLDADD(GUARA_ALL)			; バイト料合計

IF ( SUCCESS_DAYS = 0 )			; バイト終了時（全滅）
	「＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊」
WWPROF(3)					; ﾌﾟﾛﾌｨｰﾙ･ｳｲﾝﾄﾞｳ・所持金
@P
TXCLOSE(2)

RET

*ALL_OPEN
ANIME_OPEN
STRSCLEAR
GG_STR[0]="色気"
GG_VAL[0]=B_IROKE	
GG_STR[1]="因業"
GG_VAL[1]=B_INGYO	
GG_STR[2]="モラル"
GG_VAL[2]=B_MORALS
GG_STR[3]="信仰"
GG_VAL[3]=B_SHINKOH
GG_STR[4]="気だて"
GG_VAL[4]=V_KIDATE
GG_STR[5]="ストレス"
GG_VAL[5]=B_STRESS
GG_MAX=6
GXXSTART
FLAG_ALLOPEN=1
RET

*ALL_CLOSE
IF ( FLAG_ALLOPEN=1 )
	FLAG_ALLOPEN=0
	GR_CLOSE						; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ CLOSE
	ANIME_CLOSE
RET

; ******************
;	lib.
; ******************

*GXXSTART
;PTBK(17)			; パーツバンク設定
;PTCLR				; パーツバッファクリア
;PTGT(4,140,20,35)	; パーツ座標ＰＵＴ
GR_F(1,23)			; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ OPEN  PROCWRK_TOP #23 52,200byte
C3 = 0
C2 = 0
IGRP_X=4
IGRP_Y=340
IGRP_CNT=0
C1 = GG_MAX
	IGRP_TBL1[IGRP_CNT]=GG_STR[C2]
	IGRP_TBL3[IGRP_CNT]=GG_VAL[C2]
	IGRP_CNT++ C2++
	IF ( IGRP_CNT = 2 )
		C3++ GRPOPEN(1,C3)		; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
		IGRP_CNT = 0
		IGRP_X += 24
LOOP C1
IF ( IGRP_CNT ! 0 )
	C3++ GRPOPEN(1,C3)		; ﾊﾟﾗﾒｰﾀ･ｳｲﾝﾄﾞｳ
IGRP_TBL1[0]="もうけ"
IGRP_TBL3[0]=0
IGRP_CNT=1
IGRP_X=4
IGRP_Y=140
C3++ GOLDOPEN(1,C3)			; ﾊﾟﾗﾒｰﾀ 所持金ｳｲﾝﾄﾞｳ
GG_GOLD_HDL=C3
RET

*GXXD
C3 = AX
C4 = BX
MOD( C3, 2 )	C5 = AX		; 余り
C6 = C3 / 2 + 1				; handle.
GRPUPD(C6,C5,C4)			;
RET

*GXXDGOLD
GOLDUPD(GG_GOLD_HDL,0,GUARA_ALL)
RET

; ******************
;	Txxx
; ******************

*ANIME_INIT
TIMEWAIT1=8		;8;6
TIMELOPMAX=5	;5;12

;仕事中１
ALOCX[0]	=264/8
ALOCY[0]	=113-79
ALOCC[0]	=0
ALOCCNT[0]	=12
.FILM0[12]=1,1,2,2,1, 1,2,2,2,1, 1,1

;仕事ミス・病気１
ALOCX[2]	=264/8
ALOCY[2]	=113-79
ALOCC[2]	=0
ALOCCNT[2]	=15
.FILM2[15]=2,2,3,4,3, 4,7,8,7,8, 5,6,5,6,5

;サボリ
ALOCX[4]	=272/8
ALOCY[4]	=113-79
ALOCC[4]	=0
ALOCCNT[4]	=10
.FILM4[10]=9,10,10,10,10, 11,12,12,12,12

;客１
ALOCX[5]	=240/8
ALOCY[5]	=113-79
ALOCC[5]	=0
ALOCCNT[5]	=13
.FILM5[13]=13,13,13,14,14, 14,14,14,14,13, 13,14,13

;客１(ミス１連動)
ALOCX[6]	=240/8
ALOCY[6]	=113-79
ALOCC[6]	=0
ALOCCNT[6]	=15
.FILM6[15]=15,16,15,16,15, 16,15,16,16,16, 16,16,16,16,16

;ホステス１
ALOCX[7]	=208/8
ALOCY[7]	=113-79
ALOCC[7]	=0
ALOCCNT[7]	=10
.FILM7[10]= 17,18,17,18,17, 18,19,20,19,20
;ホステス２
ALOCX[8]	=120/8
ALOCY[8]	=113-79
ALOCC[8]	=0
ALOCCNT[8]	=12
.FILM8[12]= 23,23,24,23,21, 22,21,22,21,22, 21,22
;ホステス３
ALOCX[9]	=64/8
ALOCY[9]	=113-79
ALOCC[9]	=0
ALOCCNT[9]	=11
.FILM9[11]= 27,28,27,27,29, 29,30,31,31,32, 31
;客２(シンタク)
ALOCX[10]	=96/8
ALOCY[10]	=113-79
ALOCC[10]	=0
ALOCCNT[10]	=6
.FILM10[6]= 25,25,25,25,26, 26
;マスター
ALOCX[11]	=0/8
ALOCY[11]	=113-79
ALOCC[11]	=0
ALOCCNT[11]	=9
.FILM11[9]= 33,33, 33,33, 33,33, 34,34,34
;用心棒
ALOCX[12]	=168/8
ALOCY[12]	=0
ALOCC[12]	=0
ALOCCNT[12]	=23
.FILM12[23]= 35,36,35,36,35, 36,35,36,35,36, 35,36,35,36,35, 36,35,36,35,36, 37,37,37

;ローソク
ALOCX[13]	=56/8
ALOCY[13]	=0
ALOCC[13]	=0
ALOCCNT[13]	=2
.FILM13[2]= 38,39
RET


*ANIME_OPEN
WWANIME(1,0)			; anime window open.
WWANIME(3,0,"J014B")	; pictuer load.
WWANIME(6,0,"J014A")	; anime pattern add load.
WWANIME(4,0)			; put BG.
WWANIME(5,0)			; pictuer copy vvram -> CRT.
TXOPEN(A_TEXT_HDL)
RET

*ANIME_CLOSE
WWANIME(2,0)		: anime window close.
TXCLOSE(A_TEXT_HDL)
RET

*ANIME_WIDW
IF ( SUCCESS_FLAG ! 0 )		; 成功フラグ
	ANMT001(0)				; 0=仕事中１
	GOTO ANIME_WIDW_SHOW
IF ( S_HIKOUKA >= 10 )		; 失敗時（非行化指数１０以上）
	ANMT001(4)				; 4=サボリ
	GOTO ANIME_WIDW_SHOW
ANMT001(2)					; 2=仕事ミス１
*ANIME_WIDW_SHOW
RET


*ANMT001
; AX :	0=仕事中１,1=仕事中２,2=仕事ミス１,3=仕事ミス２,4=サボリ
SLCANM = AX
IF ( SLCANM = 2  SLCANM_OLD ! 2 )
	ALOCC[2] = 0

TIMELOP=TIMELOPMAX
	WWANIME(4,0)		; put BG.
	TIMER1(TIMEWAIT1)

	ANIM_NUM(13)	APUT(ALOCX[13],ALOCY[13],DX)	ALOCC[13]++

	IF ( SLCANM ! 2 )
		ANIM_NUM(5)	APUT(ALOCX[5],ALOCY[5],DX)
		ALOCC[5]++
	IF ( SLCANM = 2 )
		ANIM_NUM(6)	APUT(ALOCX[6],ALOCY[6],DX)
		ALOCC[6]++

	ANIM_NUM(10)	APUT(ALOCX[10],ALOCY[10],DX)	ALOCC[10]++

	ANIM_NUM(7)		APUT(ALOCX[7],ALOCY[7],DX)		ALOCC[7]++
	ANIM_NUM(8)		APUT(ALOCX[8],ALOCY[8],DX)		ALOCC[8]++
	ANIM_NUM(9)		APUT(ALOCX[9],ALOCY[9],DX)		ALOCC[9]++

	ANIM_NUM(11)	APUT(ALOCX[11],ALOCY[11],DX)	ALOCC[11]++
	ANIM_NUM(12)	APUT(ALOCX[12],ALOCY[12],DX)	ALOCC[12]++

	ANIM_NUM(SLCANM)	APUT(ALOCX[SLCANM],ALOCY[SLCANM],DX)
	ALOCC[SLCANM]++

	WAIT1
	WWANIME(5,0)		; pictuer copy vvram -> CRT.
LOOP TIMELOP
SLCANM_OLD = SLCANM
RET

*ANMTSUNDAY
TIMELOP=TIMELOPMAX
	WWANIME(4,0)		; put BG.
	TIMER1(TIMEWAIT1)
	WAIT1
	WWANIME(5,0)		; pictuer copy vvram -> CRT.
LOOP TIMELOP
RET

*ANIM_NUM
;ANIM_NUM(filmnumber)
;-O- DX pattern number.
IF ( ALOCC[AX] >= ALOCCNT[AX] )
	ALOCC[AX] = 0
BX = ALOCC[AX]
IF (AX=0) DX=FILM0[BX]
IF (AX=2) DX=FILM2[BX]
IF (AX=4) DX=FILM4[BX]
IF (AX=5) DX=FILM5[BX]
IF (AX=6) DX=FILM6[BX]
IF (AX=7) DX=FILM7[BX]
IF (AX=8) DX=FILM8[BX]
IF (AX=9) DX=FILM9[BX]
IF (AX=10) DX=FILM10[BX]
IF (AX=11) DX=FILM11[BX]
IF (AX=12) DX=FILM12[BX]
IF (AX=13) DX=FILM13[BX]
DX--	; DX pattern number.
RET

*APUT
; anime pattern put vvram.
; APUT(x,y,pattern)
IF ( CX < 0 ) RET
GIRX=AX
GIRY=BX
SI = CX * 2 + 1
DX = BX
CX = AX
AX = 7
BX = 0
;	WWANIME(7,0,ALOCX[0],ALOCY[0],ALOCC[0])
WWANIME	; anime pattern put vvram.
RET


;************************************
;	誕生日チェック
;************************************

*BORTH_CHECK

IF ( P_BORTHMONTH = VAL_MONTH  P_BORTHDAY = VAL_DAY )			; 娘の誕生日
	IF ( P_BORTHMONTH[1] = VAL_MONTH  P_BORTHDAY[1] = VAL_DAY )	; 父の誕生日
		E_ETC[14]=P_NENREI[1]			;「父の誕生日」をチェックした年齢
		IF ( URA_FATHER >=60 )			;父との関係値が６０以上
			ALL_CLOSE
			EVE4243						;娘の誕生日と父の誕生日の合体
			IF ( DAY_COUNTER ! 1 )
				ALL_OPEN
			RET

IF ( P_BORTHMONTH = VAL_MONTH )			; 誕生月
	IF ( P_BORTHDAY = VAL_DAY )			; 誕生日
		ALL_CLOSE
		EVENT042						; 42 娘の誕生日
		IF ( DAY_COUNTER ! 1 )
			ALL_OPEN
		RET

IF ( P_BORTHMONTH[1] = VAL_MONTH )		; 父の誕生月
	IF ( P_BORTHDAY[1] = VAL_DAY )		; 父の誕生日
		E_ETC[14]=P_NENREI[1]			;「父の誕生日」をチェックした年齢
		IF ( URA_FATHER >=60 )			;父との関係値が６０以上
			ALL_CLOSE
			EVENT043					; 43 父の誕生日
			IF ( DAY_COUNTER ! 1 )
				ALL_OPEN
			RET
RET

*CHK_RENAI
IF ( FLAG_RENAI!0 )		;状態・恋愛
	RANDAM(100)			;２０％の確率
	IF ( IRND<=20 )
		SUCCESS_LINE=0
RET

*CHK_HIKOUKA
IF ( S_HIKOUKA >= 10 S_HIKOUKA < 30 )	;状態・やや反抗的
	RANDAM(100)							;４０％の確率
	IF ( IRND<=40 )
		SUCCESS_LINE=0
	RET

IF ( S_HIKOUKA >= 30 )			;状態・反抗的
	RANDAM(100)					;８０％の確率
	IF ( IRND<=80 )
		SUCCESS_LINE=0
	RET
RET
;
;	end of "JOB013.TXT"
;
